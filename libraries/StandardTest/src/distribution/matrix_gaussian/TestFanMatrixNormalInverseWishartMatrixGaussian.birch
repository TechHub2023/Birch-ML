class TestFanMatrixNormalInverseWishartMatrixGaussian < TestModel {
  Σ:Random<Real[_,_]>;
  X:Random<Real[_,_]>;
  Y1:Random<Real[_,_]>;
  Y2:Random<Real[_,_]>;
  Y3:Random<Real[_,_]>;
  Y4:Random<Real[_,_]>;
  Y5:Random<Real[_,_]>;

  n:Integer <- 5;
  p:Integer <- 4;

  k:Real;
  Ψ:Real[p,p];
  M:Real[n,p];
  U:Real[n,n];
  V:Real[n,n];

  override function initialize() {
    k <- simulate_uniform(2.0, 10.0);
    Ψ <- matrix_lambda(\(i:Integer, j:Integer) -> { return simulate_uniform(-2.0, 2.0); }, p, p);
    M <- matrix_lambda(\(i:Integer, j:Integer) -> { return simulate_uniform(-10.0, 10.0); }, n, p);
    U <- matrix_lambda(\(i:Integer, j:Integer) -> { return simulate_uniform(-2.0, 2.0); }, n, n);
    V <- matrix_lambda(\(i:Integer, j:Integer) -> { return simulate_uniform(-2.0, 2.0); }, n, n);

    Ψ <- outer(Ψ, Ψ) + diagonal(1.0e-2, p);
    U <- outer(U, U) + diagonal(1.0e-2, n);
    V <- outer(V, V) + diagonal(1.0e-2, n);
  }

  override function simulate() {
    Σ ~ InverseWishart(Ψ, k);
    X ~ MatrixGaussian(M, U, Σ);
    Y1 ~ MatrixGaussian(X, V, Σ);
    Y2 ~ MatrixGaussian(X, V, Σ);
    Y3 ~ MatrixGaussian(X, V, Σ);
    Y4 ~ MatrixGaussian(X, V, Σ);
    Y5 ~ MatrixGaussian(X, V, Σ);
  }

  override function forward() -> Real[_] {
    Σ.peek();
    X.peek();
    Y1.peek();
    Y2.peek();
    Y3.peek();
    Y4.peek();
    Y5.peek();
    return vectorize();
  }

  override function backward() -> Real[_] {
    assert !Y5.hasValue();
    Y5.peek();
    Y4.peek();
    Y3.peek();
    Y2.peek();
    Y1.peek();
    assert !X.hasValue();
    X.peek();
    assert !Σ.hasValue();
    Σ.peek();
    return vectorize();
  }

  override function backwardGrad() {
    assert !Y5.hasValue();
    Y5.peek();
    Y4.peek();
    Y3.peek();
    Y2.peek();
    Y1.peek();
    assert !X.hasValue();
    X.peek();
    assert !Σ.hasValue();
    // ^ currently can't do finite difference gradient estimates on symmetric
    //   matrices
  }

  function marginal() -> Distribution<Real[_,_]> {
    return Y5.getDistribution();
  }

  function vectorize() -> Real[_] {
    z:Real[size()];
    let k <- 0;
    z[k + 1.. k + p*p] <- vec(Σ.peek());
    k <- k + p*p;
    z[k + 1.. k + n*p] <- vec(X.peek());
    k <- k + n*p;
    z[k + 1.. k + n*p] <- vec(Y1.peek());
    k <- k + n*p;
    z[k + 1.. k + n*p] <- vec(Y2.peek());
    k <- k + n*p;
    z[k + 1.. k + n*p] <- vec(Y3.peek());
    k <- k + n*p;
    z[k + 1.. k + n*p] <- vec(Y4.peek());
    k <- k + n*p;
    z[k + 1.. k + n*p] <- vec(Y5.peek());
    return z;
  }

  override function size() -> Integer {
    return p*p + 6*n*p;
  }
}

program test_conjugacy_fan_matrix_normal_inverse_wishart_matrix_gaussian(
    N:Integer <- 10000, lazy:Boolean <- false) {
  m:TestFanMatrixNormalInverseWishartMatrixGaussian;
  test_conjugacy(m, N, lazy);
}

program test_pdf_fan_matrix_normal_inverse_wishart_matrix_gaussian(
    N:Integer <- 10000, B:Integer <- 1000, S:Integer <- 20,
    lazy:Boolean <- false) {
  m:TestFanMatrixNormalInverseWishartMatrixGaussian;
  m.initialize();
  m.simulate();
  test_pdf(m.marginal(), N, B, S, lazy);
}

program test_grad_fan_matrix_normal_inverse_wishart_matrix_gaussian(
    N:Integer <- 1000, backward:Boolean <- false) {
  m:TestFanMatrixNormalInverseWishartMatrixGaussian;
  if !backward {
    warn("forward order not supported, switching to backward order");
  }
  test_grad(m, N, true);
}
