class TestGammaExponential < TestModel {
  k:Random<Real>;
  θ:Random<Real>;
  λ:Random<Real>;
  x:Random<Real>;

  override function initialize() {
    k ~ Uniform(2.0, 10.0);
    θ ~ Uniform(0.1, 2.0);
  }

  override function simulate() {
    λ ~ Gamma(k, θ);
    x ~ Exponential(λ);
  }

  override function forward() -> Real[_] {
    y:Real[2];
    y[1] <- λ.eval();
    y[2] <- x.eval();
    return y;
  }

  override function backward() -> Real[_] {
    y:Real[2];
    assert !x.hasValue();
    y[2] <- x.eval();
    assert !λ.hasValue();
    y[1] <- λ.eval();
    return y;
  }

  function marginal() -> Distribution<Real> {
    return x.getDistribution();
  }

  override function size() -> Integer {
    return 2;
  }
}

program test_conjugacy_gamma_exponential(N:Integer <- 10000,
    lazy:Boolean <- false) {
  m:TestGammaExponential;
  test_conjugacy(m, N, lazy);
}

program test_pdf_gamma_exponential(N:Integer <- 10000,
    S:Integer <- 20, lazy:Boolean <- false) {
  m:TestGammaExponential;
  m.initialize();
  m.simulate();
  test_pdf(m.marginal(), N, S, lazy);
}

program test_cdf_gamma_exponential(N:Integer <- 10000) {
  m:TestGammaExponential;
  m.initialize();
  m.simulate();
  test_cdf(m.marginal(), N);
}

program test_grad_gamma_exponential(N:Integer <- 1000,
    backward:Boolean <- false) {
  m:TestGammaExponential;
  test_grad(m, N, backward);
}
