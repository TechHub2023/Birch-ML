class TestGammaPoisson < TestModel {
  λ:Random<Real>;
  x:Random<Integer>;
  k:Random<Real>;
  θ:Random<Real>;

  override function initialize() {
    k ~ Uniform(1.0, 10.0);
    θ ~ Uniform(0.1, 10.0);
  }

  override function simulate() {
    λ ~ Gamma(k, θ);
    x ~ Poisson(λ);
  }

  override function forward() -> Real[_] {
    y:Real[2];
    y[1] <- λ.peek();
    y[2] <- x.peek();
    return y;
  }

  override function backward() -> Real[_] {
    y:Real[2];
    assert !x.hasValue();
    y[2] <- x.peek();
    assert !λ.hasValue();
    y[1] <- λ.peek();
    return y;
  }

  function marginal() -> Distribution<Integer> {
    return x.getDistribution();
  }

  override function size() -> Integer {
    return 2;
  }
}

program test_conjugacy_gamma_poisson(N:Integer <- 10000, lazy:Boolean <- false) {
  m:TestGammaPoisson;
  test_conjugacy(m, N, lazy);
}

program test_pdf_gamma_poisson(N:Integer <- 10000, B:Integer <- 0,
    S:Integer <- 0, lazy:Boolean <- false) {
  m:TestGammaPoisson;
  m.initialize();
  m.simulate();
  test_pdf(m.marginal(), N, lazy);
}

program test_cdf_gamma_poisson() {
  m:TestGammaPoisson;
  m.initialize();
  m.simulate();
  test_cdf(m.marginal());
}

program test_grad_gamma_poisson(N:Integer <- 1000,
    backward:Boolean <- false) {
  m:TestGammaPoisson;
  test_grad(m, N, backward);
}
