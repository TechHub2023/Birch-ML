class TestScaledGammaExponential < TestModel {
  λ:Random<Real>;
  x:Random<Real>;

  a:Random<Real>;
  k:Random<Real>;
  θ:Random<Real>;

  override function initialize() {
    a ~ Uniform(0.1, 10.0);
    k ~ Uniform(2.0, 10.0);
    θ ~ Uniform(0.1, 10.0);
  }

  override function simulate() {
    λ ~ Gamma(k, θ);
    x ~ Exponential(a*λ);
  }

  override function forward() -> Real[_] {
    y:Real[2];
    y[1] <- λ.peek();
    y[2] <- x.peek();
    return y;
  }

  override function backward() -> Real[_] {
    y:Real[2];
    assert !x.hasValue();
    y[2] <- x.peek();
    assert !λ.hasValue();
    y[1] <- λ.peek();
    return y;
  }

  function marginal() -> Distribution<Real> {
    return x.getDistribution();
  }

  override function size() -> Integer {
    return 2;
  }
}

program test_conjugacy_scaled_gamma_exponential(N:Integer <- 10000,
    lazy:Boolean <- false) {
  m:TestScaledGammaExponential;
  test_conjugacy(m, N, lazy);
}

program test_pdf_scaled_gamma_exponential(N:Integer <- 10000,
    B:Integer <- 100, S:Integer <- 5, lazy:Boolean <- false) {
  m:TestScaledGammaExponential;
  m.initialize();
  m.simulate();
  test_pdf(m.marginal(), N, B, S, lazy);
}

program test_cdf_scaled_gamma_exponential(N:Integer <- 10000) {
  m:TestScaledGammaExponential;
  m.initialize();
  m.simulate();
  test_cdf(m.marginal(), N);
}

program test_grad_scaled_gamma_exponential(N:Integer <- 1000,
    backward:Boolean <- false) {
  m:TestScaledGammaExponential;
  test_grad(m, N, backward);
}
