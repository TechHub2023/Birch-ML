class TestPoisson < TestModel {
  x:Random<Integer>;
  λ:Random<Real>;

  override function initialize() {
    λ ~ Uniform(0.1, 100.0);
  }

  override function simulate() {
    x ~ Poisson(λ);
  }

  override function forward() -> Real[_] {
    return vector(x.eval(), 1);
  }

  override function backward() -> Real[_] {
    return vector(x.eval(), 1);
  }

  function marginal() -> Distribution<Integer> {
    return x.getDistribution();
  }

  override function size() -> Integer {
    return 1;
  }
}

program test_pdf_poisson(N:Integer <- 10000, B:Integer <- 0, S:Integer <- 0,
    lazy:Boolean <- false) {
  m:TestPoisson;
  m.initialize();
  m.simulate();
  test_pdf(m.marginal(), N, lazy);
}

program test_cdf_poisson() {
  m:TestPoisson;
  m.initialize();
  m.simulate();
  test_cdf(m.marginal());
}

program test_grad_poisson(N:Integer <- 1000, backward:Boolean <- false) {
  m:TestPoisson;
  test_grad(m, N, backward);
}
