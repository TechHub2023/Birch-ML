class TestFanMultivariateNormalInverseGammaMultivariateGaussian < TestModel {
  σ2:Random<Real>;
  μ:Random<Real[_]>;
  x1:Random<Real[_]>;
  x2:Random<Real[_]>;
  x3:Random<Real[_]>;
  x4:Random<Real[_]>;
  x5:Random<Real[_]>;

  n:Integer <- 4;

  α:Real;
  β:Real;
  μ_0:Real[n];
  Σ:Real[n,n];
  Ψ:Real[n,n];

  override function initialize() {
    α <- simulate_uniform(3.0, 10.0);
    β <- simulate_uniform(0.1, 10.0);
    μ_0 <- vector_lambda(\(i:Integer) -> { return simulate_uniform(-10.0, 10.0); }, n);
    Σ <- matrix_lambda(\(i:Integer, j:Integer) -> { return simulate_uniform(-2.0, 2.0); }, n, n);
    Ψ <- matrix_lambda(\(i:Integer, j:Integer) -> { return simulate_uniform(-2.0, 2.0); }, n, n);

    Σ <- outer(Σ, Σ) + diagonal(1.0e-2, n);
    Ψ <- outer(Ψ, Ψ) + diagonal(1.0e-2, n);
  }

  override function simulate() {
    σ2 ~ InverseGamma(α, β);
    μ ~ MultivariateGaussian(μ_0, Σ*σ2);
    x1 ~ MultivariateGaussian(μ, Ψ*σ2);
    x2 ~ MultivariateGaussian(μ, Ψ*σ2);
    x3 ~ MultivariateGaussian(μ, Ψ*σ2);
    x4 ~ MultivariateGaussian(μ, Ψ*σ2);
    x5 ~ MultivariateGaussian(μ, Ψ*σ2);
  }

  override function forward() -> Real[_] {
    σ2.eval();
		μ.eval();
		x1.eval();
		x2.eval();
		x3.eval();
		x4.eval();
		x5.eval();
    return vectorize();
  }

  override function backward() -> Real[_] {
    assert !x5.hasValue();
    x5.eval();
		x4.eval();
		x3.eval();
		x2.eval();
		x1.eval();
    assert !μ.hasValue();
    μ.eval();
    assert !σ2.hasValue();
    σ2.eval();
    return vectorize();
  }

  function marginal() -> Distribution<Real[_]> {
    return x5.getDistribution();
  }

  function vectorize() -> Real[_] {
    y:Real[size()];
		let k <- 1;
    y[k] <- σ2.value();
		k <- k + 1;
    y[k..(k + n - 1)] <- μ.eval();
		k <- k + n;
    y[k..(k + n - 1)] <- x1.eval();
		k <- k + n;
    y[k..(k + n - 1)] <- x2.eval();
		k <- k + n;
    y[k..(k + n - 1)] <- x3.eval();
		k <- k + n;
    y[k..(k + n - 1)] <- x4.eval();
		k <- k + n;
    y[k..(k + n - 1)] <- x5.eval();
    return y;
  }

  override function size() -> Integer {
    return 6*n + 1;
  }
}

program test_conjugacy_fan_multivariate_normal_inverse_gamma_multivariate_gaussian(
    N:Integer <- 10000, lazy:Boolean <- false) {
  m:TestFanMultivariateNormalInverseGammaMultivariateGaussian;
  test_conjugacy(m, N, lazy);
}

program test_pdf_fan_multivariate_normal_inverse_gamma_multivariate_gaussian(
    N:Integer <- 10000, S:Integer <- 100,
    lazy:Boolean <- false) {
  m:TestFanMultivariateNormalInverseGammaMultivariateGaussian;
  m.initialize();
  m.simulate();
  test_pdf(m.marginal(), N, S, lazy);
}

program test_grad_fan_multivariate_normal_inverse_gamma_multivariate_gaussian(
    N:Integer <- 1000, backward:Boolean <- false) {
  m:TestFanMultivariateNormalInverseGammaMultivariateGaussian;
  test_grad(m, N, backward);
}
