class TestLinearMatrixNormalInverseWishartMultivariateGaussian < TestModel {
  V:Random<Real[_,_]>;
  X:Random<Real[_,_]>;
  y:Random<Real[_]>;

  n:Integer <- 5;
  p:Integer <- 4;
  
  a:Real[n];
  M:Real[n,p];
  U:Real[n,n];
  c:Real[p];
  k:Real;
  Ψ:Real[p,p];

  override function initialize() {
    k <- simulate_uniform(2.0, 10.0);
    for i in 1..n {
      a[i] <- simulate_uniform(-2.0, 2.0);
      for j in 1..n {
        U[i,j] <- simulate_uniform(-2.0, 2.0);
      }
      for j in 1..p {
        M[i,j] <- simulate_uniform(-10.0, 10.0);
      }
    }
    for i in 1..p {
      c[i] <- simulate_uniform(-10.0, 10.0);
      for j in 1..p {
        Ψ[i,j] <- simulate_uniform(-10.0, 10.0);
      }
    }
    U <- outer(U, U) + diagonal(1.0e-2, n);
    Ψ <- outer(Ψ, Ψ) + diagonal(1.0e-2, p);
  }

  override function simulate() {
    V ~ InverseWishart(Ψ, k);
    X ~ MatrixGaussian(M, U, V);
    y ~ MultivariateGaussian(dot(a, X) + c, V);
  }

  override function forward() -> Real[_] {
    V.peek();
    X.peek();
    y.peek();
    return vectorize();
  }

  override function backward() -> Real[_] {
    assert !y.hasValue();
    y.peek();
    assert !X.hasValue();
    X.peek();
    assert !V.hasValue();
    V.peek();
    return vectorize();
  }

  override function backwardGrad() {
    assert !y.hasValue();
    y.peek();
    assert !X.hasValue();
    X.peek();
    assert !V.hasValue();
    // ^ currently can't do finite difference gradient estimates on symmetric
    //   matrices
  }

  function marginal() -> Distribution<Real[_]> {
    return y.getDistribution();
  }

  function vectorize() -> Real[_] {
    z:Real[size()];
    let k <- 0;
    z[k + 1.. k + p*p] <- vec(V.peek());
    k <- k + p*p;
    z[k + 1.. k + n*p] <- vec(X.peek());
    k <- k + n*p;
    z[k + 1.. k + p] <- y.peek();
    return z;
  }

  override function size() -> Integer {
    return p*p + n*p + p;
  }
}

program test_conjugacy_linear_matrix_normal_inverse_wishart_multivariate_gaussian(
    N:Integer <- 10000, lazy:Boolean <- false) {
  m:TestLinearMatrixNormalInverseWishartMultivariateGaussian;
  test_conjugacy(m, N, lazy);
}

program test_pdf_linear_matrix_normal_inverse_wishart_multivariate_gaussian(
    N:Integer <- 10000, B:Integer <- 1000, S:Integer <- 20,
    lazy:Boolean <- false) {
  m:TestLinearMatrixNormalInverseWishartMultivariateGaussian;
  m.initialize();
  m.simulate();
  test_pdf(m.marginal(), N, B, S, lazy);
}

program test_grad_linear_matrix_normal_inverse_wishart_multivariate_gaussian(
    N:Integer <- 1000, backward:Boolean <- false) {
  m:TestLinearMatrixNormalInverseWishartMultivariateGaussian;
  if !backward {
    warn("forward order not supported, switching to backward order");
  }
  test_grad(m, N, true);
}
