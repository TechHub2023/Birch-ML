/*
 * Test Array.
 */
program test_basic_array() {
  o:Array<Integer>;
  
  o.pushBack(1);
  o.pushBack(2);
  o.pushBack(4);
  o.pushBack(5);
  if !check_container(o, [1, 2, 4, 5]) {
    exit(1);
  }
  
  o.insert(3, 3);
  if !check_container(o, [1, 2, 3, 4, 5]) {
    exit(1);
  }

  o.erase(4);
  if !check_container(o, [1, 2, 3, 5]) {
    exit(1);
  }
  
  if o.front() != 1 {
    exit(1);
  }
  
  if o.back() != 5 {
    exit(1);
  }

  o.clear();
  if o.size() != 0 || !o.empty() {
    stderr.print("clear failed\n");
    exit(1);
  }
}

function check_container<Container>(o:Container, values:Integer[_]) -> Boolean {
  let result <- true;
  
  /* size */
  if o.size() != length(values) {
    stderr.print("incorrect total size\n");
    result <- false;
  }
  
  /* contents */
  for i in 1..o.size() {
    if o.get(i) != values[i] {
      stderr.print("incorrect value\n");
      result <- false;
    }
  }

  /* iterator */
  let iter <- o.walk();
  let i <- 0;
  while iter.hasNext() {
    i <- i + 1;
    if iter.next() != values[i] {
      stderr.print("incorrect value\n");
      result <- false;
    }
  }

  return result;
}
