/**
 * Particle filter.
 *
 * ```mermaid
 * classDiagram
 *    ParticleFilter <|-- AliveParticleFilter
 *    link ParticleFilter "../ParticleFilter/"
 *    link AliveParticleFilter "../AliveParticleFilter/"
 * ```
 */
class ParticleFilter {
  /**
   * Particles.
   */
  x:Array<Particle>;

  /**
   * Log weights.
   */
  w:Real[_];

  /**
   * Logarithm of sum of weights.
   */
  lsum:Real <- 0.0;

  /**
   * Effective sample size.
   */
  ess:Real <- 0.0;

  /**
   * Log normalizing constant.
   */
  lnormalize:Real <- 0.0;

  /**
   * Number of propagations. This is not the same as the number of particles;
   * the number of propagations performed may, according to the filter type,
   * differ from the number of particles, such as for alive and rejection
   * control particle filters.
   */
  npropagations:Integer <- 0;

  /**
   * Accept rate of moves.
   */
  raccepts:Real <- 0.0/0.0;

  /**
   * Number of particles.
   */
  nparticles:Integer <- 1;

  /**
   * Threshold for resampling. Resampling is performed whenever the
   * effective sample size, as a proportion of `N`, drops below this
   * threshold.
   */
  trigger:Real <- 0.7;

  /**
   * Should delayed sampling be used?
   */
  delayed:Boolean <- true;

  /**
   * Should automatic differentiation be used?
   */
  autodiff:Boolean <- false;

  /**
   * Create a particle of the type required for this filter.
   */
  function particle(model:Model) -> Particle {
    return construct<Particle>(model, delayed, autodiff);
  }

  /**
   * Start filter.
   *
   * - model: Model.
   * - input: Input buffer.
   */
  function filter(model:Model, input:Buffer) {
    x.clear();
    for n in 1..nparticles {
      x.pushBack(particle(global.copy(model)));
    }
    w <- vector(0.0, nparticles);
    ess <- nparticles;
    lsum <- 0.0;
    lnormalize <- 0.0;
    npropagations <- 0;
    raccepts <- 0.0/0.0;
    simulate(input);
  }

  /**
   * Step filter.
   *
   * - t: Step number, beginning at 1.
   * - input: Input buffer.
   */
  function filter(t:Integer, input:Buffer) {
    resample(t);
    simulate(t, input);
  }

  /**
   * Step filter.
   *
   * - t: Step number, beginning at 1.
   * - input: Input buffer.
   * - κ: Markov kernel.
   */
  function filter(t:Integer, input:Buffer, κ:Kernel) {
    resample(t);
    move(t, κ);
    simulate(t, input);
  }

  /**
   * Start particles.
   *
   * - input: Input buffer.
   */
  function simulate(input:Buffer) {
    parallel for n in 1..nparticles {
      let m <- x[n].m;
      let h <- x[n].h;
      with h {
        h.w <- 0.0;
        m.read(input);
        m.simulate();
        w[n] <- w[n] + h.w;
      }
    }
    (ess, lsum) <- resample_reduce(w);
    lnormalize <- lnormalize + lsum - log(nparticles);
    npropagations <- nparticles;
  }

  /**
   * Step particles.
   *
   * - t: Step number.
   * - input: Input buffer.
   */
  function simulate(t:Integer, input:Buffer) {
    parallel for n in 1..nparticles {
      let m <- x[n].m;
      let h <- x[n].h;
      with h {
        h.w <- 0.0;
        m.read(t, input);
        m.simulate(t);
        w[n] <- w[n] + h.w;
      }
    }
    (ess, lsum) <- resample_reduce(w);
    lnormalize <- lnormalize + lsum - log(nparticles);
    npropagations <- nparticles;
  }

  /**
   * Resample particles.
   *
   * - t: Step number.
   */
  function resample(t:Integer) {
    if ess < trigger*nparticles {
      let a <- resample_systematic(w);
      w <- vector(0.0, nparticles);

      /* apply bridge finding to any particle with at least two offspring,
       * using the fact that the ancestor vector is in ascending order */
      dynamic parallel for n in 2..nparticles {
        if a[n] == a[n - 1] && (n <= 2 || a[n] != a[n - 2]) {
          bridge(x[a[n]]);
        }
      }

      /* permute the ancestry vector so that it has the property a[n] == n
       * where n has at least one offspring */
      a <- permute_ancestors(a);

      /* copy particles where we have a[n] != n */
      dynamic parallel for n in 1..nparticles {
        if a[n] != n {
          x[n] <- global.copy(x[a[n]]);
        }
      }
      ess <- nparticles;
      lsum <- log(nparticles);
    } else {
      /* normalize weights to sum to nparticles */
      w <- w - vector(lsum - log(nparticles), nparticles);
    }
    collect();
  }

  /**
   * Move particles.
   *
   * - t: Step number.
   * - κ: Markov kernel.
   */
  function move(t:Integer, κ:Kernel) {
    if ess < trigger*nparticles {
      let α <- vector(0.0, nparticles);  // acceptance rates for each particle
      parallel for n in 1..nparticles {
        α[n] <- κ.apply(t, x[n]);
      }
      raccepts <- sum(α)/nparticles;  // average acceptance rate
    }
    //collect();
  }

  /**
   * Reconfigure particle filter.
   *
   * - delayed: Should delayed sampling be used?
   * - autodiff: Should automatic differentiation be used?
   */
  function reconfigure(delayed:Boolean, autodiff:Boolean) {
    this.delayed <- delayed;
    this.autodiff <- autodiff;

    /* replace event handlers */
    let iter <- x.walk();
    while iter.hasNext() {
      iter.next().reconfigure(delayed, autodiff);
    }
  }

  override function read(buffer:Buffer) {
    nparticles <-? buffer.get<Integer>("nparticles");
    trigger <-? buffer.get<Real>("trigger");
    delayed <-? buffer.get<Boolean>("delayed");
    autodiff <-? buffer.get<Boolean>("autodiff");
  }
}
