/**
 * Conditional particle sampler. 
 *
 * ```mermaid
 * classDiagram
 *    ParticleSampler <|-- ConditionalParticleSampler
 *    ConditionalParticleSampler <|-- ParticleGibbsSampler
 *    link ParticleSampler "../ParticleSampler/"
 *    link ConditionalParticleSampler "../ConditionalParticleSampler/"
 *    link ParticleGibbsSampler "../ParticleGibbsSampler/"
 * ```
 */
class ConditionalParticleSampler < ParticleSampler {
  final override function sample(filter:ParticleFilter, model:Model,
      input:Buffer) {
    let c <- ConditionalParticleFilter?(filter);
    if !c? {
      error("A ConditionalParticleSampler requires a ConditionalParticleFilter.");
    }
    return sample(c!, model, input);
  }

  final override function sample(filter:ParticleFilter, t:Integer,
      input:Buffer) {
    let c <- ConditionalParticleFilter?(filter);
    if !c? {
      error("A ConditionalParticleSampler requires a ConditionalParticleFilter.");
    }
    return sample(c!, t, input);
  }

  final override function draw(filter:ParticleFilter) -> (Model, Real) {
    let c <- ConditionalParticleFilter?(filter);
    if !c? {
      error("A ConditionalParticleSampler requires a ConditionalParticleFilter.");
    }
    return draw(c!);
  }

  /**
   * Start a sample.
   *
   * - filter: Conditional particle filter.
   * - model: Model.
   * - input: Input buffer.
   */
  function sample(filter:ConditionalParticleFilter, model:Model, input:Buffer) {
    filter.filter(model, input);
  }

  /**
   * Step a sample.
   *
   * - filter: Conditional particle filter.
   * - input: Input buffer.
   * - t: The step number, beginning at 1.
   */
  function sample(filter:ConditionalParticleFilter, t:Integer, input:Buffer) {
    filter.filter(t, input);
  }

  /**
   * Draw a sample.
   */
  function draw(filter:ConditionalParticleFilter) -> (Model, Real) {
    return (filter.x[filter.b].m, 0.0);
  }
}
