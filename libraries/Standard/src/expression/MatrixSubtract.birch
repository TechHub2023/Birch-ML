/**
 * Lazy matrix subtract.
 */
final class MatrixSubtract(l:Expression<Real[_,_]>, r:Expression<Real[_,_]>) <
    MatrixBinaryExpression<Expression<Real[_,_]>,Expression<Real[_,_]>,
    Real[_,_]>(l, r) {  
  override function doRows() -> Integer {
    return global.rows(l!);
  }
  
  override function doColumns() -> Integer {
    return global.columns(l!);
  }

  override function doCompute() {
    x <- global.eval(l!) - global.eval(r!);
  }

  override function doGrad(gen:Integer) {
    count_grad(l!, gen, d!);
    count_grad(r!, gen, -d!);
  }

  override function graftLinearMatrixGaussian() ->
      TransformLinearMatrix<MatrixGaussian>? {
    t:TransformLinearMatrix<MatrixGaussian>?;
    if !hasValue() {
      x1:MatrixGaussian?;

      if (t <- graft_linear_matrix_gaussian(l!))? {
        t!.subtract(r!);
      } else if (t <- graft_linear_matrix_gaussian(r!))? {
        t!.negateAndAdd(l!);
      } else if (x1 <- graft_matrix_gaussian(l!))? {
        t <- TransformLinearMatrix<MatrixGaussian>(box(identity(global.rows(x1!))), x1!, -r!);
      } else if (x1 <- graft_matrix_gaussian(r!))? {
        t <- TransformLinearMatrix<MatrixGaussian>(box(diagonal(-1.0, global.rows(x1!))), x1!, l!);
      }
    }
    return t;
  }
  
  override function graftLinearMatrixNormalInverseWishart(compare:Distribution<LLT>) ->
      TransformLinearMatrix<MatrixNormalInverseWishart>? {
    t:TransformLinearMatrix<MatrixNormalInverseWishart>?;
    if !hasValue() {
      x1:MatrixNormalInverseWishart?;

      if (t <- graft_linear_matrix_normal_inverse_wishart(l!, compare))? {
        t!.subtract(r!);
      } else if (t <- graft_linear_matrix_normal_inverse_wishart(r!, compare))? {
        t!.negateAndAdd(l!);
      } else if (x1 <- graft_matrix_normal_inverse_wishart(l!, compare))? {
        t <- TransformLinearMatrix<MatrixNormalInverseWishart>(box(identity(global.rows(x1!))), x1!, -r!);
      } else if (x1 <- graft_matrix_normal_inverse_wishart(r!, compare))? {
        t <- TransformLinearMatrix<MatrixNormalInverseWishart>(box(diagonal(-1.0, global.rows(x1!))), x1!, l!);
      }
    }
    return t;
  }
}

/**
 * Lazy matrix subtract.
 */
operator (l:Expression<Real[_,_]> - r:Expression<Real[_,_]>) ->
    MatrixSubtract {
  assert global.rows(l) == global.rows(r);
  assert global.columns(l) == global.columns(r);
  return construct<MatrixSubtract>(l, r);
}

/**
 * Lazy matrix subtract.
 */
operator (l:Real[_,_] - r:Expression<Real[_,_]>) -> MatrixSubtract {
  return box(l) - r;
}

/**
 * Lazy matrix subtract.
 */
operator (l:Expression<Real[_,_]> - r:Real[_,_]) -> MatrixSubtract {
  return l - box(r);
}
