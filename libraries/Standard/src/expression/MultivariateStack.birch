/**
 * Lazy vector stack.
 */
final class MultivariateStack(l:Expression<Real[_]>, r:Expression<Real[_]>) <
    MultivariateBinaryExpression<Expression<Real[_]>,Expression<Real[_]>,
    Real[_]>(l, r) {  
  override function doRows() -> Integer {
    return global.rows(l!) + global.rows(r!);
  }

  override function doCompute() {
    x <- stack(global.eval(l!), global.eval(r!));
  }

  override function doGrad(gen:Integer) {
    let l1 <- global.length(global.eval(l!));
    let l2 <- global.length(global.eval(r!));
    count_grad(l!, gen, d![1..l1]);
    count_grad(r!, gen, d![(l1 + 1)..(l1 + l2)]);
  }
}

/**
 * Lazy vector stack.
 */
function stack(l:Expression<Real[_]>, r:Expression<Real[_]>) ->
    MultivariateStack {
  return construct<MultivariateStack>(l, r);
}

/**
 * Lazy matrix stack.
 */
function stack(l:Real[_], r:Expression<Real[_]>) -> MultivariateStack {
  return stack(box(l), r);
}

/**
 * Lazy matrix stack.
 */
function stack(l:Expression<Real[_]>, r:Real[_]) -> MultivariateStack {
  return stack(l, box(r));
}
