/**
 * Boxed and split matrix expression.
 *
 * - Value: Result type.
 * - Form: Expression type.
 */
final class MatrixSplitExpression<Value,Form>(x:Value, f:Form) <
    BoxedForm<Value,Form>(x, f) {
  function grad(d:Real, i:Integer, j:Integer) {
    if this.f? {
      if this.visitCount == 0 {
        assert !this.d?;
        this.d <- matrix(0.0, this.rows(), this.columns());
      }
      this.d![i,j] <- this.d![i,j] + d;
      this.visitCount <- this.visitCount + 1;
      if this.visitCount >= this.evalCount {
        assert this.visitCount == this.evalCount || this.evalCount == 0;
        // ^ can have visitCount > evalCount = 0 if eval() has never been
        //   called on this object, typically because it is root expression
        //   used from client code, rather than a subexpression used from
        //   other expressions
        global.grad(this.f!, this.d!);  // upstream gradients accumulated
        this.d <- nil;  // clear intermediate gradients to save memory
        this.visitCount <- 0;  // reset for next time
      }
    }
  }
}

/**
 * Box and split a matrix expression into a matrix of expressions.
 *
 * - f: Form of the expression.
 */
function matrix_split<Form>(f:Form) -> {
  return matrix_split(eval(f), f);
}

/**
 * Box and split a matrix expression into a matrix of expressions.
 *
 * - x: Result of the expression.
 * - f: Form of the expression.
 */
function matrix_split<Value,Form>(x:Value, f:Form) -> {
  return construct<MatrixSplitExpression<Value,Form>>(x, f);
}

function length<Value,Form>(x:MatrixSplitExpression<Value,Form>) -> Integer {
  return x.length();
}

function size<Value,Form>(x:MatrixSplitExpression<Value,Form>) -> Integer {
  return x.size();
}

function rows<Value,Form>(x:MatrixSplitExpression<Value,Form>) -> Integer {
  return x.rows();
}

function columns<Value,Form>(x:MatrixSplitExpression<Value,Form>) -> Integer {
  return x.columns();
}

function link<Value,Form>(x:MatrixSplitExpression<Value,Form>) {
  x.link();
}

function value<Value,Form>(x:MatrixSplitExpression<Value,Form>) -> {
  return x.value();
}

function peek<Value,Form>(x:MatrixSplitExpression<Value,Form>) -> {
  return x.peek();
}

function eval<Value,Form>(x:MatrixSplitExpression<Value,Form>) -> {
  return x.eval();
}

function grad<Value,Form,Gradient>(x:MatrixSplitExpression<Value,Form>,
    d:Gradient) {
  x.grad(d);
}

function grad<Value,Form,Gradient>(x:MatrixSplitExpression<Value,Form>,
    d:Gradient, i:Integer) {
  x.grad(d, i);
}

function grad<Value,Form,Gradient>(x:MatrixSplitExpression<Value,Form>,
    d:Gradient, i:Integer, j:Integer) {
  x.grad(d, i, j);
}

function label<Value,Form>(x:MatrixSplitExpression<Value,Form>, gen:Integer) {
  x.label(gen);
}

function constant<Value,Form>(x:MatrixSplitExpression<Value,Form>,
    gen:Integer) {
  x.constant(gen);
}

function constant<Value,Form>(x:MatrixSplitExpression<Value,Form>) {
  x.constant();
}
