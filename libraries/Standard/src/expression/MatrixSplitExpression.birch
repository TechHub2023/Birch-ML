/**
 * Boxed and split matrix expression.
 *
 * - Value: Result type.
 * - Form: Expression type.
 */
final class MatrixSplitExpression<Value,Form>(x:Value, f:Form) <
    BoxedExpression<Value,Form> {
  function grad(d:Value, i:Integer, j:Integer) {
    if !flagConstant {
      if visitCount == 0 {
        this.d <- matrix<Value>(0, rows(), columns());
      }
      this.d[i,j] <- this.d[i,j] + d;
      visitCount <- visitCount + 1;
      if visitCount >= evalCount {
        assert visitCount == evalCount || evalCount == 0;
        // ^ can have visitCount > evalCount = 0 if eval() has never been
        //   called on this object, typically because it is root expression
        //   used from client code, rather than a subexpression used from
        //   other expressions
        f.grad(d!);       // upstream gradients all accumulated, propagate
        d <- nil;         // clear intermediate gradients to save memory
        visitCount <- 0;  // reset for next time
      }
    }
  }
}

/**
 * Box and split a matrix expression into a matrix of expressions.
 *
 * - f: Form of the expression.
 */
function matrix_split<Form>(f:Form) -> {
  return matrix_split(f.eval(), f);
}

/**
 * Box and split a matrix expression into a matrix of expressions.
 *
 * - x: Result of the expression.
 * - f: Form of the expression.
 */
function matrix_split<Value,Form>(x:Value, f:Form) -> {
  let y <- construct<MatrixSplitExpression<Value,Form>>(x, f);
  return vector_lambda(\(i:Integer, j:Integer) -> { return element(y, i, j); },
      rows(x), columns(x));
}
