/**
 * Lazy matrix multiply.
 */
final class MatrixMultiply(l:Expression<Real[_,_]>,
    r:Expression<Real[_,_]>) < MatrixBinaryExpression<Expression<Real[_,_]>,
    Expression<Real[_,_]>,Real[_,_]>(l, r) {  
  override function doRows() -> Integer {
    return global.rows(l!);
  }
  
  override function doColumns() -> Integer {
    return global.columns(r!);
  }

  override function doCompute() {
    x <- global.eval(l!)*global.eval(r!);
  }

  override function doGrad(gen:Integer) {
    count_grad(l!, gen, d!*transpose(global.eval(r!)));
    count_grad(r!, gen, transpose(global.eval(l!))*d!);
  }

  override function graftLinearMatrixGaussian() -> TransformLinearMatrix<MatrixGaussian>? {
    t:TransformLinearMatrix<MatrixGaussian>?;
    if !hasValue() {
      x1:MatrixGaussian?;
    
      if (t <- graft_linear_matrix_gaussian(r!))? {
        t!.leftMultiply(l!);
      } else if (x1 <- graft_matrix_gaussian(r!))? {
        t <- TransformLinearMatrix<MatrixGaussian>(l!, x1!);
      }
    }
    return t;
  }
  
  override function graftLinearMatrixNormalInverseWishart(compare:Distribution<LLT>) ->
      TransformLinearMatrix<MatrixNormalInverseWishart>? {
    t:TransformLinearMatrix<MatrixNormalInverseWishart>?;
    if !hasValue() {
      x1:MatrixNormalInverseWishart?;

      if (t <- graft_linear_matrix_normal_inverse_wishart(r!, compare))? {
        t!.leftMultiply(l!);
      } else if (x1 <- graft_matrix_normal_inverse_wishart(r!, compare))? {
        t <- TransformLinearMatrix<MatrixNormalInverseWishart>(l!, x1!);
      }
    }
    return t;
  }
}

/**
 * Lazy matrix multiply.
 */
operator (l:Expression<Real[_,_]>*r:Expression<Real[_,_]>) ->
    MatrixMultiply {
  return construct<MatrixMultiply>(l, r);
}

/**
 * Lazy matrix multiply.
 */
operator (l:Real[_,_]*r:Expression<Real[_,_]>) -> MatrixMultiply {
  return box(l)*r;
}

/**
 * Lazy matrix multiply.
 */
operator (l:Expression<Real[_,_]>*r:Real[_,_]) -> MatrixMultiply {
  return l*box(r);
}
