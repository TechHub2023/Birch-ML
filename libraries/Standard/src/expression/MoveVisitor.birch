/**
 * Move arguments in a delayed expression.
 *
 * @param x Vectorized arguments.
 */
final class MoveVisitor(x:Real[_]) {
  /**
   * Vectorized arguments.
   */
  x:Real[_] <- x;

  function visit(p:Distribution<Real>) {
    assert p.offset >= 0;
    let m <- p.offset + 1;
    cpp{{
    auto y = x.slice(m);
    }}
    p.x <- y;
  }

  function visit(p:Distribution<Real[_]>) {
    assert p.offset >= 0;
    let m <- p.offset + 1;
    let n <- m + size(p.x!) - 1;
    p.x <- x[m..n];
  }

  function visit(p:Distribution<Real[_,_]>) {
    assert p.offset >= 0;
    let m <- p.offset + 1;
    let n <- m + size(p.x!) - 1;
    p.x <- mat(x[m..n], columns(p.x!));
  }

  function visit<Value>(p:Distribution<Value>) {
    // ignore
  }
}
