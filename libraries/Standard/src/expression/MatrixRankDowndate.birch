/**
 * Lazy rank downdate.
 */
final class MatrixRankDowndate<Right>(l:Expression<LLT>, r:Right) <
    MatrixBinaryExpression<Expression<LLT>,Right,LLT>(l, r) {  
  override function doRows() -> Integer {
    return l!.rows();
  }
  
  override function doColumns() -> Integer {
    return l!.columns();
  }

  override function doCompute() {
    x <- rank_downdate(l!.eval(), r!.eval());
  }

  override function doGrad(gen:Integer) {
    l!.countGrad(gen, d!);
    r!.countGrad(gen, -(d! + transpose(d!))*r!.eval());
  }
}

/**
 * Lazy rank 1 downdate.
 */
function rank_downdate(l:Expression<LLT>, r:Expression<Real[_]>) ->
    MatrixRankDowndate<Expression<Real[_]>> {
  assert l.columns() == r.rows();
  return construct<MatrixRankDowndate<Expression<Real[_]>>>(l, r);
}

/**
 * Lazy rank 1 downdate.
 */
function rank_downdate(l:LLT, r:Expression<Real[_]>) ->
    MatrixRankDowndate<Expression<Real[_]>> {
  return rank_downdate(box(l), r);
}

/**
 * Lazy rank 1 downdate.
 */
function rank_downdate(l:Expression<LLT>, r:Real[_]) ->
    MatrixRankDowndate<Expression<Real[_]>> {
  return rank_downdate(l, box(r));
}

/**
 * Lazy rank $k$ downdate.
 */
function rank_downdate(l:Expression<LLT>, r:Expression<Real[_,_]>) ->
    MatrixRankDowndate<Expression<Real[_,_]>> {
  assert l.columns() == r.rows();
  return construct<MatrixRankDowndate<Expression<Real[_,_]>>>(l, r);
}

/**
 * Lazy rank $k$ downdate.
 */
function rank_downdate(l:LLT, r:Expression<Real[_,_]>) ->
    MatrixRankDowndate<Expression<Real[_,_]>> {
  return rank_downdate(box(l), r);
}

/**
 * Lazy rank $k$ downdate.
 */
function rank_downdate(l:Expression<LLT>, r:Real[_,_]) ->
    MatrixRankDowndate<Expression<Real[_,_]>> {
  return rank_downdate(l, box(r));
}
