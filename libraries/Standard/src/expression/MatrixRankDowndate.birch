/**
 * Lazy rank downdate.
 */
final class MatrixRankDowndate<Right>(l:Expression<LLT>, r:Right) <
    MatrixBinaryExpression<Expression<LLT>,Right,LLT>(l, r) {  
  override function doRows() -> Integer {
    return global.rows(l!);
  }
  
  override function doColumns() -> Integer {
    return global.columns(l!);
  }

  override function doCompute() {
    x <- rank_downdate(global.eval(l!), global.eval(r!));
  }

  override function doGrad(gen:Integer) {
    count_grad(l!, gen, d!);
    count_grad(r!, gen, -(d! + transpose(d!))*global.eval(r!));
  }
}

/**
 * Lazy rank 1 downdate.
 */
function rank_downdate(l:Expression<LLT>, r:Expression<Real[_]>) ->
    MatrixRankDowndate<Expression<Real[_]>> {
  assert global.columns(l) == global.rows(r);
  return construct<MatrixRankDowndate<Expression<Real[_]>>>(l, r);
}

/**
 * Lazy rank 1 downdate.
 */
function rank_downdate(l:LLT, r:Expression<Real[_]>) ->
    MatrixRankDowndate<Expression<Real[_]>> {
  return rank_downdate(box(l), r);
}

/**
 * Lazy rank 1 downdate.
 */
function rank_downdate(l:Expression<LLT>, r:Real[_]) ->
    MatrixRankDowndate<Expression<Real[_]>> {
  return rank_downdate(l, box(r));
}

/**
 * Lazy rank $k$ downdate.
 */
function rank_downdate(l:Expression<LLT>, r:Expression<Real[_,_]>) ->
    MatrixRankDowndate<Expression<Real[_,_]>> {
  assert global.columns(l) == global.rows(r);
  return construct<MatrixRankDowndate<Expression<Real[_,_]>>>(l, r);
}

/**
 * Lazy rank $k$ downdate.
 */
function rank_downdate(l:LLT, r:Expression<Real[_,_]>) ->
    MatrixRankDowndate<Expression<Real[_,_]>> {
  return rank_downdate(box(l), r);
}

/**
 * Lazy rank $k$ downdate.
 */
function rank_downdate(l:Expression<LLT>, r:Real[_,_]) ->
    MatrixRankDowndate<Expression<Real[_,_]>> {
  return rank_downdate(l, box(r));
}
