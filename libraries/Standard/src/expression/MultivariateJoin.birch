/**
 * Lazy `join`.
 */
final class MultivariateJoin<Value>(m:Expression<Value>[_]) <
    MultivariateExpression<Value[_]> {
  /**
   * Arguments.
   */
  m:Expression<Value>[_]? <- m;

  override function doDepth() -> Integer {
    let depth <- 0;
    for i in 1..length() {
      depth <- max(depth, m![i].depth());
    }
    return depth + 1;
  }

  override function doRows() -> Integer {
    return global.length(m!);
  }

  override function doValue() -> Value[_] {
    return transform(m!, \(x:Expression<Value>) -> Value {
        return global.value(x);
      });
  }
  
  override function doEval(gen:Integer) -> Value[_] {
    return transform(m!, \(x:Expression<Value>) -> Value {
        return count_eval(x, gen);
      });
  }

  override function doReval(gen:Integer) -> Value[_] {
    return transform(m!, \(x:Expression<Value>) -> Value {
        return count_reval(x, gen);
      });
  }
  
  override function doGrad(gen:Integer) {
    for_each(m!, d!, \(x:Expression<Value>, d:Value) {
        count_grad(x, gen, d!);
      });
  }

  override function doConstant() {
    m <- nil;
  }
}

/**
 * Lazy `join`. Converts a vector of scalar expressions into a vector
 * expression.
 */
function join<Value>(m:Expression<Value>[_]) -> MultivariateJoin<Value> {
  return construct<MultivariateJoin<Value>>(m);
}

/**
 * Lazy `split`. Converts a vector expression into a vector of scalar
 * expressions.
 */
function split<Value>(m:Expression<Value[_]>) -> Expression<Value>[_] {
  let z <- canonical(m);
  // ^ canonical(m) above is an identity function for all but Random objects;
  //   for these it wraps the Random in an additional expression that can
  //   accumulate gradients by element (which a Random cannot) before passing
  //   the whole vector of accumulated gradients onto the Random

  return vector(\(i:Integer) -> Expression<Value> {
        return construct<MultivariateElement<Value>>(z, i);
      }, global.length(z));
}
