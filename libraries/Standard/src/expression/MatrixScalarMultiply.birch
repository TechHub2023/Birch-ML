/**
 * Lazy matrix multiply.
 */
final class MatrixScalarMultiply(l:Expression<Real>,
    r:Expression<Real[_,_]>) < MatrixBinaryExpression<Expression<Real>,
    Expression<Real[_,_]>,Real[_,_]>(l, r) {  
  override function doRows() -> Integer {
    return r!.rows();
  }
  
  override function doColumns() -> Integer {
    return r!.columns();
  }

  override function doCompute() {
    x <- l!.eval()*r!.eval();
  }

  override function doGrad(gen:Integer) {
    l!.countGrad(gen, trace(d!*transpose(r!.eval())));
    r!.countGrad(gen, l!.eval()*d!);
  }

  override function graftLinearMatrixGaussian() -> TransformLinearMatrix<MatrixGaussian>? {
    t:TransformLinearMatrix<MatrixGaussian>?;
    if !hasValue() {
      x1:MatrixGaussian?;
    
      if (t <- r!.graftLinearMatrixGaussian())? {
        t!.multiply(l!);
      } else if (x1 <- r!.graftMatrixGaussian())? {
        t <- TransformLinearMatrix<MatrixGaussian>(diagonal(l!, r!.rows()), x1!);
      }
    }
    return t;
  }

  override function graftLinearMatrixNormalInverseWishart(compare:Distribution<LLT>) ->
      TransformLinearMatrix<MatrixNormalInverseWishart>? {
    t:TransformLinearMatrix<MatrixNormalInverseWishart>?;
    if !hasValue() {
      x1:MatrixNormalInverseWishart?;

      if (t <- r!.graftLinearMatrixNormalInverseWishart(compare))? {
        t!.multiply(l!);
      } else if (x1 <- r!.graftMatrixNormalInverseWishart(compare))? {
        t <- TransformLinearMatrix<MatrixNormalInverseWishart>(diagonal(l!, r!.rows()), x1!);
      }
    }
    return t;
  }
}

/**
 * Lazy matrix multiply.
 */
operator (l:Expression<Real>*r:Expression<Real[_,_]>) ->
    MatrixScalarMultiply {
  return construct<MatrixScalarMultiply>(l, r);
}

/**
 * Lazy matrix multiply.
 */
operator (l:Real*r:Expression<Real[_,_]>) -> MatrixScalarMultiply {
  return box(l)*r;
}

/**
 * Lazy matrix multiply.
 */
operator (l:Expression<Real>*r:Real[_,_]) -> MatrixScalarMultiply {
  return l*box(r);
}

/**
 * Lazy matrix multiply.
 */
operator (l:Expression<Real[_,_]>*r:Expression<Real>) -> MatrixScalarMultiply {
  return r*l;
}

/**
 * Lazy matrix multiply.
 */
operator (l:Real[_,_]*r:Expression<Real>) -> MatrixScalarMultiply {
  return r*l;
}

/**
 * Lazy matrix multiply.
 */
operator (l:Expression<Real[_,_]>*r:Real) -> MatrixScalarMultiply {
  return r*l;
}
