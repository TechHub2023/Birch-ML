/**
 * Lazy matrix stack.
 */
final class MatrixStack(l:Expression<Real[_,_]>, r:Expression<Real[_,_]>) <
    MatrixBinaryExpression<Expression<Real[_,_]>,Expression<Real[_,_]>,
    Real[_,_]>(l, r) {  
  override function doRows() -> Integer {
    return global.rows(l!) + global.rows(r!);
  }
  
  override function doColumns() -> Integer {
    assert global.columns(l!) == global.columns(r!);
    return global.columns(l!);
  }

  override function doCompute() {
    x <- stack(global.eval(l!), global.eval(r!));
  }

  override function doGrad(gen:Integer) {
    let r1 <- global.rows(global.eval(l!));
    let r2 <- global.rows(global.eval(r!));
    let c1 <- global.columns(global.eval(l!));
    count_grad(l!, gen, d![1..r1, 1..c1]);
    count_grad(r!, gen, d![(r1 + 1)..(r1 + r2), 1..c1]);
  }
}

/**
 * Lazy matrix stack.
 */
function stack(l:Expression<Real[_,_]>, r:Expression<Real[_,_]>) ->
    MatrixStack {
  assert global.columns(l) == global.columns(r);
  return construct<MatrixStack>(l, r);
}

/**
 * Lazy matrix stack.
 */
function stack(l:Real[_,_], r:Expression<Real[_,_]>) -> MatrixStack {
  return stack(box(l), r);
}

/**
 * Lazy matrix stack.
 */
function stack(l:Expression<Real[_,_]>, r:Real[_,_]) -> MatrixStack {
  return stack(l, box(r));
}
