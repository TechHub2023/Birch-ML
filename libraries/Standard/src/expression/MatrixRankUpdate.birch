/**
 * Lazy rank update.
 */
final class MatrixRankUpdate<Right>(l:Expression<LLT>, r:Right) <
    MatrixBinaryExpression<Expression<LLT>,Right,LLT>(l, r) {  
  override function doRows() -> Integer {
    return global.rows(l!);
  }
  
  override function doColumns() -> Integer {
    return global.columns(r!);
  }

  override function doCompute() {
    x <- rank_update(global.eval(l!), global.eval(r!));
  }

  override function doGrad(gen:Integer) {
    count_grad(l!, gen, d!);
    count_grad(r!, gen, (d! + transpose(d!))*global.eval(r!));
  }
}

/**
 * Lazy rank 1 update.
 */
function rank_update(l:Expression<LLT>, r:Expression<Real[_]>) ->
    MatrixRankUpdate<Expression<Real[_]>> {
  assert global.columns(l) == global.rows(r);
  return construct<MatrixRankUpdate<Expression<Real[_]>>>(l, r);
}

/**
 * Lazy rank 1 update.
 */
function rank_update(l:LLT, r:Expression<Real[_]>) ->
    MatrixRankUpdate<Expression<Real[_]>> {
  return rank_update(box(l), r);
}

/**
 * Lazy rank 1 update.
 */
function rank_update(l:Expression<LLT>, r:Real[_]) ->
    MatrixRankUpdate<Expression<Real[_]>> {
  return rank_update(l, box(r));
}

/**
 * Lazy rank $k$ update.
 */
function rank_update(l:Expression<LLT>, r:Expression<Real[_,_]>) ->
    MatrixRankUpdate<Expression<Real[_,_]>> {
  assert global.columns(l) == global.rows(r);
  return construct<MatrixRankUpdate<Expression<Real[_,_]>>>(l, r);
}

/**
 * Lazy rank $k$ update.
 */
function rank_update(l:LLT, r:Expression<Real[_,_]>) ->
    MatrixRankUpdate<Expression<Real[_,_]>> {
  return rank_update(box(l), r);
}

/**
 * Lazy rank $k$ update.
 */
function rank_update(l:Expression<LLT>, r:Real[_,_]) ->
    MatrixRankUpdate<Expression<Real[_,_]>> {
  return rank_update(l, box(r));
}
