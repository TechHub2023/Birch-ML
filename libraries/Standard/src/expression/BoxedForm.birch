/**
 * Boxed form. Memoizes forward evaluation, acting as a checkpoint for
 * reverse-mode automatic differentiation.
 *
 * - Value: Result type.
 * - Form: Form form.
 */
final class BoxedForm<Value,Form>(x:Value!, f:Form) <
    Expression<Value>(x, false) {
  /**
   * Expression form.
   */
  f:Form? <- f;

  override function doMove() {
    this.x <- global.move(f!);
  }

  override function doShallowGrad() {
    global.shallow_grad(f!, this.d!);
    this.d <- nil;  // clear intermediate gradient to save memory
  }

  override function doDeepGrad() {
    global.deep_grad(f!);
  }

  override function doLink() {
    global.link(f!);
  }

  override function doLabel(gen:Integer) {
    global.label(f!, this.gen);
  }

  override function doConstant(gen:Integer) {
    global.constant(f!, this.gen);
  }

  override function doConstant() {
    global.constant(f!);
    f <- nil;
  }
}
