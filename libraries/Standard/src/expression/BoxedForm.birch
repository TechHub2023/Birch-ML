/**
 * Boxed form. Memoizes forward evaluation, acting as a checkpoint for
 * reverse-mode automatic differentiation.
 *
 * @param Value Result type.
 * @param Form Form form.
 */
final class BoxedForm<Value,Form>(x:Value!, f:Form) <
    Expression<Value>(x, false) {
  /**
   * Expression form.
   */
  f:Form? <- f;

  override function doEval() {
    this.x <- global.eval(f!);
  }

  override function doMove(visitor:MoveVisitor) {
    this.x <- global.move(f!, visitor);
  }

  override function doArgs(visitor:ArgsVisitor) {
    global.args(f!, visitor);
  }

  override function doShallowGrad() {
    global.shallow_grad(f!, this.g!);
    this.g <- nil;  // clear intermediate gradient to save memory
  }

  override function doDeepGrad() {
    global.deep_grad(f!);
  }

  override function doReset() {
    global.reset(f!);
  }

  override function doRelink() {
    global.relink(f!);
  }

  override function doConstant() {
    global.constant(f!);
    f <- nil;
  }
}
