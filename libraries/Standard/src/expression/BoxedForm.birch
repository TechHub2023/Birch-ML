/**
 * Boxed form.
 *
 * - Value: Result type.
 * - Form: Expression form.
 */
class BoxedForm<Value,Form>(x:Value, f:Form) < Expression<Value> {
  /**
   * Memoized result.
   */
  x:Value <- x;

  /**
   * Expression form.
   */
  f:Form? <- f;

  /**
   * Accumulated upstream gradient.
   */
  d:Value?;

  /**
   * Generation label.
   */
  gen:Integer <- 0;

  /**
   * Number of times `eval()` has been called.
   */
  evalCount:Integer <- 0;

  /**
   * Number of times `reval()` or `grad()` has been called. Used to obtain
   * pre- and post-order traversals of the expression graph.
   */
  visitCount:Integer <- 0;

  final override function isRandom() -> Boolean {
    return false;
  }

  final override function isConstant() -> Boolean {
    return !f?;
  }

  final override function rows() -> Integer {
    return global.rows(x);
  }
  
  final override function columns() -> Integer {
    return global.columns(x);
  }

  final override function value() -> Value {
    constant();
    return x;
  }

  final override function peek() -> Value {
    return x;
  }

  final override function eval() -> Value {
    if f? {
      evalCount <- evalCount + 1;
    }
    return x;
  }

  final override function reval() -> Value {
    if f? {
      if visitCount == 0 {
        assert !d?;
        x <- global.reval(f!);
      }
      visitCount <- visitCount + 1;
      if visitCount >= evalCount {
        assert visitCount == evalCount || evalCount == 0;
        // ^ can have visitCount > evalCount = 0 if eval() has never been
        //   called on this object, typically because it is a root expression
        //   used from client code, rather than a subexpression used from
        //   other expressions
        visitCount <- 0;  // reset for next time
      }
    }
    return x;
  }

  final override function grad(d:Value) {
    if f? {
      if visitCount == 0 {
        assert !this.d?;
        this.d <- d;  // start accumulation
      } else {
        assert this.d?;
        this.d <- this.d! + d;
      }
      visitCount <- visitCount + 1;
      if visitCount >= evalCount {
        assert visitCount == evalCount || evalCount == 0;
        // ^ can have visitCount > evalCount = 0 if eval() has never been
        //   called on this object, typically because it is a root expression
        //   used from client code, rather than a subexpression used from
        //   other expressions
        global.grad(f!, this.d!);  // upstream gradients all accumulated, propagate
        this.d <- nil;    // clear intermediate gradients to save memory
        visitCount <- 0;  // reset for next time
      }
    }
  }

  final override function label(gen:Integer) {
    if f? && this.gen == 0 {
      this.gen <- gen;
      global.label(f!, gen);
    }
  }

  final override function constant(gen:Integer) {
    if f? {
      if this.gen < gen {
        constant();
      } else {
        global.constant(f!, gen);
      }
    }
  }

  final override function constant() {
    if f? {
      global.constant(f!);
      f <- nil;
      d <- nil;
      gen <- 0;
      evalCount <- 0;
      visitCount <- 0;
    }
  }
}
