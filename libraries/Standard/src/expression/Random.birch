/**
 * Random variate.
 *
 * @param Value Value type.
 *
 * Random objects, like all [Expression](../../classes/Expression/) objects,
 * are stateful. Random objects in the pilot state are considered
 * *variables*, meaning that a call to `grad()` will compute gradients with
 * respect to them, and a further call to `move()` will apply a Markov kernel
 * to update their value. Random objects in the value state are considered
 * *constants*.
 */
class Random<Value> < Expression<Value>(nil, false) {
  /**
   * Distribution associated with the random variate.
   */
  p:Distribution<Value>?;

  /**
   * Value assignment.
   */
  operator <- x:Value! {
    this.set(x);
    if this.hasDistribution() {
      this.p!.assign(x);
    }
    this.constant();
  }

  /**
   * Value assignment.
   */
  function set(x:Value!) {
    assert !this.hasValue();
    this.x <- x;
    this.g <- nil;
  }

  /**
   * Does this have a distribution?
   */
  function hasDistribution() -> Boolean {
    return p?;
  }

  /**
   * Get the distribution.
   */
  function getDistribution() -> Distribution<Value> {
    assert p?;
    return Distribution<Value>?(p!.getUpdated())!;
  }

  /**
   * Set the distribution.
   */
  function setDistribution(p:Distribution<Value>) {
    assert !this.p?;
    this.p <- p;
    if this.x? {
      p.assign(this.x!);
    } else if p.x? {
      this.x <- p.x!;
    }
  }

  /**
   * Does this have a marginalized distribution?
   */
  function hasMarginalizedDistribution() -> Boolean {
    return hasDistribution() && p!.getUpdated().hasMarginalized();
  }

  /**
   * Get the marginalized distribution.
   */
  function getMarginalizedDistribution() -> Delay {
    return p!.getUpdated().getMarginalized().getUpdated();
  }

  /**
   * Graft.
   */
  function graft() -> Delay {
    assert p?;
    return p!.graft();
  }

  function isGaussian() -> Boolean {
    return hasDistribution() && getDistribution().isGaussian();
  }

  function isBeta() -> Boolean {
    return hasDistribution() && getDistribution().isBeta();
  }

  function isGamma() -> Boolean {
    return hasDistribution() && getDistribution().isGamma();
  }

  function isInverseGamma() -> Boolean {
    return hasDistribution() && getDistribution().isInverseGamma();
  } 

  function isInverseWishart() -> Boolean {
    return hasDistribution() && getDistribution().isInverseWishart();
  } 

  function isNormalInverseGamma() -> Boolean {
    return hasDistribution() && getDistribution().isNormalInverseGamma();
  }

  function isDirichlet() -> Boolean {
    return hasDistribution() && getDistribution().isDirichlet();
  }

  function isRestaurant() -> Boolean {
    return hasDistribution() && getDistribution().isRestaurant();
  }

  function isMultivariateGaussian() -> Boolean {
    return hasDistribution() && getDistribution().isMultivariateGaussian();
  }

  function isMultivariateNormalInverseGamma() -> Boolean {
    return hasDistribution() && getDistribution().isMultivariateNormalInverseGamma();
  }

  function isMultivariateNormalInverseWishart() -> Boolean {
    return hasDistribution() && getDistribution().isMultivariateNormalInverseWishart();
  }

  function isMatrixGaussian() -> Boolean {
    return hasDistribution() && getDistribution().isMatrixGaussian();
  }

  function isMatrixNormalInverseWishart() -> Boolean {
    return hasDistribution() && getDistribution().isMatrixNormalInverseWishart();
  }

  function isBoundedDiscrete() -> Boolean {
    return hasDistribution() && getDistribution().isBoundedDiscrete();
  }

  override function doEval() {
    assert hasDistribution();
    this.x <- global.eval(getDistribution());
  }

  override function doMove(visitor:MoveVisitor) {
    if hasDistribution() {
      this.x <- global.move(getDistribution(), visitor);
    }
  }

  override function doArgs(visitor:ArgsVisitor) {
    if hasDistribution() {
      global.args(getDistribution(), visitor);
    }
  }

  override function doShallowGrad(visitor:GradVisitor) {
    if hasDistribution() {
      global.shallow_grad(getDistribution(), this.g!, visitor);
    }
  }

  override function doDeepGrad(visitor:GradVisitor) {
    if hasDistribution() {
      global.deep_grad(getDistribution(), visitor);
    }
  }

  override function doReset() {
    if hasDistribution() {
      global.reset(getDistribution());
    }
  }

  override function doRelink(visitor:RelinkVisitor) {
    if hasDistribution() {
      global.relink(getDistribution(), visitor);
    }
  }

  override function doConstant() {
    if hasDistribution() {
      global.constant(getDistribution());
    }
  }

  override function read(buffer:Buffer) {
    this <-? buffer.get<Value>();
  }

  override function write(buffer:Buffer) {
    if this.hasValue() || this.hasDistribution() {
      buffer.set(this.value()!);
    } else {
      buffer.setNil();
    }
  }
}

/**
 * Create a Random.
 */
function make_random<Value>() -> Random<Value> {
  o:Random<Value>;
  return o;
}

/**
 * Box a value in a Random.
 *
 * @param x Value to assign.
 */
function make_random<Value>(x:Value) -> Random<Value> {
  o:Random<Value>;
  o <- x;
  return o;
}

/**
 * Box a value in a Random.
 *
 * @param x Value to assign.
 */
function make_random<Value>(x:Value?) -> Random<Value> {
  o:Random<Value>;
  o <-? x;
  return o;
}

/**
 * Box a value in a Random.
 *
 * @param x Value to assign.
 */
function make_random<Value>(x:Value!) -> Random<Value> {
  o:Random<Value>;
  o <- x;
  return o;
}

/**
 * Box a value in a Random.
 *
 * @param x Optional value to assign.
 */
function make_random<Value>(x:Value!?) -> Random<Value> {
  o:Random<Value>;
  if x? {
    o <- x!;
  }
  return o;
}

/**
 * Box a value in a Random (identity operation).
 *
 * @param x Existing object.
 */
function make_random<Value>(x:Random<Value>) -> Random<Value> {
  return x;
}
