/**
 * Random variate.
 *
 * - Value: Value type.
 *
 * Random objects, like all [Expression](../../classes/Expression/) objects,
 * are stateful. Random objects in the pilot state are considered
 * *variables*, meaning that a call to `grad()` will compute gradients with
 * respect to them, and a further call to `move()` will apply a Markov kernel
 * to update their value. Random objects in the value state are considered
 * *constants*.
 */
final class Random<Value> < Expression<Value>(nil) {  
  /**
   * Associated distribution.
   */
  p:Distribution<Value>?;
  
  /**
   * Accumulated gradient.
   */
  d:Value?;

  /**
   * Value assignment.
   */
  operator <- x:Value {
    assert !this.x?;
    assert !this.p?;
    this.x <- x;
    constant();
  }

  /**
   * Optional assignment.
   */
  operator <- x:Value? {
    if x? {
      this <- x!;
    }
  }

  override function distribution() -> Distribution<Value>? {
    return p;
  }

  override function isRandom() -> Boolean {
    return true;
  }

  /**
   * Does this have a distribution?
   */
  function hasDistribution() -> Boolean {
    return p?;
  }

  /**
   * Get the distribution.
   */
  function getDistribution() -> Distribution<Value> {
    return p!;
  }

  /**
   * Does this have a gradient?
   */
  function hasGradient() -> Boolean {
    return d?;
  }

  /**
   * Get the gradient of an evaluated function with respect to this random
   * variable at its current value.
   */
  function getGradient() -> Value {
    return d!;
  }

  /**
   * Assume the distribution for the random variate. When a value for the
   * random variate is required, it will be simulated from this distribution
   * and trigger an *update* on the delayed sampling graph.
   *
   * - p: The distribution.
   */
  function assume(p:Distribution<Value>) {
    assert !this.p?;
    assert !this.x?;
    p.setRandom(this);
    this.p <- p;
  }

  /**
   * Set a pilot value.
   *
   * - x: Pilot value.
   */
  function setPilot(gen:Integer, x:Value) {
    assert !this.p?;
    assert !this.x?;
    assert this.pilotCount == 0;

    this.x <- x;
    this.generation <- gen;
  }

  override function doDepth() -> Integer {
    return 1;
  }
  
  override function doRows() -> Integer {
    return p!.rows();
  }

  override function doColumns() -> Integer {
    return p!.columns();
  }

  override function doValue() -> Value {
    assert p?;
    p!.prune();
    let x <- p!.simulate();
    p!.update(x);
    p!.unlink();
    p!.unsetRandom(this);
    return x;
  }

  override function doPilot(gen:Integer) -> Value {
    return doGet();
  }

  override function doGet() -> Value {
    if x? {
      /* pilot value already set */
      return x!;
    } else {
      /* simulate a pilot value */
      assert p?;
      if p!.supportsLazy() {
        p!.prune();
        let x <- p!.simulateLazy();
        assert x?;
        p!.updateLazy(this);
        p!.unlink();
        p!.unsetRandom(this);
        return x!;
      } else {
        return doValue();
      }
    }
  }

  override function doMove(gen:Integer, κ:Kernel) -> Value {
    return κ.move(this);
  }

  override function doPrior() -> Expression<Real>? {
    if p? {
      let p1 <- p!.logpdfLazy(this);
      p <- nil;
      if p1? {
        let p2 <- p1!.prior();
        if p2? {
          return p1! + p2!;
        } else {
          return p1!;
        }
      }
    }
    return nil;
  }

  override function doCompare(gen:Integer, x:DelayExpression, κ:Kernel) ->
      Real {
    let y <- Random<Value>?(x)!;
    return κ.logpdf(y, this) - κ.logpdf(this, y);
  }

  function doAccumulateGrad(d:Value) {
    // ^ override is committed from the declaration as Value is not
    //   necessarily one of the supported gradient types in Expression (and
    //   need not be for this to work correctly)
    if this.d? {
      this.d <- this.d! + d;
    } else {
      this.d <- d;
    }
  }

  override function doGrad(gen:Integer) {
    //  
  }

  override function doClearGrad() {
    d <- nil;
  }

  override function doCount(gen:Integer) {
    //
  }
  
  override function doConstant() {
    //
  }

  override function doDetach() {
    p <- nil;
  }

  override function graftGaussian() -> Gaussian? {
    if !hasValue() {
      let q <- p!.graftGaussian();
      p <-? Distribution<Value>?(q);
      return q;
    }
    return nil;
  }
    
  override function graftBeta() -> Beta? {
    if !hasValue() {
      let q <- p!.graftBeta();
      p <-? Distribution<Value>?(q);
      return q;
    }
    return nil;
  }
  
  override function graftGamma() -> Gamma? {
    if !hasValue() {
      let q <- p!.graftGamma();
      p <-? Distribution<Value>?(q);
      return q;
    }
    return nil;
  }
  
  override function graftInverseGamma() -> InverseGamma? {
    if !hasValue() {
      let q <- p!.graftInverseGamma();
      p <-? Distribution<Value>?(q);
      return q;
    }
    return nil;
  } 

  override function graftInverseWishart() -> InverseWishart? {
    if !hasValue() {
      let q <- p!.graftInverseWishart();
      p <-? Distribution<Value>?(q);
      return q;
    }
    return nil;
  } 
  
  override function graftNormalInverseGamma(compare:Distribution<Real>) -> NormalInverseGamma? {
    if !hasValue() {
      let q <- p!.graftNormalInverseGamma(compare);
      p <-? Distribution<Value>?(q);
      return q;
    }
    return nil;
  }
  
  override function graftDirichlet() -> Dirichlet? {
    if !hasValue() {
      let q <- p!.graftDirichlet();
      p <-? Distribution<Value>?(q);
      return q;
    }
    return nil;
  }

  override function graftRestaurant() -> Restaurant? {
    if !hasValue() {
      let q <- p!.graftRestaurant();
      p <-? Distribution<Value>?(q);
      return q;
    }
    return nil;
  }

  override function graftMultivariateGaussian() -> MultivariateGaussian? {
    if !hasValue() {
      let q <- p!.graftMultivariateGaussian();
      p <-? Distribution<Value>?(q);
      return q;
    }
    return nil;
  }

  override function graftMultivariateNormalInverseGamma(compare:Distribution<Real>) ->
      MultivariateNormalInverseGamma? {
    if !hasValue() {
      let q <- p!.graftMultivariateNormalInverseGamma(compare);
      p <-? Distribution<Value>?(q);
      return q;
    }
    return nil;
  }

  override function graftMatrixGaussian() -> MatrixGaussian? {
    if !hasValue() {
      let q <- p!.graftMatrixGaussian();
      p <-? Distribution<Value>?(q);
      return q;
    }
    return nil;
  }

  override function graftMatrixNormalInverseWishart(compare:Distribution<LLT>) ->
      MatrixNormalInverseWishart? {
    if !hasValue() {
      let q <- p!.graftMatrixNormalInverseWishart(compare);
      p <-? Distribution<Value>?(q);
      return q;
    }
    return nil;
  }

  override function graftDiscrete() -> Discrete? {
    if !hasValue() {
      let q <- p!.graftDiscrete();
      p <-? Distribution<Value>?(q);
      return q;
    }
    return nil;
  }

  override function graftBoundedDiscrete() -> BoundedDiscrete? {
    if !hasValue() {
      let q <- p!.graftBoundedDiscrete();
      p <-? Distribution<Value>?(q);
      return q;
    }
    return nil;
  }

  function read(buffer:Buffer) {
    this <- buffer.get<Value>();
  }

  function write(buffer:Buffer) {
    if hasValue() || hasDistribution() {
      buffer.set(value());
    } else {
      buffer.setNil();
    }
  }
}

/**
 * Create a Random.
 */
function Random<Value>() -> Random<Value> {
  o:Random<Value>;
  return o;
}
