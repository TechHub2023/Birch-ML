/**
 * Lazy matrix solve.
 */
final class MatrixSolve<Left>(l:Left, r:Expression<Real[_,_]>) <
    MatrixBinaryExpression<Left,Expression<Real[_,_]>,Real[_,_]>(l, r) {  
  override function doRows() -> Integer {
    return global.rows(l!);
  }
  
  override function doColumns() -> Integer {
    return global.columns(r!);
  }

  override function doCompute() {
    x <- solve(global.eval(l!), global.eval(r!));
  }

  override function doGrad(gen:Integer) {
    let l1 <- global.eval(l!);
    let r1 <- global.eval(r!);
    let z <- solve(transpose(l1), d!);
    count_grad(l!, gen, -z*transpose(solve(l1, r1)));
    count_grad(r!, gen, z);
  }
}

/**
 * Lazy solve.
 */
function solve(l:Expression<Real[_,_]>, r:Expression<Real[_,_]>) ->
    MatrixSolve<Expression<Real[_,_]>> {
  assert global.columns(l) == global.rows(r);
  return construct<MatrixSolve<Expression<Real[_,_]>>>(l, r);
}

/**
 * Lazy solve.
 */
function solve(l:Real[_,_], r:Expression<Real[_,_]>) ->
    MatrixSolve<Expression<Real[_,_]>> {
  return solve(box(l), r);
}

/**
 * Lazy solve.
 */
function solve(l:Expression<Real[_,_]>, r:Real[_,_]) ->
    MatrixSolve<Expression<Real[_,_]>> {
  return solve(l, box(r));
}

/**
 * Lazy solve.
 */
function solve(l:Expression<LLT>, r:Expression<Real[_,_]>) ->
    MatrixSolve<Expression<LLT>> {
  assert global.columns(l) == global.rows(r);
  return construct<MatrixSolve<Expression<LLT>>>(l, r);
}

/**
 * Lazy solve.
 */
function solve(l:LLT, r:Expression<Real[_,_]>) ->
    MatrixSolve<Expression<LLT>> {
  return solve(box(l), r);
}

/**
 * Lazy solve.
 */
function solve(l:Expression<LLT>, r:Real[_,_]) ->
    MatrixSolve<Expression<LLT>> {
  return solve(l, box(r));
}
