/**
 * Lazy multivariate multiply.
 */
final class MultivariateMultiply(l:Expression<Real[_,_]>,
    r:Expression<Real[_]>) < MultivariateBinaryExpression<
    Expression<Real[_,_]>,Expression<Real[_]>,Real[_]>(l, r) {  
  override function doRows() -> Integer {
    return global.rows(l!);
  }

  override function doCompute() {
    x <- global.eval(l!)*global.eval(r!);
  }

  override function doGrad(gen:Integer) {
    count_grad(l!, gen, outer(d!, global.eval(r!)));
    count_grad(r!, gen, transpose(global.eval(l!))*d!);
  }

  override function graftLinearMultivariateGaussian() ->
      TransformLinearMultivariate<MultivariateGaussian>? {
    t:TransformLinearMultivariate<MultivariateGaussian>?;
    if !hasValue() {
      x1:MultivariateGaussian?;
      if (t <- graft_linear_multivariate_gaussian(r!))? {
        t!.leftMultiply(l!);
      } else if (x1 <- graft_multivariate_gaussian(r!))? {
        t <- TransformLinearMultivariate<MultivariateGaussian>(l!, x1!);
      }
    }
    return t;
  }
  
  override function graftLinearMultivariateNormalInverseGamma(compare:Distribution<Real>) ->
      TransformLinearMultivariate<MultivariateNormalInverseGamma>? {
    t:TransformLinearMultivariate<MultivariateNormalInverseGamma>?;
    if !hasValue() {
      x1:MultivariateNormalInverseGamma?;

      if (t <- graft_linear_multivariate_normal_inverse_gamma(r!, compare))? {
        t!.leftMultiply(l!);
      } else if (x1 <- graft_multivariate_normal_inverse_gamma(r!, compare))? {
        t <- TransformLinearMultivariate<MultivariateNormalInverseGamma>(l!, x1!);
      }
    }
    return t;
  }
}

/**
 * Lazy multivariate multiply.
 */
operator (l:Expression<Real[_,_]>*r:Expression<Real[_]>) ->
    MultivariateMultiply {
  assert global.columns(l) == global.rows(r);
  return construct<MultivariateMultiply>(l, r);
}

/**
 * Lazy multivariate multiply.
 */
operator (l:Real[_,_]*r:Expression<Real[_]>) -> MultivariateMultiply {
  return box(l)*r;
}

/**
 * Lazy multivariate multiply.
 */
operator (l:Expression<Real[_,_]>*r:Real[_]) -> MultivariateMultiply {
  return l*box(r);
}
