/**
 * Lazy multiply.
 */
final class Multiply<Left,Right,Value>(l:Left, r:Right) <
    ScalarBinaryExpression<Left,Right,Value>(l, r) {  
  override function doCompute() {
    x <- l!.eval()*r!.eval();
  }

  override function doGrad(gen:Integer) {
    l!.countGrad(gen, d!*r!.eval());
    r!.countGrad(gen, d!*l!.eval());
  }

  override function graftScaledGamma() -> TransformLinear<Gamma>? {
    t:TransformLinear<Gamma>?;
    if !hasValue() {
      x1:Gamma?;
    
      if (t <- l!.graftScaledGamma())? {
        t!.multiply(Real(r!));
      } else if (t <- r!.graftScaledGamma())? {
        t!.multiply(Real(l!));
      } else if (x1 <- l!.graftGamma())? {
        t <- TransformLinear<Gamma>(Real(r!), x1!);
      } else if (x1 <- r!.graftGamma())? {
        t <- TransformLinear<Gamma>(Real(l!), x1!);
      }
    }
    return t;
  }

  override function graftLinearGaussian() -> TransformLinear<Gaussian>? {
    t:TransformLinear<Gaussian>?;
    if !hasValue() {
      x1:Gaussian?;
    
      if (t <- l!.graftLinearGaussian())? {
        t!.multiply(Real(r!));
      } else if (t <- r!.graftLinearGaussian())? {
        t!.multiply(Real(l!));
      } else if (x1 <- l!.graftGaussian())? {
        t <- TransformLinear<Gaussian>(Real(r!), x1!);
      } else if (x1 <- r!.graftGaussian())? {
        t <- TransformLinear<Gaussian>(Real(l!), x1!);
      }
    }
    return t;
  }

  override function graftDotMultivariateGaussian() ->
      TransformDot<MultivariateGaussian>? {
    t:TransformDot<MultivariateGaussian>?;
    if !hasValue() {
      if (t <- l!.graftDotMultivariateGaussian())? {
        t!.multiply(Real(r!));
      } else if (t <- r!.graftDotMultivariateGaussian())? {
        t!.multiply(Real(l!));
      }
    }
    return t;
  }
 
  override function graftLinearNormalInverseGamma(compare:Distribution<Real>) ->
      TransformLinear<NormalInverseGamma>? {
    t:TransformLinear<NormalInverseGamma>?;
    if !hasValue() {
      x1:NormalInverseGamma?;
    
      if (t <- l!.graftLinearNormalInverseGamma(compare))? {
        t!.multiply(Real(r!));
      } else if (t <- r!.graftLinearNormalInverseGamma(compare))? {
        t!.multiply(Real(l!));
      } else if (x1 <- l!.graftNormalInverseGamma(compare))? {
        t <- TransformLinear<NormalInverseGamma>(Real(r!), x1!);
      } else if (x1 <- r!.graftNormalInverseGamma(compare))? {
        t <- TransformLinear<NormalInverseGamma>(Real(l!), x1!);
      }
    }
    return t;
  }

  override function graftDotMultivariateNormalInverseGamma(compare:Distribution<Real>) ->
      TransformDot<MultivariateNormalInverseGamma>? {
    t:TransformDot<MultivariateNormalInverseGamma>?;
    if !hasValue() {
      if (t <- l!.graftDotMultivariateNormalInverseGamma(compare))? {
        t!.multiply(Real(r!));
      } else if (t <- r!.graftDotMultivariateNormalInverseGamma(compare))? {
        t!.multiply(Real(l!));
      }
    }
    return t;
  }

  override function graftDiscrete() -> Discrete? {
    t:Discrete?;
    if !hasValue() {
      t <- graftBoundedDiscrete();
      if !t? {
        x1:Discrete?;
        if (x1 <- l!.graftDiscrete())? {
          t <- LinearDiscrete(Integer(r!), x1!, box(0));
        } else if (x1 <- r!.graftDiscrete())? {
          t <- LinearDiscrete(Integer(l!), x1!, box(0));
        }
      }
    }
    return t;
  }

  override function graftBoundedDiscrete() -> BoundedDiscrete? {
    t:BoundedDiscrete?;
    if !hasValue() {
      let l1 <- l!.graftBoundedDiscrete();
      let r1 <- r!.graftBoundedDiscrete();
      if l1? {
        t <- LinearBoundedDiscrete(Integer(r!), l1!, box(0));
      } else if r1? {
        t <- LinearBoundedDiscrete(Integer(l!), r1!, box(0));
      }
    }
    return t;
  }
}

/**
 * Lazy multiply.
 */
operator (l:Expression<Real>*r:Expression<Real>) ->
    Multiply<Expression<Real>,Expression<Real>,Real> {
  return construct<Multiply<Expression<Real>,Expression<Real>,Real>>(l, r);
}

/**
 * Lazy multiply.
 */
operator (l:Real*r:Expression<Real>) ->
    Multiply<Expression<Real>,Expression<Real>,Real> {
  return box(l)*r;
}

/**
 * Lazy multiply.
 */
operator (l:Expression<Real>*r:Real) ->
    Multiply<Expression<Real>,Expression<Real>,Real> {
  return l*box(r);
}

/**
 * Lazy multiply.
 */
operator (l:Expression<Integer>*r:Expression<Integer>) ->
    Multiply<Expression<Integer>,Expression<Integer>,Integer> {
  return construct<Multiply<Expression<Integer>,Expression<Integer>,Integer>>(l, r);
}

/**
 * Lazy multiply.
 */
operator (l:Integer*r:Expression<Integer>) ->
    Multiply<Expression<Integer>,Expression<Integer>,Integer> {
  return box(l)*r;
}

/**
 * Lazy multiply.
 */
operator (l:Expression<Integer>*r:Integer) ->
    Multiply<Expression<Integer>,Expression<Integer>,Integer> {
  return l*box(r);
}
