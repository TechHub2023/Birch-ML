/**
 * Lazy `transpose`.
 */
final class MatrixTranspose(m:Expression<Real[_,_]>) <
    MatrixUnaryExpression<Expression<Real[_,_]>,Real[_,_]>(m) {
  override function doRows() -> Integer {
    return m!.columns();
  }
  
  override function doColumns() -> Integer {
    return m!.rows();
  }

  override function doCompute() {
    x <- transpose(m!.eval());
  }

  override function doGrad(gen:Integer) {
    m!.countGrad(gen, transpose(d!));
  }
}

/**
 * Lazy `transpose`.
 */
function transpose(m:Expression<Real[_,_]>) -> MatrixTranspose {
  return construct<MatrixTranspose>(m);
}

/**
 * Lazy `transpose`, identity function.
 */
function transpose(m:Expression<LLT>) -> Expression<LLT> {
  return m;
}
