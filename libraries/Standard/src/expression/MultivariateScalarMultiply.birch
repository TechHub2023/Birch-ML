/**
 * Lazy multivariate multiply.
 */
final class MultivariateScalarMultiply(l:Expression<Real>,
    r:Expression<Real[_]>) < MultivariateBinaryExpression<Expression<Real>,
    Expression<Real[_]>,Real[_]>(l, r) {  
  override function doRows() -> Integer {
    return global.rows(r!);
  }

  override function doCompute() {
    x <- global.eval(l!)*global.eval(r!);
  }

  override function doGrad(gen:Integer) {
    count_grad(l!, gen, dot(d!, global.eval(r!)));
    count_grad(r!, gen, global.eval(l!)*d!);
  }

  override function graftLinearMultivariateGaussian() ->
      TransformLinearMultivariate<MultivariateGaussian>? {
    t:TransformLinearMultivariate<MultivariateGaussian>?;
    if !hasValue() {
      x1:MultivariateGaussian?;
    
      if (t <- graft_linear_multivariate_gaussian(r!))? {
        t!.multiply(l!);
      } else if (x1 <- graft_multivariate_gaussian(r!))? {
        t <- TransformLinearMultivariate<MultivariateGaussian>(diagonal(l!, global.rows(r!)), x1!);
      }
    }
    return t;
  }
  
  override function graftLinearMultivariateNormalInverseGamma(compare:Distribution<Real>) ->
      TransformLinearMultivariate<MultivariateNormalInverseGamma>? {
    t:TransformLinearMultivariate<MultivariateNormalInverseGamma>?;
    if !hasValue() {
      x1:MultivariateNormalInverseGamma?;

      if (t <- graft_linear_multivariate_normal_inverse_gamma(r!, compare))? {
        t!.multiply(l!);
      } else if (x1 <- graft_multivariate_normal_inverse_gamma(r!, compare))? {
        t <- TransformLinearMultivariate<MultivariateNormalInverseGamma>(diagonal(l!, global.rows(r!)), x1!);
      }
    }
    return t;
  }
}

/**
 * Lazy multivariate multiply.
 */
operator (l:Expression<Real>*r:Expression<Real[_]>) ->
    MultivariateScalarMultiply {
  return construct<MultivariateScalarMultiply>(l, r);
}

/**
 * Lazy multivariate multiply.
 */
operator (l:Real*r:Expression<Real[_]>) -> MultivariateScalarMultiply {
  return box(l)*r;
}

/**
 * Lazy multivariate multiply.
 */
operator (l:Expression<Real>*r:Real[_]) -> MultivariateScalarMultiply {
  return l*box(r);
}

/**
 * Lazy multivariate multiply.
 */
operator (l:Expression<Real[_]>*r:Expression<Real>) ->
    MultivariateScalarMultiply {
  return r*l;
}

/**
 * Lazy multivariate multiply.
 */
operator (l:Real[_]*r:Expression<Real>) -> MultivariateScalarMultiply {
  return r*l;
}

/**
 * Lazy multivariate multiply.
 */
operator (l:Expression<Real[_]>*r:Real) -> MultivariateScalarMultiply {
  return r*l;
}
