/**
 * Scalar ternary expression.
 *
 * - Left: Left argument type.
 * - Middle: Middle argument type.
 * - Right: Right argument type.
 * - Value: Result type.
 */
abstract class ScalarTernaryExpression<Left,Middle,Right,Value>(
    l:Left, m:Middle, r:Right) < ScalarExpression<Value> {  
  /**
   * Left argument.
   */
  l:Left? <- l;

  /**
   * Middle argument.
   */
  m:Middle? <- m;
  
  /**
   * Right argument.
   */
  r:Right? <- r;

  final override function doDepth() -> Integer {
    return max(global.depth(l!), max(global.depth(m!), global.depth(r!))) + 1;
  }

  final override function doValue() {
    global.value(l!);
    global.value(m!);
    global.value(r!);
  }

  final override function doEval(gen:Integer) {
    count_eval(l!, gen);
    count_eval(m!, gen);
    count_eval(r!, gen);
  }

  final override function doReval(gen:Integer) {
    count_reval(l!, gen);
    count_reval(m!, gen);
    count_reval(r!, gen);
  }

  final override function doConstant() {
    l <- nil;
    m <- nil;
    r <- nil;
  }
}
