/**
 * Lazy divide.
 */
final class Divide(l:Expression<Real>, r:Expression<Real>) <
    ScalarBinaryExpression<Expression<Real>,Expression<Real>,Real>(l, r) {  
  override function doCompute() {
    x <- l!.eval()/r!.eval();
  }

  override function doGrad(gen:Integer) {
    l!.countGrad(gen, d!/r!.eval());
    r!.countGrad(gen, -d!*l!.eval()/pow(r!.eval(), 2.0));
  }

  override function graftLinearGaussian() -> TransformLinear<Gaussian>? {
    t:TransformLinear<Gaussian>?;
    if !hasValue() {
      l1:Gaussian?;
      if (t <- l!.graftLinearGaussian())? {
        t!.divide(r!);
      } else if (l1 <- l!.graftGaussian())? {
        t <- TransformLinear<Gaussian>(1.0/r!, l1!);
      }
    }
    return t;
  }

  override function graftDotMultivariateGaussian() ->
      TransformDot<MultivariateGaussian>? {
    t:TransformDot<MultivariateGaussian>?;
    if !hasValue() {
      if (t <- l!.graftDotMultivariateGaussian())? {
        t!.divide(r!);
      }
    }
    return t;
  }
  
  override function graftLinearNormalInverseGamma(compare:Distribution<Real>) ->
      TransformLinear<NormalInverseGamma>? {
    t:TransformLinear<NormalInverseGamma>?;
    if !hasValue() {
      l1:NormalInverseGamma?;
      if (t <- l!.graftLinearNormalInverseGamma(compare))? {
        t!.divide(r!);
      } else if (l1 <- l!.graftNormalInverseGamma(compare))? {
        t <- TransformLinear<NormalInverseGamma>(1.0/r!, l1!);
      }
    }
    return t;
  }

  override function graftDotMultivariateNormalInverseGamma(compare:Distribution<Real>) ->
      TransformDot<MultivariateNormalInverseGamma>? {
    t:TransformDot<MultivariateNormalInverseGamma>?;
    if !hasValue() {
      if (t <- l!.graftDotMultivariateNormalInverseGamma(compare))? {
        t!.divide(r!);
      }
    }
    return t;
  }

  override function graftScaledGamma() -> TransformLinear<Gamma>? {
    t:TransformLinear<Gamma>?;
    if !hasValue() {
      l1:Gamma?;
      if (t <- l!.graftScaledGamma())? {
        t!.divide(r!);
      } else if (l1 <- l!.graftGamma())? {
        t <- TransformLinear<Gamma>(1.0/r!, l1!);
      }
    }
    return t;
  }
}

/**
 * Lazy divide.
 */
operator (l:Expression<Real>/r:Expression<Real>) -> Divide {
  return construct<Divide>(l, r);
}

/**
 * Lazy divide.
 */
operator (l:Real/r:Expression<Real>) -> Divide {
  return box(l)/r;
}

/**
 * Lazy divide.
 */
operator (l:Expression<Real>/r:Real) -> Divide {
  return l/box(r);
}
