/**
 * Lazy multivariate solve.
 */
final class MultivariateSolve<Left,LeftValue>(l:Left, r:Expression<Real[_]>) <
    MultivariateBinaryExpression<Left,Expression<Real[_]>,Real[_]>(l, r) {  
  override function doRows() -> Integer {
    return r!.rows();
  }
  
  override function doCompute() {
    x <- solve(l!.eval(), r!.eval());
  }

  override function doGrad(gen:Integer) {
    let l1 <- l!.eval();
    let r1 <- r!.eval();
    let z <- solve(transpose(l1), d!);
    l!.countGrad(gen, outer(-z, solve(l1, r1)));
    r!.countGrad(gen, z);
  }
}

/**
 * Lazy solve.
 */
function solve(l:Expression<Real[_,_]>, r:Expression<Real[_]>) ->
    MultivariateSolve<Expression<Real[_,_]>,Real[_,_]> {
  assert l.columns() == r.rows();
  return construct<MultivariateSolve<Expression<Real[_,_]>,Real[_,_]>>(l, r);
}

/**
 * Lazy solve.
 */
function solve(l:Real[_,_], r:Expression<Real[_]>) ->
    MultivariateSolve<Expression<Real[_,_]>,Real[_,_]> {
  return solve(box(l), r);
}

/**
 * Lazy solve.
 */
function solve(l:Expression<Real[_,_]>, r:Real[_]) ->
    MultivariateSolve<Expression<Real[_,_]>,Real[_,_]> {
  return solve(l, box(r));
}

/**
 * Lazy solve.
 */
function solve(l:Expression<LLT>, r:Expression<Real[_]>) ->
    MultivariateSolve<Expression<LLT>,LLT> {
  assert l.columns() == r.rows();
  return construct<MultivariateSolve<Expression<LLT>,LLT>>(l, r);
}

/**
 * Lazy solve.
 */
function solve(l:LLT, r:Expression<Real[_]>) ->
    MultivariateSolve<Expression<LLT>,LLT> {
  return solve(box(l), r);
}

/**
 * Lazy solve.
 */
function solve(l:Expression<LLT>, r:Real[_]) ->
    MultivariateSolve<Expression<LLT>,LLT> {
  return solve(l, box(r));
}
