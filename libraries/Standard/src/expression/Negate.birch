/**
 * Lazy negation.
 */
final class Negate(m:Expression<Real>) <
    ScalarUnaryExpression<Expression<Real>,Real>(m) {
  override function doCompute() {
    x <- -m!.eval();
  }

  override function doGrad(gen:Integer) {
    m!.countGrad(gen, -d!);
  }

  override function graftLinearGaussian() -> TransformLinear<Gaussian>? {
    t:TransformLinear<Gaussian>?;
    if !hasValue() {
      x1:Gaussian?;
      if (t <- m!.graftLinearGaussian())? {
        t!.negate();
      } else if (x1 <- m!.graftGaussian())? {
        t <- TransformLinear<Gaussian>(box(-1.0), x1!, box(0.0));
      }
    }
    return t;
  }

  override function graftDotMultivariateGaussian() ->
      TransformDot<MultivariateGaussian>? {
    t:TransformDot<MultivariateGaussian>?;
    if !hasValue() {
      if (t <- m!.graftDotMultivariateGaussian())? {
        t!.negate();
      }
    }
    return t;
  }
  
  override function graftLinearNormalInverseGamma(compare:Distribution<Real>) ->
      TransformLinear<NormalInverseGamma>? {
    t:TransformLinear<NormalInverseGamma>?;
    if !hasValue() {
      x1:NormalInverseGamma?;
      if (t <- m!.graftLinearNormalInverseGamma(compare))? {
        t!.negate();
      } else if (x1 <- m!.graftNormalInverseGamma(compare))? {
        t <- TransformLinear<NormalInverseGamma>(box(-1.0), x1!, box(0.0));
      }
    }
    return t;
  }

  override function graftDotMultivariateNormalInverseGamma(compare:Distribution<Real>) ->
      TransformDot<MultivariateNormalInverseGamma>? {
    t:TransformDot<MultivariateNormalInverseGamma>?;
    if !hasValue() {
      if (t <- m!.graftDotMultivariateNormalInverseGamma(compare))? {
        t!.negate();
      }
    }
    return t;
  }
}

/**
 * Lazy negation.
 */
operator (-m:Expression<Real>) -> Negate {
  return construct<Negate>(m);
}
