/**
 * Lazy subtract.
 */
final class Subtract<Left,Right,Value>(l:Left, r:Right) <
    ScalarBinaryExpression<Left,Right,Value>(l, r) {
  override function doCompute() {
    x <- global.eval(l!) - global.eval(r!);
  }
  
  override function doGrad(gen:Integer) {
    count_grad(l!, gen, d!);
    count_grad(r!, gen, -d!);
  }

  override function graftLinearGaussian() -> TransformLinear<Gaussian>? {
    t:TransformLinear<Gaussian>?;
    if !hasValue() {
      x1:Gaussian?;
    
      if (t <- graft_linear_gaussian(l!))? {
        t!.add(Real(-r!));
      } else if (t <- graft_linear_gaussian(r!))? {
        t!.negateAndAdd(Real(l!));
      } else if (x1 <- graft_gaussian(l!))? {
        t <- TransformLinear<Gaussian>(box(1.0), x1!, Real(-r!));
      } else if (x1 <- graft_gaussian(r!))? {
        t <- TransformLinear<Gaussian>(box(-1.0), x1!, Real(l!));
      }
    }
    return t;
  }

  override function graftDotMultivariateGaussian() ->
      TransformDot<MultivariateGaussian>? {
    t:TransformDot<MultivariateGaussian>?;
    if !hasValue() {
      if (t <- graft_dot_multivariate_gaussian(l!))? {
        t!.add(Real(-r!));
      } else if (t <- graft_dot_multivariate_gaussian(r!))? {
        t!.negateAndAdd(Real(l!));
      }
    }
    return t;
  }

  override function graftLinearNormalInverseGamma(
      compare:Distribution<Real>) ->
      TransformLinear<NormalInverseGamma>? {
    t:TransformLinear<NormalInverseGamma>?;
    if !hasValue() {
      x1:NormalInverseGamma?;

      if (t <- graft_linear_normal_inverse_gamma(l!, compare))? {
        t!.subtract(Real(r!));
      } else if (t <- graft_linear_normal_inverse_gamma(r!, compare))? {
        t!.negateAndAdd(Real(l!));
      } else if (x1 <- graft_normal_inverse_gamma(l!, compare))? {
        t <- TransformLinear<NormalInverseGamma>(box(1.0), x1!, Real(-r!));
      } else if (x1 <- graft_normal_inverse_gamma(r!, compare))? {
        t <- TransformLinear<NormalInverseGamma>(box(-1.0), x1!, Real(l!));
      }
    }
    return t;
  }

  override function graftDotMultivariateNormalInverseGamma(
      compare:Distribution<Real>) ->
      TransformDot<MultivariateNormalInverseGamma>? {
    t:TransformDot<MultivariateNormalInverseGamma>?;
    if !hasValue() {
      if (t <- graft_dot_multivariate_normal_inverse_gamma(l!, compare))? {
        t!.subtract(Real(r!));
      } else if (t <- graft_dot_multivariate_normal_inverse_gamma(r!, compare))? {
        t!.negateAndAdd(Real(l!));
      }
    }
    return t;
  }

  override function graftDiscrete() -> Discrete? {
    t:Discrete?;
    if !hasValue() {
      t <- graftBoundedDiscrete();
      if !t? {
        m:Discrete?;
        if (m <- graft_discrete(l!))? {
          t <- LinearDiscrete(box(1), m!, Integer(-r!));
        } else if (m <- graft_discrete(r!))? {
          t <- LinearDiscrete(box(-1), m!, Integer(l!));
        }
      }
    }
    return t;
  }

  override function graftBoundedDiscrete() -> BoundedDiscrete? {
    t:BoundedDiscrete?;
    if !hasValue() {
      let l1 <- graft_bounded_discrete(l!);
      let r1 <- graft_bounded_discrete(r!);
      if l1? && r1? {
        t <- SubtractBoundedDiscrete(l1!, r1!);
      } else if l1? {
        t <- LinearBoundedDiscrete(box(1), l1!, Integer(-r!));
      } else if r1? {
        t <- LinearBoundedDiscrete(box(-1), r1!, Integer(l!));
      }
    }
    return t;
  }
}

/**
 * Lazy subtract.
 */
operator (l:Expression<Real> - r:Expression<Real>) ->
    Subtract<Expression<Real>,Expression<Real>,Real> {
  return construct<Subtract<Expression<Real>,Expression<Real>,Real>>(l, r);
}

/**
 * Lazy subtract.
 */
operator (l:Real - r:Expression<Real>) ->
    Subtract<Expression<Real>,Expression<Real>,Real> {
  return box(l) - r;
}

/**
 * Lazy subtract.
 */
operator (l:Expression<Real> - r:Real) ->
    Subtract<Expression<Real>,Expression<Real>,Real> {
  return l - box(r);
}

/**
 * Lazy subtract.
 */
operator (l:Expression<Integer> - r:Expression<Integer>) ->
    Subtract<Expression<Integer>,Expression<Integer>,Integer> {
  return construct<Subtract<Expression<Integer>,Expression<Integer>,Integer>>(l, r);
}

/**
 * Lazy subtract.
 */
operator (l:Integer - r:Expression<Integer>) ->
    Subtract<Expression<Integer>,Expression<Integer>,Integer> {
  return box(l) - r;
}

/**
 * Lazy subtract.
 */
operator (l:Expression<Integer> - r:Integer) ->
    Subtract<Expression<Integer>,Expression<Integer>,Integer> {
  return l - box(r);
}
