/**
 * Lazy subtract.
 */
final class Subtract(l:Expression<Real>, r:Expression<Real>) <
    ScalarBinaryExpression<Expression<Real>,Expression<Real>,Real>(l, r) {
  override function doCompute() {
    x <- l!.eval() - r!.eval();
  }
  
  override function doGrad(gen:Integer) {
    l!.countGrad(gen, d!);
    r!.countGrad(gen, -d!);
  }

  override function graftLinearGaussian() -> TransformLinear<Gaussian>? {
    t:TransformLinear<Gaussian>?;
    if !hasValue() {
      x1:Gaussian?;
    
      if (t <- l!.graftLinearGaussian())? {
        t!.add(-r!);
      } else if (t <- r!.graftLinearGaussian())? {
        t!.negateAndAdd(l!);
      } else if (x1 <- l!.graftGaussian())? {
        t <- TransformLinear<Gaussian>(box(1.0), x1!, -r!);
      } else if (x1 <- r!.graftGaussian())? {
        t <- TransformLinear<Gaussian>(box(-1.0), x1!, l!);
      }
    }
    return t;
  }

  override function graftDotMultivariateGaussian() ->
      TransformDot<MultivariateGaussian>? {
    t:TransformDot<MultivariateGaussian>?;
    if !hasValue() {
      if (t <- l!.graftDotMultivariateGaussian())? {
        t!.add(-r!);
      } else if (t <- r!.graftDotMultivariateGaussian())? {
        t!.negateAndAdd(l!);
      }
    }
    return t;
  }

  override function graftLinearNormalInverseGamma(
      compare:Distribution<Real>) ->
      TransformLinear<NormalInverseGamma>? {
    t:TransformLinear<NormalInverseGamma>?;
    if !hasValue() {
      x1:NormalInverseGamma?;

      if (t <- l!.graftLinearNormalInverseGamma(compare))? {
        t!.subtract(r!);
      } else if (t <- r!.graftLinearNormalInverseGamma(compare))? {
        t!.negateAndAdd(l!);
      } else if (x1 <- l!.graftNormalInverseGamma(compare))? {
        t <- TransformLinear<NormalInverseGamma>(box(1.0), x1!, -r!);
      } else if (x1 <- r!.graftNormalInverseGamma(compare))? {
        t <- TransformLinear<NormalInverseGamma>(box(-1.0), x1!, l!);
      }
    }
    return t;
  }

  override function graftDotMultivariateNormalInverseGamma(
      compare:Distribution<Real>) ->
      TransformDot<MultivariateNormalInverseGamma>? {
    t:TransformDot<MultivariateNormalInverseGamma>?;
    if !hasValue() {
      if (t <- l!.graftDotMultivariateNormalInverseGamma(compare))? {
        t!.subtract(r!);
      } else if (t <- r!.graftDotMultivariateNormalInverseGamma(compare))? {
        t!.negateAndAdd(l!);
      }
    }
    return t;
  }
}

/**
 * Lazy subtract.
 */
operator (l:Expression<Real> - r:Expression<Real>) -> Subtract {
  return construct<Subtract>(l, r);
}

/**
 * Lazy subtract.
 */
operator (l:Real - r:Expression<Real>) -> Subtract {
  return box(l) - r;
}

/**
 * Lazy subtract.
 */
operator (l:Expression<Real> - r:Real) -> Subtract {
  return l - box(r);
}
