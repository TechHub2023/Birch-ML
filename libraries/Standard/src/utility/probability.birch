cpp{{
/**
 * Event handler for each thread.
 */
static thread_local libbirch::Shared<birch::type::Handler> handler =
    birch::PlayHandler(true);
}}

/**
 * Get the event handler.
 */
function get_handler() -> Handler {
  cpp{{
  return ::handler;
  }}
}

/**
 * Set the event handler.
 *
 * !!! attention
 *     Typically one does not call this directly, but rather uses the
 *     [`with`](https://www.birch.sh/language/probability/) statement, which
 *     is hard-wired to call this on exit to restore the previous handler.
 */
function set_handler(handler:Handler) {
  cpp{{
  ::handler = handler;
  }}
}

/**
 * Swap the event handler with another.
 *
 * - handler: The new handler.
 *
 * Returns: The previous handler.
 *
 * !!! attention
 *     Typically one does not call this directly, but rather uses the
 *     [`with`](https://www.birch.sh/language/probability/) statement, which
 *     is hard-wired to call this on entry to set the new handler.
 */
function swap_handler(handler:Handler) -> Handler {
  cpp{{
  auto& current = ::handler;
  auto previous = handler;
  std::swap(current, previous);
  return previous;
  }}
}

/**
 * Handle simulate event.
 *
 * - p: Distribution.
 *
 * Returns: Value simulated from the distribution.
 *
 * !!! attention
 *     Typically one does not call this directly, but rather uses the
 *     [`<~`](https://www.birch.sh/language/probability/) operator, which is
 *     hard-wired to call this internally.
 */
function handle_simulate<Right>(p:Right) -> Right.Value {
  let event <- SimulateEvent(p.distribution());
  get_handler().handle(event);
  return event.value();
}

/**
 * Handle observe event.
 *
 * - x: Variate.
 * - p: Distribution.
 *
 * Returns: `x`.
 *
 * !!! attention
 *     Typically one does not call this directly, but rather uses the
 *     [`~>`](https://www.birch.sh/language/probability/) operator, which is
 *     hard-wired to call this internally.
 */
function handle_observe<Left,Right>(x:Left, p:Right) -> Left {
  get_handler().handle(ObserveEvent(x, p.distribution()));
  return x;
}

/**
 * Handle assume event.
 *
 * - x: Random.
 * - p: Distribution.
 *
 * Returns: `x`.
 *
 * !!! attention
 *     Typically one does not call this directly, but rather uses the
 *     [`~`](https://www.birch.sh/language/probability/) operator, which is
 *     hard-wired to call this internally.
 */
function handle_assume<Left,Right>(x:Left, p:Right) -> Left {
  get_handler().handle(AssumeEvent(x, p.distribution()));
  return x;
}

/**
 * Handle assume event.
 *
 * - p: Distribution.
 *
 * Returns: A newly construct [Random](../classes/Random) with value type the
 * same as `p`.
 *
 * !!! attention
 *     Typically one does not call this directly, but rather uses the
 *     [`~`](https://www.birch.sh/language/probability/) operator, which is
 *     hard-wired to call this internally. This particular version is used
 *     when a newly declared variable is initialized with the `~` operator.
 */
function handle_assume<Right>(p:Right) -> Random<Right.Value> {
  x:Random<Right.Value>;
  return handle_assume(x, p);
}

/**
 * Handle factor event.
 *
 * - w: Log-weight.
 *
 * !!! attention
 *     Typically one does not call this directly, but rather uses the
 *     [`factor`](https://www.birch.sh/language/probability/) statement, which
 *     is hard-wired to call this internally.
 */
function handle_factor(w:Real) {
  get_handler().handle(FactorEvent(w));
}
