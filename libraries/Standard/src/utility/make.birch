/**
 * Make an object, with the type given as an argument.
 *
 *   - Type: The type.
 *
 * Returns: An optional with a value if successful, or no value if not
 * successful.
 *
 * The make will not succeed if the type is a class with initialization
 * parameters, or a compound type that includes such a class.
 */
function make<Type>() -> Type? {
  cpp{{
  return libbirch::make_optional<Type>();
  }}
}

/**
 * Make an object, with the class given as a string.
 *
 *   - name: Name of the class.
 *
 * Returns: An optional with a value if successful, or no value if not
 * successful.
 *
 * The make will not succeed if the class has initialization parameters.
 */
function make(name:String) -> Object? {
  result:Object?;
  symbol:String <- "make_" + name + "_";
  cpp{{
  using make_t = Object_*();
  void* addr = dlsym(RTLD_DEFAULT, symbol.c_str());
  if (addr) {
    result = Object(reinterpret_cast<make_t*>(addr)());
  }
  }}
  if !result? {
    warn("could not make object of type " + name +
        "; class may not exist or may require initialization arguments.");
  }
  return result;
}

/**
 * Make an object, with the class given in a buffer.
 *
 *   - buffer: The buffer.
 *
 * Returns: An optional with a value if successful, or no value if not
 * successful.
 *
 * If the buffer contains a key `class`, an object of that class is 
 * constructed. The buffer is then passed to the `read()` function of the new
 * object.
 *
 * The make will not succeed if the class has initialization parameters.
 */
function make(buffer:Buffer) -> Object? {
  result:Object?;
  let className <- buffer.get<String>("class");
  if className? {
    result <- make(className!);
  }
  if result? {
    result!.read(buffer);
  }
  return result;
}

/**
 * Make an object, with the class given in a buffer, otherwise by a generic
 * type.
 *
 * - Type: Class if not given in buffer.
 *
 * - buffer: The buffer.
 *
 * Returns: An optional with a value if successful, or no value if not
 * successful.
 *
 * If the buffer contains a key `class`, an object of that class is 
 * constructed. Otherwise, an object of class `Type` is constructed, if
 * possible. The buffer is then passed to the `read()` function of the new
 * object.
 *
 * The make will not succeed if the class has initialization parameters.
 */
function make<Type>(buffer:Buffer) -> Type? {
  result:Type?;
  let className <- buffer.get<String>("class");
  if className? {
    result <- Type?(make(className!));
  } else {
    result <- make<Type>();
  }
  if result? {
    result!.read(buffer);
  }
  return result;
}
