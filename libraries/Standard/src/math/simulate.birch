cpp{{
#include <random>

static auto make_rngs() {
  std::vector<std::mt19937_64,libbirch::Allocator<std::mt19937_64>> rngs(
      libbirch::get_max_threads());
  std::random_device rd;
  for (unsigned i = 0; i < rngs.size(); ++i) {
    rngs[i].seed(rd());
  }
  return rngs;
}

static auto& get_rng() {
  static auto rngs = make_rngs();
  return rngs[libbirch::get_thread_num()];
}
}}

/**
 * Seed the pseudorandom number generator.
 *
 * - seed: Seed value.
 */
function seed(s:Integer) {
  cpp{{
  #pragma omp parallel num_threads(libbirch::get_max_threads())
  {
    get_rng().seed(s + libbirch::get_thread_num());
  }
  }}
}

/**
 * Seed the pseudorandom number generator with entropy.
 */
function seed() {
  cpp{{
  std::random_device rd;
  #pragma omp parallel num_threads(libbirch::get_max_threads())
  {
    #pragma omp critical
    get_rng().seed(rd());
  }
  }}
}

/**
 * Simulate a Bernoulli distribution.
 *
 * - ρ: Probability of a true result.
 */
function simulate_bernoulli(ρ:Real) -> Boolean {
  assert 0.0 <= ρ && ρ <= 1.0;
  cpp{{
  return std::bernoulli_distribution(ρ)(get_rng());
  }}
}

/**
 * Simulate a delta distribution.
 *
 * - μ: Location.
 */
function simulate_delta(μ:Integer) -> Integer {
  return μ;
}

/**
 * Simulate a binomial distribution.
 *
 * - n: Number of trials.
 * - ρ: Probability of a true result.
 */
function simulate_binomial(n:Integer, ρ:Real) -> Integer {
  assert 0 <= n;
  assert 0.0 <= ρ && ρ <= 1.0;
  cpp{{
  return std::binomial_distribution<birch::type::Integer>(n, ρ)(get_rng());
  }}
}

/**
 * Simulate a negative binomial distribution.
 *
 * - k: Number of successes before the experiment is stopped.
 * - ρ: Probability of success.
 *
 * Returns the number of failures.
 */
function simulate_negative_binomial(k:Integer, ρ:Real) -> Integer {
  assert 0 < k;
  assert 0.0 <= ρ && ρ <= 1.0;
  cpp{{
  return std::negative_binomial_distribution<birch::type::Integer>(k, ρ)(get_rng());
  }}
}

/**
 * Simulate a Poisson distribution.
 *
 * - λ: Rate.
 */
function simulate_poisson(λ:Real) -> Integer {
  assert 0.0 <= λ;
  if (λ > 0.0) {
    cpp{{
    return std::poisson_distribution<birch::type::Integer>(λ)(get_rng());
    }}
  } else {
    return 0;
  }
}

/**
 * Simulate a categorical distribution.
 *
 * - ρ: Category probabilities. These should sum to one.
 */
function simulate_categorical(ρ:Real[_]) -> Integer {
  return simulate_categorical(ρ, 1.0);
}

/**
 * Simulate a categorical distribution.
 *
 * - ρ: Unnormalized category probabilities.
 * - Z: Sum of the unnormalized category probabilities.
 */
function simulate_categorical(ρ:Real[_], Z:Real) -> Integer {
  assert length(ρ) > 0;
  assert abs(sum(ρ) - Z) < 1.0e-6;

  u:Real <- simulate_uniform(0.0, Z);
  x:Integer <- 1;
  P:Real <- ρ[1];
  while (P < u) {
    assert x <= length(ρ);
    x <- x + 1;
    assert 0.0 <= ρ[x];
    P <- P + ρ[x];
    assert P < Z + 1.0e-6;
  }
  return x;
}

/**
 * Simulate a multinomial distribution.
 *
 * - n: Number of trials.
 * - ρ: Category probabilities. These should sum to one.
 *
 * This uses an $\mathcal{O}(N)$ implementation based on:
 *
 * Bentley, J. L. and J. B. Saxe (1979). Generating sorted lists of random
 * numbers. Technical Report 2450, Carnegie Mellon University, Computer
 * Science Department.
 */
function simulate_multinomial(n:Integer, ρ:Real[_]) -> Integer[_] {
  return simulate_multinomial(n, ρ, 1.0);
}

/**
 * Simulate a compound-gamma distribution.
 *
 * - k: Shape.
 * - α: Shape.
 * - β: Scale.
 *
 */
 function simulate_inverse_gamma_gamma(k:Real, α:Real, β:Real) -> Real {
    return simulate_gamma(k, simulate_inverse_gamma(α, β));
 }

/**
 * Simulate a multinomial distribution.
 *
 * - n: Number of trials.
 * - ρ: Unnormalized category probabilities.
 * - Z: Sum of the unnormalized category probabilities.
 *
 * This uses an $\mathcal{O}(N)$ implementation based on:
 *
 * Bentley, J. L. and J. B. Saxe (1979). Generating sorted lists of random
 * numbers. Technical Report 2450, Carnegie Mellon University, Computer
 * Science Department.
 */
function simulate_multinomial(n:Integer, ρ:Real[_], Z:Real) -> Integer[_] {
  assert length(ρ) > 0;
  assert abs(sum(ρ) - Z) < 1.0e-6;

  D:Integer <- length(ρ);
  R:Real <- ρ[D];
  lnMax:Real <- 0.0;
  j:Integer <- D;
  i:Integer <- n;
  u:Real;
  x:Integer[_] <- vector(0, D);
    
  while i > 0 {
    u <- simulate_uniform(0.0, 1.0);
    lnMax <- lnMax + log(u)/i;
    u <- Z*exp(lnMax);
    while u < Z - R {
      j <- j - 1;
      R <- R + ρ[j];
    }
    x[j] <- x[j] + 1;
    i <- i - 1;
  }
  while j > 1 {
    j <- j - 1;
    x[j] <- 0;
  }
  return x;
}

/**
 * Simulate a Dirichlet distribution.
 *
 * - α: Concentrations.
 */
function simulate_dirichlet(α:Real[_]) -> Real[_] {
  D:Integer <- length(α);
  x:Real[D];
  z:Real <- 0.0;

  for i in 1..D {
    x[i] <- simulate_gamma(α[i], 1.0);
    z <- z + x[i];
  }
  z <- 1.0/z;
  for i in 1..D {
    x[i] <- z*x[i];
  }  
  return x;
}

/**
 * Simulate a Dirichlet distribution.
 *
 * - α: Concentration.
 * - D: Number of dimensions.
 */
function simulate_dirichlet(α:Real, D:Integer) -> Real[_] {
  assert D > 0;
  x:Real[D];
  z:Real <- 0.0;

  for i in 1..D {
    x[i] <- simulate_gamma(α, 1.0);
    z <- z + x[i];
  }
  z <- 1.0/z;
  for i in 1..D {
    x[i] <- z*x[i];
  }
  return x;
}

/**
 * Simulate a uniform distribution.
 *
 * - l: Lower bound of interval.
 * - u: Upper bound of interval.
 */
function simulate_uniform(l:Real, u:Real) -> Real {
  assert l <= u;
  cpp{{
  return std::uniform_real_distribution<birch::type::Real>(l, u)(get_rng());
  }}
}

/**
 * Simulate a uniform distribution on an integer range.
 *
 * - l: Lower bound of range.
 * - u: Upper bound of range.
 */
function simulate_uniform_int(l:Integer, u:Integer) -> Integer {
  assert l <= u;
  cpp{{
  return std::uniform_int_distribution<birch::type::Integer>(l, u)(get_rng());
  }}
}

/**
 * Simulate a uniform distribution on unit vectors.
 *
 * - D: Number of dimensions.
 */
function simulate_uniform_unit_vector(D:Integer) -> Real[_] {
  u:Real[D];
  for d in 1..D {
    u[d] <- simulate_gaussian(0.0, 1.0);
  }
  return u/dot(u);
}

/**
 * Simulate an exponential distribution.
 *
 * - λ: Rate.
 */
function simulate_exponential(λ:Real) -> Real {
  assert 0.0 < λ;
  cpp{{
  return std::exponential_distribution<birch::type::Real>(λ)(get_rng());
  }}
}

/**
 * Simulate an Weibull distribution.
 *
 * - k: Shape.
 * - λ: Scale.
 */
function simulate_weibull(k:Real, λ:Real) -> Real {
  assert 0.0 < k;
  assert 0.0 < λ;
  cpp{{
  return std::weibull_distribution<birch::type::Real>(k, λ)(get_rng());
  }}
}

/**
 * Simulate a Gaussian distribution.
 *
 * - μ: Mean.
 * - σ2: Variance.
 */
function simulate_gaussian(μ:Real, σ2:Real) -> Real {
  assert 0.0 <= σ2;
  if (σ2 == 0.0) {
    return μ;
  } else {
    cpp{{
    return std::normal_distribution<birch::type::Real>(μ, std::sqrt(σ2))(get_rng());
    }}
  }
}

/**
 * Simulate a Student's $t$-distribution.
 *
 * - k: Degrees of freedom.
 */
function simulate_student_t(k:Real) -> Real {
  assert 0.0 < k;
  cpp{{
  return std::student_t_distribution<birch::type::Real>(k)(get_rng());
  }}
}

/**
 * Simulate a Student's $t$-distribution with location and scale.
 *
 * - k: Degrees of freedom.
 * - μ: Location.
 * - v: Scale.
 */
function simulate_student_t(k:Real, μ:Real, v:Real) -> Real {
  assert 0.0 < k;
  assert 0.0 < v;
  let y <- simulate_gaussian(0.0, v/k);
  let z <- simulate_chi_squared(k);
  return μ + y/sqrt(z/k);
}

/**
 * Simulate a multivariate Student's $t$-distribution variate with
 * location and scale.
 *
 * - k: Degrees of freedom.
 * - μ: Mean.
 * - U: Spread.
 * - v: Scale.
 */
function simulate_multivariate_student_t(k:Real, μ:Real[_], U:LLT, v:Real) ->
    Real[_] {
  assert 0.0 < k;
  assert 0.0 < v;
  let n <- length(μ);
  let y <- vector(\(i:Integer) -> Real {
      return simulate_gaussian(0.0, v/k);
    }, n);
  let z <- simulate_chi_squared(k);
  return μ + cholesky(U)*y/sqrt(z/k);
}

/**
 * Simulate a multivariate Student's $t$-distribution variate with
 * location and scale.
 *
 * - k: Degrees of freedom.
 * - μ: Mean.
 * - u: Scale.
 * - V: Spread.
 */
function simulate_multivariate_student_t(k:Real, μ:Real[_], u:Real, V:LLT) ->
    Real[_] {
  let p <- length(μ);
  let Σ <- simulate_inverse_wishart(V, k + p - 1.0);
  return simulate_multivariate_gaussian(μ, llt(u*canonical(Σ)));
}

/**
 * Simulate a matrix Student's $t$-distribution variate.
 *
 * - k: Degrees of freedom.
 * - M: Mean.
 * - U: Spread.
 * - Ψ: Spread.
 */
function simulate_matrix_student_t(k:Real, M:Real[_,_], U:LLT, Ψ:LLT) ->
    Real[_,_] {
  let p <- columns(M);
  let V <- simulate_inverse_wishart(Ψ, k + p - 1.0);
  return simulate_matrix_gaussian(M, U, V);
}

/**
 * Simulate a beta distribution.
 *
 * - α: Shape.
 * - β: Shape.
 */
function simulate_beta(α:Real, β:Real) -> Real {
  assert 0.0 < α;
  assert 0.0 < β;
  
  u:Real <- simulate_gamma(α, 1.0);
  v:Real <- simulate_gamma(β, 1.0);
  
  return u/(u + v);
}

/**
 * Simulate $\chi^2$ distribution.
 *
 * - ν: Degrees of freedom.
 */
function simulate_chi_squared(ν:Real) -> Real {
  assert 0.0 < ν;
  cpp{{
  return std::chi_squared_distribution<birch::type::Real>(ν)(get_rng());
  }}
}

/**
 * Simulate a gamma distribution.
 *
 * - k: Shape.
 * - θ: Scale.
 */
function simulate_gamma(k:Real, θ:Real) -> Real {
  assert 0.0 < k;
  assert 0.0 < θ;
  cpp{{
  return std::gamma_distribution<birch::type::Real>(k, θ)(get_rng());
  }}
}

/**
 * Simulate a Wishart distribution.
 *
 * - Ψ: Scale.
 * - k: Degrees of freedeom.
 */
function simulate_wishart(Ψ:LLT, k:Real) -> LLT {
  assert rows(Ψ) == columns(Ψ);
  assert k > rows(Ψ) - 1.0;
  let p <- rows(Ψ);
  A:Real[p,p];
  
  for i in 1..p {
    for j in 1..p {
      if j == i {
        /* on diagonal */
        A[i,j] <- sqrt(simulate_chi_squared(k - i + 1.0));
      } else if j < i {
        /* in lower triangle */
        A[i,j] <- simulate_gaussian(0.0, 1.0);
      } else {
        /* in upper triangle */
        A[i,j] <- 0.0;
      }
    }
  }
  let L <- cholesky(Ψ)*A;
  return llt(L*transpose(L));
}

/**
 * Simulate an inverse-gamma distribution.
 *
 * - α: Shape.
 * - β: Scale.
 */
function simulate_inverse_gamma(α:Real, β:Real) -> Real {
  return 1.0/simulate_gamma(α, 1.0/β);
}

/**
 * Simulate an inverse-Wishart distribution.
 *
 * - Ψ: Scale.
 * - k: Degrees of freedeom.
 */
function simulate_inverse_wishart(Ψ:LLT, k:Real) -> LLT {
  return inv(simulate_wishart(inv(Ψ), k));
}

/**
 * Simulate a normal inverse-gamma distribution.
 *
 * - μ: Mean.
 * - a2: Variance scale.
 * - α: Shape of inverse-gamma on variance.
 * - β: Scale of inverse-gamma on variance.
 */
function simulate_normal_inverse_gamma(μ:Real, a2:Real, α:Real,
    β:Real) -> Real {
  return simulate_student_t(2.0*α, μ, a2*2.0*β);
}

/**
 * Simulate a beta-bernoulli distribution.
 *
 * - α: Shape.
 * - β: Shape.
 */
function simulate_beta_bernoulli(α:Real, β:Real) -> Boolean {
  assert 0.0 < α;
  assert 0.0 < β;
  
  return simulate_bernoulli(simulate_beta(α, β));
}

/**
 * Simulate a beta-binomial distribution.
 *
 * - n: Number of trials.
 * - α: Shape.
 * - β: Shape.
 */
function simulate_beta_binomial(n:Integer, α:Real, β:Real) -> Integer {
  assert 0 <= n;
  assert 0.0 < α;
  assert 0.0 < β;
  
  return simulate_binomial(n, simulate_beta(α, β));
}

/**
 * Simulate a beta-negative-binomial distribution.
 *
 * - k: Number of successes.
 * - α: Shape.
 * - β: Shape.
 */
function simulate_beta_negative_binomial(k:Integer, α:Real, β:Real) -> Integer {
  assert 0.0 < α;
  assert 0.0 < β;
  assert 0 < k;

  return simulate_negative_binomial(k, simulate_beta(α, β));
}


/**
 * Simulate a gamma-Poisson distribution.
 *
 * - k: Shape.
 * - θ: Scale.
 */
function simulate_gamma_poisson(k:Real, θ:Real) -> Integer {
  assert 0.0 < k;
  assert 0.0 < θ;
  assert k == floor(k);
  
  return simulate_negative_binomial(Integer(k), 1.0/(θ + 1.0));
}

/**
 * Simulate a Lomax distribution.
 *
 * - λ: Scale.
 * - α: Shape.
 */
function simulate_lomax(λ:Real, α:Real) -> Real {
  assert 0.0 < λ;
  assert 0.0 < α;

  u:Real <- simulate_uniform(0.0, 1.0);
  return λ*(pow(u, -1.0/α)-1.0);
}

/**
 * Simulate a Dirichlet-categorical distribution.
 *
 * - α: Concentrations.
 */
function simulate_dirichlet_categorical(α:Real[_]) -> Integer {
  return simulate_categorical(simulate_dirichlet(α));
}

/**
 * Simulate a Dirichlet-multinomial distribution.
 *
 * - n: Number of trials.
 * - α: Concentrations.
 */
function simulate_dirichlet_multinomial(n:Integer, α:Real[_]) -> Integer[_] {
  return simulate_multinomial(n, simulate_dirichlet(α));
}

/**
 * Simulate a categorical distribution with Chinese restaurant process
 * prior.
 *
 * - α: Concentration.
 * - θ: Discount.
 * - n: Enumerated items.
 * - N: Total number of items.
 */
function simulate_crp_categorical(α:Real, θ:Real, n:Integer[_],
    N:Integer) -> Integer {
  assert N >= 0;
  assert sum(n) == N;

  k:Integer <- 0;
  K:Integer <- length(n);
  if (N == 0) {
    /* first component */
    k <- 1;
  } else {
    u:Real <- simulate_uniform(0.0, N + θ);
    U:Real <- K*α + θ;
    if (u < U) {
      /* new component */
      k <- K + 1;
    } else {
      /* existing component */
      while (k < K && u > U) {
        k <- k + 1;
        U <- U + n[k] - α;
      }
    }
  }
  return k;
}

/**
 * Simulate a Gaussian distribution with a normal inverse-gamma prior.
 *
 * - μ: Mean.
 * - a2: Variance.
 * - α: Shape of the inverse-gamma.
 * - β: Scale of the inverse-gamma.
 */
function simulate_normal_inverse_gamma_gaussian(μ:Real, a2:Real,
    α:Real, β:Real) -> Real {
  return simulate_student_t(2.0*α, μ, 2.0*β*(1.0 + a2));
}

/**
 * Simulate a Gaussian distribution with a normal inverse-gamma prior.
 *
 * - a: Scale.
 * - μ: Mean.
 * - a2: Variance.
 * - c: Offset.
 * - α: Shape of the inverse-gamma.
 * - β: Scale of the inverse-gamma.
 */
function simulate_linear_normal_inverse_gamma_gaussian(a:Real, μ:Real,
    a2:Real, c:Real, α:Real, β:Real) -> Real {
  return simulate_student_t(2.0*α, a*μ + c, 2.0*β*(1.0 + a*a*a2));
}

/**
 * Simulate a multivariate Gaussian distribution.
 *
 * - μ: Mean.
 * - Σ: Covariance.
 */
function simulate_multivariate_gaussian(μ:Real[_], Σ:LLT) -> Real[_] {
  let D <- length(μ);
  z:Real[D];
  for d in 1..D {
    z[d] <- simulate_gaussian(0.0, 1.0);
  }
  return μ + cholesky(Σ)*z;
}

/**
 * Simulate a multivariate Gaussian distribution with independent elements.
 *
 * - μ: Mean.
 * - σ2: Variance.
 */
function simulate_multivariate_gaussian(μ:Real[_], σ2:Real[_]) -> Real[_] {
  let D <- length(μ);
  z:Real[D];
  for d in 1..D {
    z[d] <- μ[d] + simulate_gaussian(0.0, σ2[d]);
  }
  return z;
}

/**
 * Simulate a multivariate Gaussian distribution with independent elements of
 * identical variance.
 *
 * - μ: Mean.
 * - σ2: Variance.
 */
function simulate_multivariate_gaussian(μ:Real[_], σ2:Real) -> Real[_] {
  let D <- length(μ);
  let σ <- sqrt(σ2);
  z:Real[D];
  for d in 1..D {
    z[d] <- μ[d] + σ*simulate_gaussian(0.0, 1.0);
  }
  return z;
}

/**
 * Simulate a multivariate normal inverse-gamma distribution.
 *
 * - ν: Precision times mean.
 * - Λ: Precision.
 * - α: Shape of inverse-gamma on scale.
 * - β: Scale of inverse-gamma on scale.
 */
function simulate_multivariate_normal_inverse_gamma(ν:Real[_], Λ:LLT,
    α:Real, β:Real) -> Real[_] {
  return simulate_multivariate_student_t(2.0*α, solve(Λ, ν), inv(Λ), 2.0*β);
}

/**
 * Simulate a multivariate Gaussian distribution with a multivariate normal
 * inverse-gamma prior.
 *
 * - ν: Precision times mean.
 * - Λ: Precision.
 * - α: Shape of the inverse-gamma.
 * - β: Scale of the inverse-gamma.
 * - γ: Scale accumulator of the inverse-gamma.
 */
function simulate_multivariate_normal_inverse_gamma_multivariate_gaussian(
    ν:Real[_], Λ:LLT, α:Real, γ:Real) -> Real[_] {
  let n <- length(ν);
  let μ <- solve(Λ, ν);
  let β <- γ - 0.5*dot(μ, ν);

  return simulate_multivariate_student_t(2.0*α, μ,
      llt(identity(n) + canonical(inv(Λ))), 2.0*β);
}

/**
 * Simulate a multivariate Gaussian distribution with a linear transformation
 * of a multivariate linear normal inverse-gamma prior.
 *
 * - A: Scale.
 * - ν: Precision times mean.
 * - Λ: Precision.
 * - c: Offset.
 * - α: Shape of the inverse-gamma.
 * - γ: Scale accumulator of the inverse-gamma.
 */
function simulate_linear_multivariate_normal_inverse_gamma_multivariate_gaussian(
    A:Real[_,_], ν:Real[_], Λ:LLT, c:Real[_], α:Real, γ:Real) -> Real[_] {
  let n <- rows(A);
  let μ <- solve(Λ, ν);
  let β <- γ - 0.5*dot(μ, ν);
  return simulate_multivariate_student_t(2.0*α, A*μ + c,
      llt(identity(n) + A*solve(Λ, transpose(A))), 2.0*β);
}

/**
 * Simulate a Gaussian distribution with a linear transformation of a
 * multivariate linear normal inverse-gamma prior.
 *
 * - a: Scale.
 * - ν: Precision times mean.
 * - Λ: Precision.
 * - c: Offset.
 * - α: Shape of the inverse-gamma.
 * - γ: Scale accumulator of the inverse-gamma.
 */
function simulate_linear_multivariate_normal_inverse_gamma_gaussian(
    a:Real[_], ν:Real[_], Λ:LLT, c:Real, α:Real, γ:Real) -> Real {
  let μ <- solve(Λ, ν);
  let β <- γ - 0.5*dot(μ, ν);
  return simulate_student_t(2.0*α, dot(a, μ) + c,
      2.0*β*(1.0 + dot(a, solve(Λ, a))));
}

/**
 * Simulate a matrix Gaussian distribution.
 *
 * - M: Mean.
 * - U: Among-row covariance.
 * - V: Among-column covariance.
 */
function simulate_matrix_gaussian(M:Real[_,_], U:LLT, V:LLT) -> Real[_,_] {
  assert rows(M) == rows(U);
  assert rows(M) == columns(U);
  assert columns(M) == rows(V);
  assert columns(M) == columns(V);
  
  let N <- rows(M);
  let P <- columns(M);
  Z:Real[N,P];
  for n in 1..N {
    for p in 1..P {
      Z[n,p] <- simulate_gaussian(0.0, 1.0);
    }
  }
  return M + cholesky(U)*Z*transpose(cholesky(V));
}

/**
 * Simulate a matrix Gaussian distribution with independent columns.
 *
 * - M: Mean.
 * - U: Among-row covariance.
 * - σ2: Among-column variances.
 */
function simulate_matrix_gaussian(M:Real[_,_], U:LLT, σ2:Real[_]) ->
    Real[_,_] {
  assert rows(M) == rows(U);
  assert rows(M) == columns(U);
  assert columns(M) == length(σ2);
  
  let N <- rows(M);
  let P <- columns(M);
  Z:Real[N,P];
  for n in 1..N {
    for p in 1..P {
      Z[n,p] <- simulate_gaussian(0.0, 1.0);
    }
  }
  return M + cholesky(U)*Z*diagonal(sqrt(σ2));
}

/**
 * Simulate a matrix Gaussian distribution with independent rows.
 *
 * - M: Mean.
 * - V: Among-column variances.
 */
function simulate_matrix_gaussian(M:Real[_,_], V:LLT) -> Real[_,_] {
  assert columns(M) == rows(V);
  assert columns(M) == columns(V);
  
  let N <- rows(M);
  let P <- columns(M);
  Z:Real[N,P];
  for n in 1..N {
    for p in 1..P {
      Z[n,p] <- simulate_gaussian(0.0, 1.0);
    }
  }
  return M + Z*transpose(cholesky(V));
}

/**
 * Simulate a matrix Gaussian distribution with independent elements.
 *
 * - M: Mean.
 * - σ2: Variances.
 */
function simulate_matrix_gaussian(M:Real[_,_], σ2:Real[_]) -> Real[_,_] {
  assert columns(M) == length(σ2);
  
  let N <- rows(M);
  let P <- columns(M);
  X:Real[N,P];
  for n in 1..N {
    for p in 1..P {
      X[n,p] <- simulate_gaussian(M[n,p], σ2[p]);
    }
  }
  return X;
}

/**
 * Simulate a matrix Gaussian distribution with independent elements of
 * identical variance.
 *
 * - M: Mean.
 * - σ2: Variance.
 */
function simulate_matrix_gaussian(M:Real[_,_], σ2:Real) -> Real[_,_] {
  let N <- rows(M);
  let P <- columns(M);
  X:Real[N,P];
  for n in 1..N {
    for p in 1..P {
      X[n,p] <- simulate_gaussian(M[n,p], σ2);
    }
  }
  return X;
}

/**
 * Simulate a matrix normal-inverse-Wishart distribution.
 *
 * - N: Precision times mean matrix.
 * - Λ: Precision.
 * - Ψ: Variance shape.
 * - k: Degrees of freedom.
 */
function simulate_matrix_normal_inverse_wishart(N:Real[_,_], Λ:LLT, Ψ:LLT,
    k:Real) -> Real[_,_] {
  let p <- columns(N);
  let M <- solve(Λ, N);
  return simulate_matrix_student_t(k - p + 1.0, M, inv(Λ), Ψ);
}

/**
 * Simulate a Gaussian distribution with matrix-normal-inverse-Wishart prior.
 *
 * - N: Precision times mean matrix.
 * - Λ: Precision.
 * - Ψ: Variance shape.
 * - k: Degrees of freedom.
 */
function simulate_matrix_normal_inverse_wishart_matrix_gaussian(N:Real[_,_],
    Λ:LLT, Ψ:LLT, k:Real) -> Real[_,_] {
  let n <- rows(N);
  let p <- columns(N);
  let M <- solve(Λ, N);
  let Σ <- llt(identity(n) + canonical(inv(Λ)));
  return simulate_matrix_student_t(k - p + 1.0, M, Σ, Ψ);
}

/**
 * Simulate a Gaussian distribution with linear transformation of a
 * matrix-normal-inverse-Wishart prior.
 *
 * - A: Scale.
 * - N: Precision times mean matrix.
 * - Λ: Precision.
 * - C: Offset.
 * - Ψ: Variance shape.
 * - k: Degrees of freedom.
 */
function simulate_linear_matrix_normal_inverse_wishart_matrix_gaussian(
    A:Real[_,_], N:Real[_,_], Λ:LLT, C:Real[_,_], Ψ:LLT, k:Real) ->
    Real[_,_] {
  let n <- rows(A);
  let p <- columns(N);
  let M <- solve(Λ, N);
  let Σ <- llt(identity(n) + A*solve(Λ, transpose(A)));
  return simulate_matrix_student_t(k - p + 1.0, A*M + C, Σ, Ψ);
}

/**
 * Simulate a Gaussian distribution with linear transformation of a
 * matrix-normal-inverse-Wishart prior.
 *
 * - a: Scale.
 * - N: Precision times mean matrix.
 * - Λ: Precision.
 * - c: Offset.
 * - Ψ: Variance shape.
 * - k: Degrees of freedom.
 */
function simulate_linear_matrix_normal_inverse_wishart_multivariate_gaussian(
    a:Real[_], N:Real[_,_], Λ:LLT, c:Real[_], Ψ:LLT, k:Real) -> Real[_] {
  let p <- columns(N);
  let M <- solve(Λ, N);
  let σ2 <- 1.0 + dot(a, solve(Λ, a));
  return simulate_multivariate_student_t(k - p + 1.0, dot(a, M) + c, σ2, Ψ);
}

/**
 * Simulate a multivariate uniform distribution.
 *
 * - l: Lower bound of hyperrectangle.
 * - u: Upper bound of hyperrectangle.
 */
function simulate_independent_uniform(l:Real[_], u:Real[_]) -> Real[_] {
  assert length(l) == length(u);
  D:Integer <- length(l);
  z:Real[D];
  for d in 1..D {
    z[d] <- simulate_uniform(l[d], u[d]);
  }
  return z;
}

/**
 * Simulate a multivariate uniform distribution over integers.
 *
 * - l: Lower bound of hyperrectangle.
 * - u: Upper bound of hyperrectangle.
 */
function simulate_independent_uniform_int(l:Integer[_], u:Integer[_]) ->
    Integer[_] {
  assert length(l) == length(u);
  D:Integer <- length(l);
  z:Integer[D];
  for d in 1..D {
    z[d] <- simulate_uniform_int(l[d], u[d]);
  }
  return z;
}
