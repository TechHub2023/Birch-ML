/*
 * Update the parameters of a Beta distribution with a Bernoulli likelihood.
 *
 * - x: The variate.
 * - α: Prior first shape.
 * - β: Prior second shape.
 *
 * Returns: the posterior hyperparameters `α'` and `β'`.
 */
function update_lazy_beta_bernoulli(x:Expression<Boolean>,
    α:Expression<Real>, β:Expression<Real>) ->
    (Expression<Real>, Expression<Real>) {
  return (box(α + x), box(β + (1.0 - x)));
}

/*
 * Update the parameters of a Beta distribution with a Binomial likelihood.
 *
 * - x: The variate.
 * - n: Number of trials.
 * - α: Prior first shape.
 * - β: Prior second shape.
 *
 * Returns: the posterior hyperparameters `α'` and `β'`.
 */
function update_lazy_beta_binomial(x:Expression<Integer>,
    n:Expression<Integer>, α:Expression<Real>, β:Expression<Real>) ->
    (Expression<Real>, Expression<Real>) {
  return (box(α + x), box(β + n - x));
}

/*
 * Update the parameters of a Beta distribution with a Negative Binomial likelihood.
 *
 * - x: The variate.
 * - k: Number of successes.
 * - α: Prior first shape.
 * - β: Prior second shape.
 *
 * Returns: the posterior hyperparameters `α'` and `β'`.
 */
function update_lazy_beta_negative_binomial(x:Expression<Integer>,
    k:Expression<Integer>, α:Expression<Real>,
    β:Expression<Real>) -> (Expression<Real>, Expression<Real>) {
  return (box(α + k), box(β + x));
}

/*
 * Update the parameters of a Gamma distribution with a Poisson likelihood.
 *
 * - x: The variate.
 * - k: Prior shape.
 * - θ: Prior scale.
 *
 * Returns: the posterior hyperparameters `k'` and `θ'`.
 */
function update_lazy_gamma_poisson(x:Expression<Integer>, k:Expression<Real>,
    θ:Expression<Real>) -> (Expression<Real>, Expression<Real>) {
  return (box(k + x), box(θ/(θ + 1.0)));
}

/*
 * Update the parameters of a scaled Gamma distribution with a Poisson
 * likelihood.
 *
 * - x: The variate.
 * - a: Scale.
 * - k: Prior shape.
 * - θ: Prior scale.
 *
 * Returns: the posterior hyperparameters `k'` and `θ'`.
 */
function update_lazy_scaled_gamma_poisson(x:Expression<Integer>,
    a:Expression<Real>, k:Expression<Real>, θ:Expression<Real>) ->
    (Expression<Real>, Expression<Real>) {
  return (box(k + x), box(θ/(a*θ + 1.0)));
}

/*
 * Update the parameters of a Gamma distribution with an exponential
 * likelihood.
 *
 * - x: The variate.
 * - k: Prior shape.
 * - θ: Prior scale.
 *
 * Returns: the posterior hyperparameters `k'` and `θ'`.
 */
function update_lazy_gamma_exponential(x:Expression<Real>,
    k:Expression<Real>, θ:Expression<Real>) ->
    (Expression<Real>, Expression<Real>) {
  return (box(k + 1.0), box(θ/(1.0 + x*θ)));
}

/*
 * Update the parameters of a scaled Gamma distribution with an exponential
 * likelihood.
 *
 * - x: The variate.
 * - a: Constant scale.
 * - k: Prior shape.
 * - θ: Prior scale.
 *
 * Returns: the posterior hyperparameters `k'` and `θ'`.
 */
function update_lazy_scaled_gamma_exponential(x:Expression<Real>,
    a:Expression<Real>, k:Expression<Real>, θ:Expression<Real>) ->
    (Expression<Real>, Expression<Real>) {
  return (box(k + 1.0), box(θ/(1.0 + x*a*θ)));
}

/*
 * Update the parameters of an inverse-gamma distribution with a Weibull
 * likelihood with known shape.
 *
 * - x: The variate.
 * - k: Likelihood shape.
 * - α: Prior shape.
 * - β: Prior scale.
 *
 * Returns: the posterior hyperparameters `α'` and `β'`.
 */
function update_lazy_inverse_gamma_weibull(x:Expression<Real>,
    k:Expression<Real>, α:Expression<Real>, β:Expression<Real>) ->
    (Expression<Real>, Expression<Real>) {
  return (box(α + 1.0), box(β + pow(x, k)));
}

/*
 * Update the parameters of a scaled inverse-gamma distribution with a
 * Weibull likelihood with known shape.
 *
 * - x: The variate.
 * - k: Likelihood shape.
 * - a: Constant scale.
 * - α: Prior shape.
 * - β: Prior scale.
 *
 * Returns: the posterior hyperparameters `α'` and `β'`.
 */
function update_lazy_scaled_inverse_gamma_weibull(x:Expression<Real>,
    k:Expression<Real>, a:Expression<Real>, α:Expression<Real>,
    β:Expression<Real>) -> (Expression<Real>, Expression<Real>) {
  return (box(α + 1.0), box(β + pow(x, k)/a));
}

/*
 * Update the parameters of a Dirichlet distribution with a categorical
 * likelihood.
 *
 * - x: The variate.
 * - α: Prior concentrations.
 *
 * Returns: the posterior hyperparameters `α'`.
 */
//function update_lazy_dirichlet_categorical(x:Expression<Integer>,
//    α:Expression<Real[_]>) -> Expression<Real[_]> {
  ///@todo
//}

/*
 * Update the parameters of a Dirichlet distribution with a multinomial
 * likelihood.
 *
 * - x: The variate.
 * - n: Number of trials.
 * - α': Prior concentrations.
 *
 * Returns: the posterior hyperparameters `α'`.
 */
//function update_lazy_dirichlet_multinomial(x:Expression<Integer[_]>,
//    n:Expression<Integer>, α:Expression<Real[_]>) -> Expression<Real[_]> {
  ///@todo
//}

/*
 * Update the parameters of a Gaussian distribution with a Gaussian
 * likelihood.
 *
 * - x: The variate.
 * - μ: Prior mean.
 * - σ2: Prior variance.
 * - s2: Likelihood variance.
 *
 * Returns: the posterior hyperparameters `μ'` and `σ2'`.
 */
function update_lazy_gaussian_gaussian(x:Expression<Real>,
    μ:Expression<Real>, σ2:Expression<Real>, s2:Expression<Real>) ->
    (Expression<Real>, Expression<Real>) {
  let λ <- 1.0/σ2;
  let l <- 1.0/s2;
  let λ' <- λ + l;
  let μ' <- (λ*μ + l*x)/λ';
  return (box(μ'), box(1.0/λ'));
}

/*
 * Update the parameters of a Gaussian distribution with a Gaussian
 * likelihood and scaling.
 *
 * - x: The variate.
 * - a: Scale.
 * - μ: Prior mean.
 * - σ2: Prior variance.
 * - c: Offset.
 * - s2: Likelihood variance.
 *
 * Returns: the posterior hyperparameters `μ'` and `λ'`.
 */
function update_lazy_linear_gaussian_gaussian(x:Expression<Real>,
    a:Expression<Real>, μ:Expression<Real>, σ2:Expression<Real>,
    c:Expression<Real>, s2:Expression<Real>) -> (Expression<Real>, Expression<Real>) {
  let λ <- 1.0/σ2;
  let l <- 1.0/s2;
  let λ' <- λ + a*a*l;
  let μ' <- (λ*μ + a*l*(x - c))/λ';
  return (box(μ'), box(1.0/λ'));
}

/*
 * Update the parameters of an inverse-gamma distribution that is part
 * of a normal inverse-gamma joint distribution.
 *
 * - x: The variate.
 * - μ: Mean.
 * - λ: Precision.
 * - α: Prior shape of the inverse-gamma.
 * - β: Prior scale of the inverse-gamma.
 *
 * Returns: the posterior hyperparameters `α'` and `β'`.
 */
function update_lazy_normal_inverse_gamma(x:Expression<Real>,
    μ:Expression<Real>, λ:Expression<Real>, α:Expression<Real>,
    β:Expression<Real>) -> (Expression<Real>, Expression<Real>) {
  return (box(α + 0.5), box(β + 0.5*pow(x - μ, 2.0)*λ));
}

/*
 * Update the parameters of a normal inverse-gamma distribution with a
 * Gaussian likelihood.
 *
 * - x: The variate.
 * - μ: Mean.
 * - λ: Precision.
 * - α: Prior shape of the inverse-gamma.
 * - β: Prior scale of the inverse-gamma.
 *
 * Returns: the posterior hyperparameters `μ'`, `λ'`, `α'` and `β'`.
 */
function update_lazy_normal_inverse_gamma_gaussian(x:Expression<Real>,
    μ:Expression<Real>, λ:Expression<Real>, α:Expression<Real>,
    β:Expression<Real>) -> (Expression<Real>, Expression<Real>,
    Expression<Real>, Expression<Real>) {
  let λ' <- λ + 1.0;
  let μ' <- (λ*μ + x)/λ';
  let α' <- α + 0.5;
  let β' <- β + 0.5*(λ/λ')*pow(x - μ, 2.0);
  return (box(μ'), box(λ'), box(α'), box(β'));
}

/*
 * Update the parameters of a normal inverse-gamma distribution with a
 * Gaussian likelihood.
 *
 * - x: The variate.
 * - a: Scale.
 * - μ: Prior mean.
 * - λ: Prior precision.
 * - c: Offset.
 * - α: Prior shape of the inverse-gamma.
 * - β: Prior scale of the inverse-gamma.
 *
 * Returns: the posterior hyperparameters `μ'`, `λ'`, `α'` and `β'`.
 */
function update_lazy_linear_normal_inverse_gamma_gaussian(x:Expression<Real>,
    a:Expression<Real>, μ:Expression<Real>, λ:Expression<Real>,
    c:Expression<Real>, α:Expression<Real>, β:Expression<Real>) ->
    (Expression<Real>, Expression<Real>, Expression<Real>, Expression<Real>) {
  let y <- x - c;
  let λ' <- λ + a*a;
  let μ' <- (λ*μ + a*y)/λ';
  let α' <- α + 0.5;
  let β' <- β + 0.5*(y*y + μ*μ*λ - μ'*μ'*λ');
  return (box(μ'), box(λ'), box(α'), box(β'));
}

/*
 * Update the parameters of an inverse-gamma distribution with a
 * gamma likelihood.
 *
 * - x: The variate.
 * - k: Shape of the gamma.
 * - α: Prior shape of the inverse-gamma.
 * - β: Prior scale of the inverse-gamma.
 *
 * Returns: the posterior hyperparameters `α'` and `β'`.
 */
function update_lazy_inverse_gamma_gamma(x:Expression<Real>,
    k:Expression<Real>, α:Expression<Real>, β:Expression<Real>) ->
    (Expression<Real>, Expression<Real>) {
  return (box(α + k), box(β + x));
}

/*
 * Update the parameters of a multivariate Gaussian distribution with a
 * multivariate Gaussian likelihood.
 *
 * - x: The variate.
 * - μ: Prior mean.
 * - Σ: Prior covariance.
 * - S: Likelihood covariance.
 *
 * Returns: the posterior hyperparameters `μ'` and `Σ'`.
 */
function update_lazy_multivariate_gaussian_multivariate_gaussian(x:Expression<Real[_]>,
    μ:Expression<Real[_]>, Σ:Expression<Real[_,_]>, S:Expression<Real[_,_]>) ->
    (Expression<Real[_]>, Expression<Real[_,_]>) {
  let K' <- transpose(solve(Σ + S, Σ));
  let μ' <- μ + K'*(x - μ);
  let Σ' <- Σ - K'*Σ;
  return (box(μ'), box(Σ'));
}

/*
 * Update the parameters of a multivariate Gaussian distribution with a 
 * linear transformation and multivariate Gaussian likelihood.
 *
 * - x: The variate.
 * - A: Scale.
 * - μ: Prior mean.
 * - Σ: Prior covariance.
 * - c: Offset.
 * - S: Likelihood covariance.
 *
 * Returns: the posterior hyperparameters `μ'` and `Σ'`.
 */
function update_lazy_linear_multivariate_gaussian_multivariate_gaussian(
    x:Expression<Real[_]>, A:Expression<Real[_,_]>, μ:Expression<Real[_]>,
    Σ:Expression<Real[_,_]>, c:Expression<Real[_]>, S:Expression<Real[_,_]>) ->
    (Expression<Real[_]>, Expression<Real[_,_]>) {
  let K' <- Σ*transpose(solve(A*Σ*transpose(A) + S, A));
  let μ' <- μ + K'*(x - A*μ - c);
  let Σ' <- Σ - K'*A*Σ;
  return (box(μ'), box(Σ'));
}

/*
 * Update the parameters of a multivariate Gaussian distribution with a 
 * linear transformation involving a dot product, and a multivariate Gaussian
 * likelihood.
 *
 * - x: The variate.
 * - a: Scale.
 * - μ: Prior mean.
 * - Σ: Prior covariance.
 * - c: Offset.
 * - s2: Likelihood covariance.
 *
 * Returns: the posterior hyperparameters `μ'` and `Σ'`.
 */
function update_lazy_linear_multivariate_gaussian_gaussian(
    x:Expression<Real>, a:Expression<Real[_]>, μ:Expression<Real[_]>,
    Σ:Expression<Real[_,_]>, c:Expression<Real>, s2:Expression<Real>) ->
    (Expression<Real[_]>, Expression<Real[_,_]>) {
  let k' <- Σ*a/(dot(a, Σ*a) + s2);
  let μ' <- μ + k'*(x - dot(a, μ) - c);
  let Σ' <- Σ - outer(k', a)*Σ;
  return (box(μ'), box(Σ'));
}

/*
 * Update the parameters of an inverse-gamma distribution with a linear
 * scaling and Gaussian likelihood.
 *
 * - x: The variate.
 * - ν: Precision times mean.
 * - Λ: Precision.
 * - α: Prior shape of the inverse-gamma.
 * - β: Prior scale of the inverse-gamma.
 *
 * Returns: the posterior hyperparameters `α'` and `β'`.
 */
function update_lazy_multivariate_normal_inverse_gamma(x:Expression<Real[_]>,
    ν:Expression<Real[_]>, Λ:Expression<Real[_,_]>, α:Expression<Real>,
    β:Expression<Real>) -> (Expression<Real>, Expression<Real>) {
  let D <- x.length();
  let μ <- solve(Λ, ν);
  return (box(α + 0.5*D), box(β + 0.5*dot(x - μ, matrix<Real>(Λ)*(x - μ))));
}

/*
 * Update the parameters of a normal inverse-gamma distribution with a
 * multivariate Gaussian likelihood.
 *
 * - x: The variate.
 * - ν: Prior precision times mean.
 * - Λ: Prior precision.
 * - α: Prior shape of the inverse-gamma.
 * - γ: Prior scale accumulator.
 *
 * Returns: the posterior hyperparameters `μ'`, `Λ'`, `α'` and `γ'`.
 */
function update_lazy_multivariate_normal_inverse_gamma_multivariate_gaussian(
    x:Expression<Real[_]>, ν:Expression<Real[_]>, Λ:Expression<Real[_,_]>,
    α:Expression<Real>, γ:Expression<Real>) -> (Expression<Real[_]>,
    Expression<Real[_,_]>, Expression<Real>, Expression<Real>) {
  let D <- x.length();
  let Λ' <- rank_update(Λ, identity(rows(Λ)));
  let ν' <- ν + x;
  let α' <- α + 0.5*D;
  let γ' <- γ + 0.5*dot(x);
  return (box(ν'), box(Λ'), box(α'), box(γ'));
}

/*
 * Update the parameters of a normal inverse-gamma distribution with a
 * linear transformation and multivariate Gaussian likelihood.
 *
 * - x: The variate.
 * - A: Scale.
 * - ν: Prior precision times mean.
 * - Λ: Prior precision.
 * - c: Offset.
 * - α: Prior shape of the inverse-gamma.
 * - γ: Prior scale accumulator.
 *
 * Returns: the posterior hyperparameters `μ'`, `Λ'`, `γ'`, `α'` and `β'`.
 */
function update_lazy_linear_multivariate_normal_inverse_gamma_multivariate_gaussian(
    x:Expression<Real[_]>, A:Expression<Real[_,_]>, ν:Expression<Real[_]>,
    Λ:Expression<Real[_,_]>, c:Expression<Real[_]>, α:Expression<Real>,
    γ:Expression<Real>) -> (Expression<Real[_]>, Expression<Real[_,_]>,
    Expression<Real>, Expression<Real>) {
  let D <- length(x);
  let Λ' <- rank_update(Λ, transpose(A));
  let ν' <- ν + transpose(A)*(x - c);
  let α' <- α + 0.5*D;
  let γ' <- γ + 0.5*dot(x - c);
  return (box(ν'), box(Λ'), box(α'), box(γ'));
}

/*
 * Update the parameters of a normal inverse-gamma distribution with a
 * linear transformation involving a dot product, and Gaussian likelihood.
 *
 * - x: The variate.
 * - A: Scale.
 * - ν: Prior precision times mean.
 * - Λ: Prior precision.
 * - c: Offset.
 * - α: Prior shape of the inverse-gamma.
 * - γ: Prior scale accumulator.
 *
 * Returns: the posterior hyperparameters `μ'`, `Λ'`, `γ'`, `α'` and `β'`.
 */
function update_lazy_linear_multivariate_normal_inverse_gamma_gaussian(
    x:Expression<Real>, a:Expression<Real[_]>, ν:Expression<Real[_]>,
    Λ:Expression<Real[_,_]>, c:Expression<Real>, α:Expression<Real>,
    γ:Expression<Real>) -> (Expression<Real[_]>, Expression<Real[_,_]>,
    Expression<Real>, Expression<Real>) {
  let Λ' <- rank_update(Λ, a);
  let ν' <- ν + a*(x - c);
  let α' <- α + 0.5;
  let γ' <- γ + 0.5*pow(x - c, 2.0);
  return (box(ν'), box(Λ'), box(α'), box(γ'));
}

/*
 * Update the parameters of a matrix normal-inverse-Wishart variate.
 *
 * - X: The variate.
 * - N: Precision times mean.
 * - Λ: Precision.
 * - V: Prior variance shape.
 * - k: Prior degrees of freedom.
 *
 * Returns: the posterior hyperparameters `V'` and `k'`.
 */
function update_lazy_matrix_normal_inverse_wishart(X:Expression<Real[_,_]>,
    N:Expression<Real[_,_]>, Λ:Expression<Real[_,_]>, V:Expression<Real[_,_]>,
    k:Expression<Real>) -> (Expression<Real[_,_]>, Expression<Real>) {
  let n <- rows(X);
  let M <- solve(Λ, N);
  let V' <- V + transpose(X - M)*Λ*(X - M);
  let k' <- k + n;
  return (box(V'), box(k'));
}

/*
 * Update the parameters of a Gaussian variate with
 * matrix-normal-inverse-Wishart prior.
 *
 * - X: The variate.
 * - N: Prior precision times mean matrix.
 * - Λ: Prior precision.
 * - V: Prior variance shape.
 * - k: Prior degrees of freedom.
 *
 * Returns: the posterior hyperparameters `N'`, `Λ'`, `V'` and `k'`.
 */
function update_lazy_matrix_normal_inverse_wishart_matrix_gaussian(
    X:Expression<Real[_,_]>, N:Expression<Real[_,_]>, Λ:Expression<Real[_,_]>,
    V:Expression<Real[_,_]>, k:Expression<Real>) -> (Expression<Real[_,_]>,
    Expression<Real[_,_]>, Expression<Real[_,_]>, Expression<Real>) {
  let D <- rows(X);
  let Λ' <- rank_update(Λ, identity(rows(N)));
  let N' <- N + X;
  let M <- solve(Λ, N);
  let M' <- solve(Λ', N');
  let V' <- V + transpose(X)*X + transpose(M)*N - transpose(M')*N';
  let k' <- k + D;
  return (box(N'), box(Λ'), box(V'), box(k'));
}

/*
 * Update the parameters of a Gaussian variate with linear transformation
 * of matrix-normal-inverse-Wishart prior.
 *
 * - X: The variate.
 * - A: Scale.
 * - N: Prior precision times mean matrix.
 * - Λ: Prior precision.
 * - C: Offset.
 * - V: Prior variance shape.
 * - k: Prior degrees of freedom.
 *
 * Returns: the posterior hyperparameters `N'`, `Λ'`, `V'` and `k'`.
 */
function update_lazy_linear_matrix_normal_inverse_wishart_matrix_gaussian(
    X:Expression<Real[_,_]>, A:Expression<Real[_,_]>,
    N:Expression<Real[_,_]>, Λ:Expression<Real[_,_]>, C:Expression<Real[_,_]>,
    V:Expression<Real[_,_]>, k:Expression<Real>) -> (Expression<Real[_,_]>,
    Expression<Real[_,_]>, Expression<Real[_,_]>, Expression<Real>) {
  let D <- rows(X);
  let Λ' <- rank_update(Λ, transpose(A));
  let N' <- N + transpose(A)*(X - C);
  let M <- solve(Λ, N);
  let M' <- solve(Λ', N');
  let V' <- V + transpose(X - C)*(X - C) + transpose(M)*N - transpose(M')*N';
  let k' <- k + D;
  return (box(N'), box(Λ'), box(V'), box(k'));
}

/*
 * Update the parameters of a Gaussian variate with linear transformation
 * of matrix-normal-inverse-Wishart prior.
 *
 * - x: The variate.
 * - a: Scale.
 * - N: Prior precision times mean matrix.
 * - Λ: Prior precision.
 * - c: Offset.
 * - V: Prior variance shape.
 * - k: Prior degrees of freedom.
 *
 * Returns: the posterior hyperparameters `N'`, `Λ'`, `V'` and `k'`.
 */
function update_lazy_linear_matrix_normal_inverse_wishart_multivariate_gaussian(
    x:Expression<Real[_]>, a:Expression<Real[_]>, N:Expression<Real[_,_]>,
    Λ:Expression<Real[_,_]>, c:Expression<Real[_]>, V:Expression<Real[_,_]>,
    k:Expression<Real>) -> (Expression<Real[_,_]>, Expression<Real[_,_]>,
    Expression<Real[_,_]>, Expression<Real>) {
  let Λ' <- rank_update(Λ, a);
  let N' <- N + outer(a, x - c);
  let M <- solve(Λ, N);
  let M' <- solve(Λ', N');
  let V' <- V + outer(x - c) + transpose(M)*N - transpose(M')*N';
  let k' <- k + 1.0;
  return (box(N'), box(Λ'), box(V'), box(k'));
}
