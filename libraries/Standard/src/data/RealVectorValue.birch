/**
 * Real vector value.
 */
final class RealVectorValue(value:Real[_]) < Value {
  /**
   * The value.
   */
  value:Real[_] <- value;

  override function accept(writer:Writer) {
    writer.visit(this);
  }

  override function size() -> Integer {
    return length(value);
  }
  
  override function getRealVector() -> Real[_]? {
    return value;
  }

  override function getRealMatrix() -> Real[_,_]? {
    return column(value);
  }

  override function pushNil() -> Value {
    let o <- ArrayValue();
    for i in 1..length(value) {
      o.push(value[i]);
    }
    o.pushNil();
    return o;
  }

  override function push(x:Boolean) -> Value {
    return push(Real(x));
  }

  override function push(x:Integer) -> Value {
    return push(Real(x));
  }

  override function push(x:Real) -> Value {
    cpp{{
    value.insert(value.size(), x);
    }}
    return this;
  }

  override function push(x:String) -> Value {
    let o <- ArrayValue();
    for i in 1..length(value) {
      o.push(value[i]);
    }
    o.push(x);
    return o;
  }

  override function push(x:Object) -> Value {
    let o <- ArrayValue();
    for i in 1..length(value) {
      o.push(value[i]);
    }
    o.push(x);
    return o;
  }

  override function push(x:Boolean[_]) -> Value {
    let o <- ArrayValue();
    o.push(value);
    o.push(x);
    return o;
  }

  override function push(x:Integer[_]) -> Value {
    let o <- ArrayValue();
    o.push(value);
    o.push(x);
    return o;
  }

  override function push(x:Real[_]) -> Value {
    let o <- ArrayValue();
    o.push(value);
    o.push(x);
    return o;
  }
}

/**
 * Create a RealVectorValue.
 */
function RealVectorValue(value:Real[_]) -> RealVectorValue {
  return construct<RealVectorValue>(value);
}
