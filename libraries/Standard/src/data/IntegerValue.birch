/**
 * Integer value.
 */
final class IntegerValue(value:Integer) < Value {
  /**
   * The value.
   */
  value:Integer <- value;
  
  override function accept(writer:Writer) {
    writer.visit(this);
  }

  override function getInteger() -> Integer? {
    return value;
  }

  override function getReal() -> Real? {
    return Real(value);
  }

  override function getIntegerVector() -> Integer[_]? {
    return vector(value, 1);
  }

  override function getRealVector() -> Real[_]? {
    return vector(Real(value), 1);
  }

  override function getIntegerMatrix() -> Integer[_,_]? {
    return matrix(value, 1, 1);
  }

  override function getRealMatrix() -> Real[_,_]? {
    return matrix(Real(value), 1, 1);
  }

  override function pushNil() -> Value {
    let o <- ArrayValue();
    o.push(value);
    o.pushNil();
    return o;
  }

  override function push(x:Boolean) -> Value {
    return IntegerVectorValue([value, Integer(x)]);
  }

  override function push(x:Integer) -> Value {
    return IntegerVectorValue([value, x]);
  }

  override function push(x:Real) -> Value {
    return RealVectorValue([Real(value), x]);
  }

  override function push(x:String) -> Value {
    let o <- ArrayValue();
    o.push(value);
    o.push(x);
    return o;
  }

  override function push(x:Object) -> Value {
    let o <- ArrayValue();
    o.push(value);
    o.push(x);
    return o;
  }

  override function push(x:Boolean[_]) -> Value {
    let o <- ArrayValue();
    o.push(value);
    o.push(x);
    return o;
  }

  override function push(x:Integer[_]) -> Value {
    let o <- ArrayValue();
    o.push(value);
    o.push(x);
    return o;
  }

  override function push(x:Real[_]) -> Value {
    let o <- ArrayValue();
    o.push(value);
    o.push(x);
    return o;
  }
}

/**
 * Create an IntegerValue.
 */
function IntegerValue(value:Integer) -> IntegerValue {
  return construct<IntegerValue>(value);
}
