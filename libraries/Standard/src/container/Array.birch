/**
 * Resizeable array with constant-time random access.
 *
 * - Type: Element type. Must be default-constructible.
 */
final class Array<Type> {
  /**
   * Elements.
   */
  values:Type[_];

  /**
   * Number of elements.
   */
  function size() -> Integer {
    return length(values);
  }

  /**
   * Is this empty?
   */
  function empty() -> Boolean {
    return size() == 0;
  }

  /**
   * Clear all elements.
   */
  function clear() {
    values:Type[_];
    this.values <- values;
  }
  
  /**
   * Get an element.
   *
   * - i: Position.
   */
  function get(i:Integer) -> Type {
    return values[i];
  }

  /**
   * Set an element.
   *
   * - i: Position.
   * - x: Value.
   */
  function set(i:Integer, x:Type) {
    values[i] <- x;
  }

  /**
   * Get the first element.
   */
  function front() -> Type {
    assert size() > 0;
    return values[1];
  }

  /**
   * Get the last element.
   */
  function back() -> Type {
    assert size() > 0;
    return values[size()];
  }

  /**
   * Insert an element at the front.
   *
   * - x: Value.
   */
  function pushFront(x:Type) {
    insert(1, x);
  }

  /**
   * Insert a new default-constructed element at the front and return it.
   */
  function pushFront() -> Type {
    let x <- make<Type>();
    if x? {
      pushFront(x!);
    } else {
      error("not default constructible");
    }
    return x!;
  }

  /**
   * Insert an element at the back.
   *
   * - x: Value.
   */
  function pushBack(x:Type) {
    insert(size() + 1, x);
  }

  /**
   * Insert a new default-constructed element at the back and return it.
   */
  function pushBack() -> Type {
    let x <- make<Type>();
    if x? {
      pushBack(x!);
    } else {
      error("not default constructible");
    }
    return x!;
  }

  /**
   * Remove the first element.
   */
  function popFront() {
    erase(1);
  }

  /**
   * Remove the last element.
   */
  function popBack() {
    erase(size());
  }
  
  /**
   * Insert a new element.
   *
   * - i: Position.
   * - x: Value.
   *
   * Inserts the new element immediately before the current element at
   * position `i`. To insert at the back of the container, use a position that
   * is one more than the current size, or `pushBack()`.
   */
  function insert(i:Integer, x:Type) {
    assert 1 <= i && i <= size() + 1;
    cpp{{
    this->values.insert(i - 1, x);
    }}
  }

  /**
   * Erase an element.
   *
   * - i: Position.
   *
   * The size decreases by one.
   */
  function erase(i:Integer) {
    assert 1 <= i && i <= size();
    cpp{{
    this->values.erase(i - 1);
    }}
  }

  /**
   * Erase multiple elements.
   *
   * - i: Position.
   * - n: Number of elements.
   *
   * The size decreases by `n`.
   */
  function erase(i:Integer, n:Integer) {
    assert 1 <= i && i <= size();
    assert 1 <= n && n <= size() - i + 1;
    cpp{{
    this->values.erase(i - 1, n);
    }}
  }

  /**
   * Obtain an iterator.
   *
   * Return: an iterator across elements in forward order.
   */
  function walk() -> Iterator<Type> {
    return ArrayIterator<Type>(this);
  }

  /**
   * Convert to array.
   */
  function toArray() -> Type[_] {
    return values;
  }
  
  /**
   * Convert from array.
   */
  function fromArray(x:Type[_]) {
    values <- x;
  }

  function read(buffer:Buffer) {
    clear();
    values <-? buffer.get<Type[_]>();
  }

  function write(buffer:Buffer) {
    let f <- walk();
    while f.hasNext() {
      buffer.push(f.next());
    }
  }
}
