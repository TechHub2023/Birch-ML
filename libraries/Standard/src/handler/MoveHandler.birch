/**
 * Event handler for MoveParticleFilter.
 *
 * - delayed: Enable delayed sampling?
 *
 * ```mermaid
 * classDiagram
 *    Handler <|-- PlayHandler
 *    Handler <|-- MoveHandler
 *    link Handler "../Handler/"
 *    link PlayHandler "../PlayHandler/"
 *    link MoveHandler "../MoveHandler/"
 * ```
 */
class MoveHandler(delayed:Boolean) < Handler {
  /**
   * Is delayed sampling enabled?
   */
  delayed:Boolean <- delayed;
  
  /**
   * Deferred log-likelihood.
   */
  z:Expression<Real>?;

  final override function doHandle(event:Event) {
    /* double dispatch to one of the more specific doHandle() functions */
    event.accept(this);
  }

  final override function doHandle(record:Record, event:Event) {
    /* double dispatch to one of the more specific doHandle() functions */
    event.accept(record, this);
  }

  function doHandle<Value>(event:SimulateEvent<Value>) {
    event.x <- event.p.value();
  }

  function doHandle<Value>(event:ObserveEvent<Value>) {
    let v <- event.p.observeLazy(box(event.x));
    if v? {
      if z? {
        z <- z! + v!;
      } else {
        z <- v;
      }
    } else {
      /* lazy observe not supported for this distribution type */
      w <- w + event.p.observe(event.x);
    }
  }

  function doHandle<Value>(event:AssumeEvent<Value>) {
    if event.x.hasValue() {
      let v <- event.p.observeLazy(event.x);
      if v? {
        if z? {
          z <- z! + v!;
        } else {
          z <- v;
        }
      } else {
        /* lazy observe not supported for this distribution type */
        w <- w + event.p.observe(event.x.value());
      }
    } else {
      event.x.assume(event.p);
    }
  }

  function doHandle(event:FactorEvent) {
    if z? {
      z <- z! + event.w;
    } else {
      z <- event.w;
    }
  }

  function doHandle<Value>(record:SimulateRecord<Value>,
      event:SimulateEvent<Value>) {
    event.x <- record.x;
  }

  function doHandle<Value>(record:ObserveRecord<Value>,
      event:ObserveEvent<Value>) {
    /* observe events are replayed in the same way they are played, it's
     * only necessary to check that the observed values actually match */
    assert record.x == event.x;
    doHandle(event);
  }

  function doHandle<Value>(record:AssumeRecord<Value>,
      event:AssumeEvent<Value>) {
    if event.x.hasValue() {
      /* assume events with a value already assigned are replayed in the
       * same way they are played, it's only necessary to check that the
       * observed values actually match */
      assert record.x.hasValue() && record.x.value() == event.x.value();
      let v <- event.p.observeLazy(event.x);
      if v? {
        if z? {
          z <- z! + v!;
        } else {
          z <- v;
        }
      } else {
        /* lazy observe not supported for this distribution type */
        w <- w + event.p.observe(event.x.value());
      }
    } else {
      event.x.assume(event.p);    
      if record.x.hasValue() {
        /* if the record has a value, we can set it now, even if its
         * simulation was delayed when originally played; such delays do not
         * change the distribution, only the way it is computed */
        event.x <- record.x.value();
      }
    }
  }

  function doHandle(record:FactorRecord, event:FactorEvent) {
    /* factor events are replayed in the same way they are played */
    doHandle(event);
  }
}

/**
 * Create a MoveHandler.
 */
function MoveHandler(delayed:Boolean) -> MoveHandler {
  return construct<MoveHandler>(delayed);
}
