/*
 * Test a particular conjugacy.
 *
 * - m: Model.
 * - N: Number of samples.
 * - D: Number of dimensions.
 * - lazy: Use lazy version?
 */
function test_conjugacy<Conjugate>(m:Conjugate, N:Integer, D:Integer, lazy:Boolean) {  
  let forward <- construct<Handler>(false, false);
  let backward <- construct<Handler>(true, false);
  m.initialize();
  
  /* simulate forward */
  X1:Real[N,D];
  parallel for n in 1..N {
    let m' <- copy2(m);
    with forward {
      m'.simulate();
    }
    if lazy {
      X1[n,1..D] <- m'.forwardLazy();
    } else {
      X1[n,1..D] <- m'.forward();
    }
  }

  /* simulate backward */
  X2:Real[N,D];
  parallel for n in 1..N {
    let m' <- copy2(m);
    with backward {
      m'.simulate();
    }
    if lazy {
      X2[n,1..D] <- m'.backwardLazy();
    } else {
      X2[n,1..D] <- m'.backward();
    }
  }
  
  /* test result */
  if !pass(X1, X2) {
    exit(1);
  }
}
