class TestBetaGeometric < TestModel {
  ρ:Random<Real>;
  x:Random<Integer>;
  α:Real;
  β:Real;

  function initialize() {
    α <- simulate_uniform(1.0, 20.0);
    β <- simulate_uniform(1.0, 20.0);
  }

  function simulate() {
    ρ ~ Beta(α, β);
    x ~ Geometric(ρ);
  }

  function forward() -> Real[_] {
    y:Real[2];
    y[1] <- ρ.eval();
    y[2] <- x.eval();
    return y;
  }

  function backward() -> Real[_] {
    y:Real[2];
    assert !x.hasValue();
    y[2] <- x.eval();
    assert !ρ.hasValue();
    y[1] <- ρ.eval();
    return y;
  }

  function marginal() -> Distribution<Integer> {
    return x.getDistribution();
  }

  function size() -> Integer {
    return 2;
  }
}

program test_conjugacy_beta_geometric(N:Integer <- 10000, lazy:Boolean <- false) {
  m:TestBetaGeometric;
  test_conjugacy(m, N, lazy);
}

program test_pdf_beta_geometric(N:Integer <- 10000, B:Integer <- 0,
    S:Integer <- 0, lazy:Boolean <- false) {
  m:TestBetaGeometric;
  m.initialize();
  m.simulate();
  test_pdf(m.marginal(), N, lazy);
}

program test_grad_beta_geometric(N:Integer <- 1000,
    backward:Boolean <- false) {
  m:TestBetaGeometric;
  test_grad(m, N, backward);
}
