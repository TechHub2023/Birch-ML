class TestBetaBinomial < TestModel {
  ρ:Random<Real>;
  x:Random<Integer>;
  n:Integer;
  α:Real;
  β:Real;

  function initialize() {
    n <- simulate_uniform_int(1, 100);
    α <- simulate_uniform(1.0, 20.0);
    β <- simulate_uniform(1.0, 20.0);
  }

  function simulate() {
    ρ ~ Beta(α, β);
    x ~ Binomial(n, ρ);
  }

  function forward() -> Real[_] {
    y:Real[2];
    y[1] <- ρ.value();
    y[2] <- x.value();
    return y;
  }

  function backward() -> Real[_] {
    y:Real[2];
    assert !x.hasValue();
    y[2] <- x.value();
    assert !ρ.hasValue();
    y[1] <- ρ.value();
    return y;
  }

  function forwardLazy() -> Real[_] {
    y:Real[2];
    y[1] <- ρ.eval();
    y[2] <- x.eval();
    return y;
  }

  function backwardLazy() -> Real[_] {
    y:Real[2];
    assert !x.hasValue();
    y[2] <- x.eval();
    assert !ρ.hasValue();
    y[1] <- ρ.eval();
    return y;
  }

  function marginal() -> Distribution<Integer> {
    return x.getDistribution();
  }

  function size() -> Integer {
    return 2;
  }
}

program test_conjugacy_beta_binomial(N:Integer <- 10000, lazy:Boolean <- false) {
  m:TestBetaBinomial;
  test_conjugacy(m, N, lazy);
}

program test_pdf_beta_binomial(N:Integer <- 10000, B:Integer <- 0,
    S:Integer <- 0, lazy:Boolean <- false) {
  m:TestBetaBinomial;
  m.initialize();
  m.simulate();
  test_pdf(m.marginal(), N, lazy);
}

program test_cdf_beta_binomial() {
  m:TestBetaBinomial;
  m.initialize();
  m.simulate();
  test_cdf(m.marginal());
}
