class TestUniform < TestModel {
  x:Random<Real>;
  l:Random<Real>;
  u:Random<Real>;

  function initialize() {
    l ~ Uniform(-10.0, 10.0);
    u ~ Uniform(l, l + 20.0);
  }

  function simulate() {
    x ~ Uniform(l, u);
  }

  function forward() -> Real[_] {
    return [x.eval()];
  }

  function backward() -> Real[_] {
    return [x.eval()];
  }

  function marginal() -> Distribution<Real> {
    return x.getDistribution();
  }

  function size() -> Integer {
    return 1;
  }
}

program test_pdf_uniform(N:Integer <- 10000, B:Integer <- 100, S:Integer <- 5,
    lazy:Boolean <- false) {
  m:TestUniform;
  m.initialize();
  m.simulate();
  test_pdf(m.marginal(), N, B, S, lazy);
}

program test_cdf_uniform(N:Integer <- 10000) {
  m:TestUniform;
  m.initialize();
  m.simulate();
  test_cdf(m.marginal(), N);
}

program test_grad_uniform(N:Integer <- 1000, backward:Boolean <- false) {
  m:TestUniform;
  test_grad(m, N, backward);
}
