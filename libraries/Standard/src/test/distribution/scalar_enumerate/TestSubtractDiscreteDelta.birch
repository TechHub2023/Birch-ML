class TestSubtractDiscreteDelta < TestModel {
  x1:Random<Integer>;
  x2:Random<Integer>;
  s:Random<Integer>;

  function initialize() {
    //
  }

  function simulate() {
    x1 ~ UniformInteger(1, 100);
    x2 ~ Binomial(100, 0.25);
    s ~ Delta(x1 - x2);
  }

  function forward() -> Real[_] {
    y:Real[size()];
    y[1] <- x1.value();
    y[2] <- x2.value();
    y[3] <- s.value();
    return y;
  }

  function backward() -> Real[_] {
    y:Real[size()];
    y[3] <- s.value();
    assert !x1.hasValue();
    assert !x2.hasValue();
    y[2] <- x2.value();
    y[1] <- x1.value();
    return y;
  }

  function forwardLazy() -> Real[_] {
    y:Real[size()];
    y[1] <- x1.eval();
    y[2] <- x2.eval();
    y[3] <- s.eval();
    return y;
  }

  function backwardLazy() -> Real[_] {
    y:Real[size()];
    y[3] <- s.eval();
    assert !x1.hasValue();
    assert !x2.hasValue();
    y[2] <- x2.eval();
    y[1] <- x1.eval();
    return y;
  }

  function marginal() -> Distribution<Integer> {
    return s.getDistribution();
  }

  function size() -> Integer {
    return 3;
  }
}

program test_conjugacy_subtract_discrete_delta(N:Integer <- 10000,
    lazy:Boolean <- false) {
  m:TestSubtractDiscreteDelta;
  test_conjugacy(m, N, lazy);
}

program test_pdf_subtract_discrete_delta(N:Integer <- 10000, B:Integer <- 0,
    S:Integer <- 0, lazy:Boolean <- false) {
  m:TestSubtractDiscreteDelta;
  m.initialize();
  m.simulate();
  test_pdf(m.marginal(), N, lazy);
}
