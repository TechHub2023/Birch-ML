class TestMultivariateNormalInverseGamma < TestModel {
  σ2:Random<Real>;
  μ:Random<Real[_]>;

  n:Integer <- 4;

  α:Real;
  β:Real;
  μ_0:Real[n];
  Σ:Real[n,n];

  function initialize() {
    α <- simulate_uniform(2.0, 10.0);
    β <- simulate_uniform(0.1, 10.0);
    μ_0 <- vector_lambda(\(i:Integer) -> { return simulate_uniform(-10.0, 10.0); }, n);
    Σ <- matrix_lambda(\(i:Integer, j:Integer) -> { return simulate_uniform(-2.0, 2.0); }, n, n);
    
    Σ <- outer(Σ) + diagonal(1.0e-2, n);
  }

  function simulate() {
    σ2 ~ InverseGamma(α, β);
    μ ~ MultivariateGaussian(μ_0, Σ*σ2);
  }

  function forward() -> Real[_] {
    y:Real[size()];
    y[1] <- σ2.eval();
    y[2..(n + 1)] <- μ.eval();
    return y;
  }

  function backward() -> Real[_] {
    y:Real[size()];
    assert !μ.hasValue();
    y[2..(n + 1)] <- μ.eval();
    assert !σ2.hasValue();
    y[1] <- σ2.eval();
    return y;
  }

  function marginal() -> Distribution<Real[_]> {
    return μ.getDistribution();
  }

  function size() -> Integer {
    return n + 1;
  }
}

program test_conjugacy_multivariate_normal_inverse_gamma(N:Integer <- 10000,
    lazy:Boolean <- false) {
  m:TestMultivariateNormalInverseGamma;
  test_conjugacy(m, N, lazy);
}

program test_pdf_linear_multivariate_normal_inverse_gamma(N:Integer <- 10000,
    B:Integer <- 1000, S:Integer <- 20, lazy:Boolean <- false) {
  m:TestMultivariateNormalInverseGamma;
  m.initialize();
  m.simulate();
  test_pdf(m.marginal(), N, B, S, lazy);
}

program test_grad_multivariate_normal_inverse_gamma(N:Integer <- 1000, backward:Boolean <- false) {
  m:TestMultivariateNormalInverseGamma;
  test_grad(m, N, backward);
}
