class TestLinearMultivariateGaussianGaussian < Model {
  μ:Random<Real[_]>;
  x:Random<Real>;

  a:Real[5];
  μ_0:Real[5];
  Σ_0:Real[5,5];
  c:Real;
  σ2_1:Real;

  function initialize() {
    c <- simulate_uniform(-10.0, 10.0);
    for i in 1..5 {
      a[i] <- simulate_uniform(-2.0, 2.0);
      μ_0[i] <- simulate_uniform(-10.0, 10.0);
      for j in 1..5 {
        Σ_0[i,j] <- simulate_uniform(-2.0, 2.0);
      }
    }
    Σ_0 <- Σ_0*transpose(Σ_0) + diagonal(1.0e-2, 5);
    σ2_1 <- simulate_uniform(0.1, 10.0);
  }

  function simulate() {
    μ ~ Gaussian(μ_0, Σ_0);
    x ~ Gaussian(dot(a, μ) + c, σ2_1);
  }

  function forward() -> Real[_] {
    y:Real[6];
    y[1..5] <- μ.value();
    y[6] <- x.value();
    return y;
  }

  function backward() -> Real[_] {
    y:Real[6];
    assert !x.hasValue();
    y[6] <- x.value();
    assert !μ.hasValue();
    y[1..5] <- μ.value();
    return y;
  }

  function forwardLazy() -> Real[_] {
    y:Real[6];
    y[1..5] <- μ.eval();
    y[6] <- x.eval();
    return y;
  }

  function backwardLazy() -> Real[_] {
    y:Real[6];
    assert !x.hasValue();
    y[6] <- x.eval();
    assert !μ.hasValue();
    y[1..5] <- μ.eval();
    return y;
  }

  function marginal() -> Distribution<Real> {
    return x.getDistribution();
  }
}
