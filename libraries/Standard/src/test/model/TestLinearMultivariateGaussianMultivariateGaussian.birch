class TestLinearMultivariateGaussianMultivariateGaussian < Model {
  μ:Random<Real[_]>;
  x:Random<Real[_]>;

  n:Integer <- 4;

  A:Real[n,n];
  μ_0:Real[n];
  Σ_0:Real[n,n];
  c:Real[n];
  Σ_1:Real[n,n];
  neg:Boolean;

  function initialize() {
    for i in 1..n {
      μ_0[i] <- simulate_uniform(-10.0, 10.0);
      c[i] <- simulate_uniform(-10.0, 10.0);
      for j in 1..n {
        Σ_0[i,j] <- simulate_uniform(-2.0, 2.0);
        Σ_1[i,j] <- simulate_uniform(-2.0, 2.0);
        A[i,j] <- simulate_uniform(-2.0, 2.0);
      }
    }
    Σ_0 <- outer(Σ_0) + diagonal(1.0e-2, n);
    Σ_1 <- outer(Σ_1) + diagonal(1.0e-2, n);
    neg <- simulate_bernoulli(0.5);
  }

  function simulate() {
    μ ~ MultivariateGaussian(μ_0, Σ_0);
    if neg {
      x ~ MultivariateGaussian(A*μ - c, Σ_1);
    } else {
      x ~ MultivariateGaussian(A*μ + c, Σ_1);
    }
  }

  function forward() -> Real[_] {
    y:Real[2*n];
    y[1..n] <- μ.value();
    y[(n + 1)..2*n] <- x.value();
    return y;
  }

  function backward() -> Real[_] {
    y:Real[2*n];
    assert !x.hasValue();
    y[(n + 1)..2*n] <- x.value();
    assert !μ.hasValue();
    y[1..n] <- μ.value();
    return y;
  }

  function forwardLazy() -> Real[_] {
    y:Real[2*n];
    y[1..n] <- μ.eval();
    y[(n + 1)..2*n] <- x.eval();
    return y;
  }

  function backwardLazy() -> Real[_] {
    y:Real[2*n];
    assert !x.hasValue();
    y[(n + 1)..2*n] <- x.eval();
    assert !μ.hasValue();
    y[1..n] <- μ.eval();
    return y;
  }

  function marginal() -> Distribution<Real[_]> {
    return x.getDistribution();
  }

  function size() -> Integer {
    return 2*n;
  }
}
