class TestMultivariateNormalInverseGammaMultivariateGaussian < Model {
  σ2:Random<Real>;
  μ:Random<Real[_]>;
  x:Random<Real[_]>;

  μ_0:Real[5];
  A:Real[5,5];
  α:Real;
  β:Real;

  function initialize() {
    α <- simulate_uniform(2.0, 10.0);
    β <- simulate_uniform(0.1, 10.0);
    for i in 1..5 {
      μ_0[i] <- simulate_uniform(-10.0, 10.0);
      for j in 1..5 {
        A[i,j] <- simulate_uniform(-2.0, 2.0);
      }
    }
    A <- A*transpose(A) + diagonal(1.0e-2, 5);
  }

  function simulate() {
    σ2 ~ InverseGamma(α, β);
    μ ~ MultivariateGaussian(μ_0, A*σ2);
    x ~ MultivariateGaussian(μ, identity(length(μ))*σ2);
  }

  function forward() -> Real[_] {
    y:Real[11];
    y[1] <- σ2.value();
    y[2..6] <- μ.value();
    y[7..11] <- x.value();
    return y;
  }

  function backward() -> Real[_] {
    y:Real[11];
    assert !x.hasValue();
    y[7..11] <- x.value();
    assert !μ.hasValue();
    y[2..6] <- μ.value();
    assert !σ2.hasValue();
    y[1] <- σ2.value();
    return y;
  }

  function forwardLazy() -> Real[_] {
    y:Real[11];
    y[1] <- σ2.eval();
    y[2..6] <- μ.eval();
    y[7..11] <- x.eval();
    return y;
  }

  function backwardLazy() -> Real[_] {
    y:Real[11];
    assert !x.hasValue();
    y[7..11] <- x.eval();
    assert !μ.hasValue();
    y[2..6] <- μ.eval();
    assert !σ2.hasValue();
    y[1] <- σ2.eval();
    return y;
  }

  function marginal() -> Distribution<Real[_]> {
    return x.getDistribution();
  }
}
