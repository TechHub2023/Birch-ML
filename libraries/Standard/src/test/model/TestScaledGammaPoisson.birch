class TestScaledGammaPoisson < Model {
  λ:Random<Real>;
  x:Random<Integer>;

  a:Real;
  k:Real;
  θ:Real;
  neg:Boolean;

  function initialize() {
    a <- simulate_uniform(0.0, 10.0);
    k <- simulate_uniform_int(1, 10);
    θ <- simulate_uniform(0.0, 10.0);
    neg <- simulate_bernoulli(0.5);
  }

  function simulate() {
    λ ~ Gamma(k, θ);
    if neg {
      x ~ Poisson(λ/a);
    } else {
      x ~ Poisson(a*λ);
    }
  }

  function forward() -> Real[_] {
    y:Real[2];
    y[1] <- λ.value();
    assert !x.hasValue();
    y[2] <- x.value();
    return y;
  }

  function backward() -> Real[_] {
    y:Real[2];
    y[2] <- x.value();
    assert !λ.hasValue();
    y[1] <- λ.value();
    return y;
  }

  function forwardLazy() -> Real[_] {
    y:Real[2];
    y[1] <- λ.get();
    assert !x.hasValue();
    y[2] <- x.get();
    return y;
  }

  function backwardLazy() -> Real[_] {
    y:Real[2];
    y[2] <- x.get();
    assert !λ.hasValue();
    y[1] <- λ.get();
    return y;
  }

  function marginal() -> Distribution<Integer> {
    return x.distribution()!.graft();
  }
}
