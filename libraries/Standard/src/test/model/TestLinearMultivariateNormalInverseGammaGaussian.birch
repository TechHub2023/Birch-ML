class TestLinearMultivariateNormalInverseGammaGaussian < Model {
  σ2:Random<Real>;
  μ:Random<Real[_]>;
  x:Random<Real>;

  a:Real[10];
  μ_0:Real[10];
  Σ:Real[10,10];
  c:Real;
  α:Real;
  β:Real;

  function initialize() {
    α <- simulate_uniform(2.0, 10.0);
    β <- simulate_uniform(0.1, 10.0);
    for i in 1..10 {
      a[i] <- simulate_uniform(-2.0, 2.0);
      μ_0[i] <- simulate_uniform(-10.0, 10.0);
      for j in 1..10 {
        Σ[i,j] <- simulate_uniform(-2.0, 2.0);
      }
    }
    c <- simulate_uniform(-10.0, 10.0);
    Σ <- Σ*transpose(Σ) + diagonal(1.0e-2, 10);
  }

  function simulate() {
    σ2 ~ InverseGamma(α, β);
    μ ~ Gaussian(μ_0, Σ, σ2);
    x ~ Gaussian(dot(a, μ) + c, σ2);
  }

  function forward() -> Real[_] {
    y:Real[12];
    y[1] <- σ2.value();
    y[2..11] <- μ.value();
    y[12] <- x.value();
    return y;
  }

  function backward() -> Real[_] {
    y:Real[12];
    assert !x.hasValue();
    y[12] <- x.value();
    assert !μ.hasValue();
    y[2..11] <- μ.value();
    assert !σ2.hasValue();
    y[1] <- σ2.value();
    return y;
  }

  function forwardLazy() -> Real[_] {
    y:Real[12];
    y[1] <- σ2.eval();
    y[2..11] <- μ.eval();
    y[12] <- x.eval();
    return y;
  }

  function backwardLazy() -> Real[_] {
    y:Real[12];
    assert !x.hasValue();
    y[12] <- x.eval();
    assert !μ.hasValue();
    y[2..11] <- μ.eval();
    assert !σ2.hasValue();
    y[1] <- σ2.eval();
    return y;
  }

  function marginal() -> Distribution<Real> {
    return x.getDistribution();
  }
}
