hpp{{
#include <yaml.h>
}}

/**
 * Writer for YAML files.
 *
 * ```mermaid
 * classDiagram
 *    Writer <|-- YAMLWriter
 *    YAMLWriter <|-- JSONWriter
 *    link Writer "../Writer/"
 *    link YAMLWriter "../YAMLWriter/"
 *    link JSONWriter "../JSONWriter/"
 * ```
 */
class YAMLWriter < Writer {
  /**
   * The file.
   */
  file:File;

  /*
   * Is the file being written sequentially?
   */
  sequential:Boolean <- false;

  hpp{{
  yaml_emitter_t emitter;
  yaml_event_t event;
  }}
  
  override function open(path:String) {
    file <- fopen(path, WRITE);
    cpp{{
    yaml_emitter_initialize(&this->emitter);
    yaml_emitter_set_unicode(&this->emitter, 1);
    yaml_emitter_set_output_file(&this->emitter, this->file);
    yaml_stream_start_event_initialize(&this->event, YAML_UTF8_ENCODING);
    yaml_emitter_emit(&this->emitter, &this->event);
    yaml_document_start_event_initialize(&this->event, NULL, NULL, NULL, 1);
    yaml_emitter_emit(&this->emitter, &this->event);
    }}
  }
  
  override function dump(buffer:Buffer) {
    buffer.accept(this);
  }

  override function push(buffer:Buffer) {
    if !sequential {
      startSequence();
      sequential <- true;
    }
    buffer.accept(this);
  }

  override function flush() {
    cpp{{
    yaml_emitter_flush(&this->emitter);
    }}
    fflush(file);
  }

  override function close() {
    if sequential {
      endSequence();
    }
    cpp{{
    yaml_document_end_event_initialize(&this->event, 1);
    yaml_emitter_emit(&this->emitter, &this->event);
    yaml_stream_end_event_initialize(&this->event);
    yaml_emitter_emit(&this->emitter, &this->event);
    yaml_emitter_delete(&this->emitter);
    }}
    fclose(file);
  }

  override function visit(value:ObjectValue) {
    startMapping();
    let iter <- value.entries.walk();
    while iter.hasNext() {
      let entry <- iter.next();
      scalar(entry.key);
      entry.value.accept(this);
    }
    endMapping();
  }
  
  override function visit(value:ArrayValue) {
    startSequence();
    let iter <- value.walk();
    while iter.hasNext() {
      iter.next().accept(this);
    }
    endSequence();
  }

  override function visit(value:StringValue) {
    scalar(value.value);
  }

  override function visit(value:RealValue) {
    scalar(value.value);
  }

  override function visit(value:IntegerValue) {
    scalar(value.value);
  }

  override function visit(value:BooleanValue) {
    scalar(value.value);
  }

  override function visit(value:NilValue) {
    null();
  }
  
  override function visit(value:BooleanVectorValue) {
    startSequence();
    let v <- value.value;
    for i in 1..length(v) {
      scalar(v[i]);
    }
    endSequence();
  }

  override function visit(value:IntegerVectorValue) {
    startSequence();
    let v <- value.value;
    for i in 1..length(v) {
      scalar(v[i]);
    }
    endSequence();
  }
  
  override function visit(value:RealVectorValue) {
    startSequence();
    let v <- value.value;
    for i in 1..length(v) {
      scalar(v[i]);
    }
    endSequence();
  }
    
  function startMapping() {
    cpp{{
    yaml_mapping_start_event_initialize(&this->event, NULL, NULL, 1,
        YAML_ANY_MAPPING_STYLE);
    yaml_emitter_emit(&this->emitter, &this->event);
    }}
  }
  
  function endMapping() {
    cpp{{
    yaml_mapping_end_event_initialize(&this->event);
    yaml_emitter_emit(&this->emitter, &this->event);
    }}
  }
  
  function startSequence() {
    cpp{{
    yaml_sequence_start_event_initialize(&this->event, NULL, NULL, 1,
        YAML_ANY_SEQUENCE_STYLE);
    yaml_emitter_emit(&this->emitter, &this->event);
    }}
  }
  
  function endSequence() {
    cpp{{
    yaml_sequence_end_event_initialize(&this->event);
    yaml_emitter_emit(&this->emitter, &this->event);
    }}    
  }
  
  function scalar(x:Boolean) {
    let value <- String(x);
    cpp{{
    yaml_scalar_event_initialize(&this->event, NULL, NULL,
        (yaml_char_t*)value.c_str(), value.length(), 1, 1,
        YAML_ANY_SCALAR_STYLE);
    yaml_emitter_emit(&this->emitter, &this->event);
    }}
  }

  function scalar(x:Integer) {
    let value <- String(x);
    cpp{{
    yaml_scalar_event_initialize(&this->event, NULL, NULL,
        (yaml_char_t*)value.c_str(), value.length(), 1, 1,
        YAML_ANY_SCALAR_STYLE);
    yaml_emitter_emit(&this->emitter, &this->event);
    }}
  }

  function scalar(x:Real) {
    /* the literals NaN, Infinity and -Infinity are not correct JSON, but are
     * fine for YAML, are correct JavaScript, and are supported by Python's
     * JSON module (also based on libyaml); so we encode to this */
    value:String;
    if x == inf {
      value <- "Infinity";
    } else if x == -inf {
      value <- "-Infinity";
    } else if isnan(x) {
      value <- "NaN";
    } else {
      value <- String(x);
    }
    cpp{{
    yaml_scalar_event_initialize(&this->event, NULL, NULL,
        (yaml_char_t*)value.c_str(), value.length(), 1, 1,
        YAML_PLAIN_SCALAR_STYLE);
    yaml_emitter_emit(&this->emitter, &this->event);
    }}
  }

  function scalar(value:String) {
    cpp{{
    yaml_scalar_event_initialize(&this->event, NULL, NULL,
        (yaml_char_t*)value.c_str(), value.length(), 1, 1,
        YAML_ANY_SCALAR_STYLE);
    yaml_emitter_emit(&this->emitter, &this->event);
    }}
  }

  function null() {
    cpp{{
    yaml_scalar_event_initialize(&this->event, NULL, NULL,
        (yaml_char_t*)"null", 4, 1, 1,
        YAML_ANY_SCALAR_STYLE);
    yaml_emitter_emit(&this->emitter, &this->event);
    }}
  }
}
