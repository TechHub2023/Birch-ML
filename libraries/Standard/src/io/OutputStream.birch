/**
 * Output stream.
 */
struct OutputStream {
  /**
   * File handle.
   */
  file:File?;

  /**
   * Open file.
   *
   *   - path: Path.
   *   - mode: Mode. 
   */
  function open(path:String, mode:Integer) {
    file <- fopen(path, mode);
  }

  /**
   * Open file with default mode.
   *
   *   - path: Path.
   */
  function open(path:String) {
    open(path, WRITE);
  }

  /**
   * Close file.
   */
  function close() {
    assert file?;
    fclose(file!);
    file <- nil;
  }
  
  /**
   * Flush.
   */
  function flush() {
    assert file?;
    fflush(file!);
  }

  /**
   * Print string.
   */
  function print(value:String) {
    assert file?;
    cpp{{
    ::fprintf(this->file.value(), "%s", value.c_str());
    }}
  }

  /**
   * Print value.
   */
  function print(value:Boolean) {
    print(to_string(value));
  }

  /**
   * Print value.
   */
  function print(value:Integer) {
    print(to_string(value));
  }

  /**
   * Print value.
   */
  function print(value:Real) {
    print(to_string(value));
  }

  /**
   * Print vector.
   */
  function print(value:Boolean[_]) {
    print(to_string(value));
  }

  /**
   * Print vector.
   */
  function print(value:Integer[_]) {
    print(to_string(value));
  }

  /**
   * Print vector.
   */
  function print(value:Real[_]) {
    print(to_string(value));
  }

  /**
   * Print matrix.
   */
  function print(value:Boolean[_,_]) {
    print(to_string(value));
  }

  /**
   * Print matrix.
   */
  function print(value:Integer[_,_]) {
    print(to_string(value));
  }

  /**
   * Print matrix.
   */
  function print(value:Real[_,_]) {
    print(to_string(value));
  }
}

/**
 * Create an output stream for an already-open file.
 */
function OutputStream(file:File) -> OutputStream {
  o:OutputStream;
  o.file <- file;
  return o;
}
