/**
 * Dirichlet-categorical distribution.
 */
final class DirichletCategorical(ρ:Dirichlet) < Distribution<Integer> {
  /**
   * Category probabilities.
   */
  ρ:Dirichlet <- ρ;

  function supportsLazy() -> Boolean {
    return false;
  }

  function simulate() -> Integer {
    return simulate_dirichlet_categorical(ρ.α.value());
  }

//  function simulateLazy() -> Integer? {
//    return simulate_dirichlet_categorical(ρ.α.eval());
//  }
  
  function logpdf(x:Integer) -> Real {
    return logpdf_dirichlet_categorical(x, ρ.α.value());
  }

//  function logpdfLazy(x:Expression<Integer>) -> Expression<Real>? {
//    return logpdf_lazy_dirichlet_categorical(x, ρ.α);
//  }

  function update(x:Integer) {
    ρ.α <- box(update_dirichlet_categorical(x, ρ.α.value()));
  }

//  function updateLazy(x:Expression<Integer>) {
//    ρ.α <- update_lazy_dirichlet_categorical(x, ρ.α);
//  }

  function lower() -> Integer? {
    return 1;
  }

  function upper() -> Integer? {
    return ρ.α.rows();
  }

  function link() {
    ρ.setChild(this);
  }
  
  function unlink() {
    ρ.releaseChild(this);
  }
}

function DirichletCategorical(ρ:Dirichlet) -> DirichletCategorical {
  m:DirichletCategorical(ρ);
  m.link();
  return m;
}

/*
 * Simulate a Dirichlet-categorical distribution.
 *
 * - α: Concentrations.
 */
function simulate_dirichlet_categorical(α:Real[_]) -> Integer {
  return simulate_categorical(simulate_dirichlet(α));
}

/*
 * Observe a Dirichlet-categorical variate.
 *
 * - x: The variate.
 * - α: Concentrations.
 *
 * Returns: the log probability mass.
 */
function logpdf_dirichlet_categorical(x:Integer, α:Real[_]) -> Real {
  if (1 <= x && x <= length(α)) {
    A:Real <- sum(α);
    return lgamma(1.0 + α[x]) - lgamma(α[x]) + lgamma(A) - lgamma(1.0 + A);
  } else {
    return -inf;
  }
}

/*
 * Update the parameters of a Dirichlet distribution with a categorical
 * likelihood.
 *
 * - x: The variate.
 * - α: Prior concentrations.
 *
 * Returns: the posterior hyperparameters `α'`.
 */
function update_dirichlet_categorical(x:Integer, α:Real[_]) -> Real[_] {
  let α' <- α;
  α'[x] <- α'[x] + 1.0;
  return α';
}
