/**
 * Bernoulli distribution.
 */
final class Bernoulli<Arg>(ρ:Arg) < Distribution<Boolean> {
  /**
   * Success probability.
   */
  ρ:Arg <- ρ;

  override function supportsLazy() -> Boolean {
    return true;
  }

  override function simulate() -> Boolean {
    return simulate_bernoulli(value(ρ));
  }
  
  override function simulateLazy() -> Boolean? {
    return simulate_bernoulli(peek(ρ));
  }
  
  override function logpdf(x:Boolean) -> Real {
    return logpdf_bernoulli(x, value(ρ));
  }

  override function logpdfLazy(x:Expression<Boolean>) -> Expression<Real>? {
    return box(logpdf_bernoulli(x, ρ));
  }

  override function write(buffer:Buffer) {
    buffer.set("class", "Bernoulli");
    buffer.set("ρ", value(ρ));
  }
}

/**
 * Create Bernoulli distribution.
 */
function Bernoulli<Arg>(ρ:Arg) -> {
  return construct<Bernoulli<Arg>>(ρ);
}

/*
 * Simulate a Bernoulli distribution.
 *
 * - ρ: Probability of a true result.
 */
function simulate_bernoulli(ρ:Real) -> Boolean {
  assert 0.0 <= ρ && ρ <= 1.0;
  cpp{{
  return std::bernoulli_distribution(ρ)(rng);
  }}
}

/*
 * Observe a Bernoulli variate.
 *
 * - x: The variate.
 * - ρ: Probability of a true result.
 *
 * Returns: the log probability mass.
 */
function logpdf_bernoulli<Arg1,Arg2>(x:Arg1, ρ:Arg2) -> {
  return if_then_else(x, log(ρ), log1p(-ρ));
}
