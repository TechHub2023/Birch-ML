/**
 * Inverse Wishart distribution.
 *
 * This is typically used to establish a conjugate prior for a Bayesian
 * multivariate linear regression:
 *
 * $$\begin{align*}
 * \boldsymbol{\Sigma} &\sim \mathcal{W}^{-1}(\boldsymbol{\Psi}, \nu) \\
 * \mathbf{W} &\sim \mathcal{MN}(\mathbf{M}, \mathbf{A}, \boldsymbol{\Sigma}) \\
 * \mathbf{Y} &\sim \mathcal{N}(\mathbf{X}\mathbf{W}, \boldsymbol{\Sigma}),
 * \end{align*}$$
 *
 * where $\mathbf{X}$ are inputs and $\mathbf{Y}$ are outputs.
 *
 * The relationship is established in code as follows:
 *
 *     V:Random<Real[_,_]>;
 *     Ψ:Real[_,_];
 *     k:Real;
 *     W:Random<Real[_,_]>;
 *     M:Real[_,_];
 *     U:Real[_,_];
 *     Y:Random<Real[_,_]>;
 *     X:Real[_,_];
 *
 *     V ~ InverseWishart(Ψ, k);
 *     W ~ Gaussian(M, U, V);
 *     Y ~ Gaussian(X*W, V);
 */
final class InverseWishart(Ψ:Expression<Real[_,_]>, k:Expression<Real>) <
    Distribution<Real[_,_]> {
  /**
   * Scale.
   */
  Ψ:Expression<Real[_,_]> <- Ψ;
  
  /**
   * Degrees of freedom.
   */
  k:Expression<Real> <- k;

  function rows() -> Integer {
    return Ψ.rows();
  }

  function columns() -> Integer {
    return Ψ.columns();
  }

  function supportsLazy() -> Boolean {
    return true;
  }

  function simulate() -> Real[_,_] {
    return simulate_inverse_wishart(Ψ.value(), k.value());
  }

  function simulateLazy() -> Real[_,_]? {
    return simulate_inverse_wishart(Ψ.peak(), k.peak());
  }
  
  function logpdf(X:Real[_,_]) -> Real {
    return logpdf_inverse_wishart(X, Ψ.value(), k.value());
  }

  function logpdfLazy(X:Expression<Real[_,_]>) -> Expression<Real>? {
    return logpdf_lazy_inverse_wishart(X, Ψ, k);
  }

  function graftInverseWishart() -> InverseWishart? {
    prune();
    return this;
  }

  function write(buffer:Buffer) {
    prune();
    buffer.set("class", "InverseWishart");
    buffer.set("Ψ", Ψ);
    buffer.set("k", k);
  }
}

/**
 * Create inverse-Wishart distribution.
 */
function InverseWishart<Left,Right>(Ψ:Left, k:Right) -> {
  return construct<InverseWishart>(box(Ψ), box(k));
}

/*
 * Simulate an inverse-Wishart distribution.
 *
 * - Ψ: Scale.
 * - k: Degrees of freedeom.
 */
function simulate_inverse_wishart(Ψ:Real[_,_], k:Real) -> Real[_,_] {
  return inv(simulate_wishart(inv(Ψ), k));
}

/*
 * Observe an inverse Wishart variate.
 *
 * - X: The variate.
 * - Ψ: Scale.
 * - ν: Degrees of freedom.
 *
 * Returns: the log probability density.
 */
function logpdf_inverse_wishart(X:Real[_,_], Ψ:Real[_,_], ν:Real) -> Real {
  let p <- rows(Ψ);
  assert ν > p - 1;
  return -0.5*(ν + p - 1.0)*ldet(X) - 0.5*trace(solve(X, matrix<Real>(Ψ))) -
      0.5*ν*(p*log(2.0) - ldet(Ψ)) - lgamma(0.5*ν, p);
}

/*
 * Observe an inverse Wishart variate.
 *
 * - X: The variate.
 * - Ψ: Scale.
 * - ν: Degrees of freedom.
 *
 * Returns: the log probability density.
 */
function logpdf_lazy_inverse_wishart(X:Expression<Real[_,_]>, Ψ:Expression<Real[_,_]>, ν:Expression<Real>) -> Expression<Real> {
  let p <- rows(Ψ);
  return box(-0.5*(ν + p - 1.0)*ldet(X) - 0.5*trace(solve(X, matrix<Real>(Ψ))) -
      0.5*ν*(p*log(2.0) - ldet(Ψ)) - lgamma(0.5*ν, p));
}
