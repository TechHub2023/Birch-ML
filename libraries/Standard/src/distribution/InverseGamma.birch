/**
 * Inverse-gamma distribution.
 */
final class InverseGamma(α:Expression<Real>, β:Expression<Real>) <
    Distribution<Real> {
  /**
   * Shape.
   */
  α:Expression<Real> <- α;
  
  /**
   * Scale.
   */
  β:Expression<Real> <- β;

  function supportsLazy() -> Boolean {
    return true;
  }

  function simulate() -> Real {
    return simulate_inverse_gamma(α.value(), β.value());
  }

  function simulateLazy() -> Real? {
    return simulate_inverse_gamma(α.eval(), β.eval());
  }
  
  function logpdf(x:Real) -> Real {
    return logpdf_inverse_gamma(x, α.value(), β.value());
  }

  function logpdfLazy(x:Expression<Real>) -> Expression<Real>? {
    return logpdf_lazy_inverse_gamma(x, α, β);
  }

  function cdf(x:Real) -> Real? {
    return cdf_inverse_gamma(x, α.value(), β.value());
  }

  function quantile(P:Real) -> Real? {
    return quantile_inverse_gamma(P, α.value(), β.value());
  }

  function lower() -> Real? {
    return 0.0;
  }
  
  function graftInverseGamma() -> InverseGamma? {
    prune();
    return this;
  }

  function write(buffer:Buffer) {
    prune();
    buffer.set("class", "InverseGamma");
    buffer.set("α", α);
    buffer.set("β", β);
  }
}

/**
 * Create inverse-gamma distribution.
 */
function InverseGamma(α:Expression<Real>, β:Expression<Real>) ->
    InverseGamma {
  return construct<InverseGamma>(α, β);
}

/**
 * Create inverse-gamma distribution.
 */
function InverseGamma(α:Expression<Real>, β:Real) -> InverseGamma {
  return InverseGamma(α, box(β));
}

/**
 * Create inverse-gamma distribution.
 */
function InverseGamma(α:Real, β:Expression<Real>) -> InverseGamma {
  return InverseGamma(box(α), β);
}

/**
 * Create inverse-gamma distribution.
 */
function InverseGamma(α:Real, β:Real) -> InverseGamma {
  return InverseGamma(box(α), box(β));
}
