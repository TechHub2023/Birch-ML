/**
 * Multivariate normal-inverse-gamma-Gaussian distribution.
 */
final class MultivariateNormalInverseGammaMultivariateGaussian(
    μ:MultivariateNormalInverseGamma) < Distribution<Real[_]> {
  /**
   * Mean.
   */
  μ:MultivariateNormalInverseGamma <- μ;

  function rows() -> Integer {
    return μ.rows();
  }

  function supportsLazy() -> Boolean {
    return true;
  }

  function simulate() -> Real[_] {
    return simulate_multivariate_normal_inverse_gamma_multivariate_gaussian(
        μ.ν.value(), μ.Λ.value(), μ.α.value(), μ.γ.value());
  }

  function simulateLazy() -> Real[_]? {
    return simulate_multivariate_normal_inverse_gamma_multivariate_gaussian(
        μ.ν.eval(), μ.Λ.eval(), μ.α.eval(), μ.γ.eval());
  }
  
  function logpdf(x:Real[_]) -> Real {
    return logpdf_multivariate_normal_inverse_gamma_multivariate_gaussian(
        x, μ.ν.value(), μ.Λ.value(), μ.α.value(), μ.γ.value());
  }

  function logpdfLazy(x:Expression<Real[_]>) -> Expression<Real>? {
    return logpdf_lazy_multivariate_normal_inverse_gamma_multivariate_gaussian(
        x, μ.ν, μ.Λ, μ.α, μ.γ);
  }

  function update(x:Real[_]) {
    (μ.ν, μ.Λ, μ.α, μ.γ) <- box(update_multivariate_normal_inverse_gamma_multivariate_gaussian(
        x, μ.ν.value(), μ.Λ.value(), μ.α.value(), μ.γ.value()));
  }

  function updateLazy(x:Expression<Real[_]>) {
    (μ.ν, μ.Λ, μ.α, μ.γ) <- update_lazy_multivariate_normal_inverse_gamma_multivariate_gaussian(
        x, μ.ν, μ.Λ, μ.α, μ.γ);
  }

  function link() {
    μ.setChild(this);
  }
  
  function unlink() {
    μ.releaseChild(this);
  }
}

function MultivariateNormalInverseGammaMultivariateGaussian(μ:MultivariateNormalInverseGamma) -> MultivariateNormalInverseGammaMultivariateGaussian {
  m:MultivariateNormalInverseGammaMultivariateGaussian(μ);
  m.link();
  return m;
}

/*
 * Simulate a multivariate Gaussian distribution with a multivariate normal
 * inverse-gamma prior.
 *
 * - ν: Precision times mean.
 * - Λ: Precision.
 * - α: Shape of the inverse-gamma.
 * - β: Scale of the inverse-gamma.
 * - γ: Scale accumulator of the inverse-gamma.
 */
function simulate_multivariate_normal_inverse_gamma_multivariate_gaussian(
    ν:Real[_], Λ:Real[_,_], α:Real, γ:Real) -> Real[_] {
  let n <- length(ν);
  let μ <- solve(Λ, ν);
  let β <- γ - 0.5*dot(μ, ν);
  return simulate_multivariate_student_t(2.0*α, μ, identity(n) + inv(Λ), 2.0*β);
}

/*
 * Observe a multivariate Gaussian variate with a multivariate normal
 * inverse-gamma prior.
 *
 * - x: The variate.
 * - ν: Precision times mean.
 * - Λ: Precision.
 * - α: Shape of the inverse-gamma.
 * - γ: Scale accumulator of the inverse-gamma.
 *
 * Returns: the log probability density.
 */
function logpdf_multivariate_normal_inverse_gamma_multivariate_gaussian(
    x:Real[_], ν:Real[_], Λ:Real[_,_], α:Real, γ:Real) -> Real {
  let n <- length(ν);
  let μ <- solve(Λ, ν);
  let β <- γ - 0.5*dot(μ, ν);
  return logpdf_multivariate_student_t(x, 2.0*α, μ, identity(n) + inv(Λ), 2.0*β);
}

/*
 * Observe a multivariate Gaussian variate with a multivariate normal
 * inverse-gamma prior.
 *
 * - x: The variate.
 * - ν: Precision times mean.
 * - Λ: Precision.
 * - α: Shape of the inverse-gamma.
 * - γ: Scale accumulator of the inverse-gamma.
 *
 * Returns: the log probability density.
 */
function logpdf_lazy_multivariate_normal_inverse_gamma_multivariate_gaussian(x:Expression<Real[_]>,
    ν:Expression<Real[_]>, Λ:Expression<Real[_,_]>, α:Expression<Real>, γ:Expression<Real>) -> Expression<Real> {
  let n <- length(ν);
  let μ <- solve(Λ, ν);
  let β <- γ - 0.5*dot(μ, ν);
  return logpdf_lazy_multivariate_student_t(x, box(2.0*α), box(μ), box(identity(n) + inv(Λ)), box(2.0*β));
}

/*
 * Update the parameters of a normal inverse-gamma distribution with a
 * multivariate Gaussian likelihood.
 *
 * - x: The variate.
 * - ν: Prior precision times mean.
 * - Λ: Prior precision.
 * - α: Prior shape of the inverse-gamma.
 * - γ: Prior scale accumulator.
 *
 * Returns: the posterior hyperparameters `μ'`, `Λ'`, `α'` and `γ'`.
 */
function update_multivariate_normal_inverse_gamma_multivariate_gaussian(
    x:Real[_], ν:Real[_], Λ:Real[_,_], α:Real, γ:Real) -> (Real[_], Real[_,_], Real,
    Real) {
  let D <- length(x);
  let Λ' <- rank_update(Λ, identity(rows(Λ)));
  let ν' <- ν + x;
  let α' <- α + 0.5*D;
  let γ' <- γ + 0.5*dot(x);
  return (ν', Λ', α', γ');
}

/*
 * Update the parameters of a normal inverse-gamma distribution with a
 * multivariate Gaussian likelihood.
 *
 * - x: The variate.
 * - ν: Prior precision times mean.
 * - Λ: Prior precision.
 * - α: Prior shape of the inverse-gamma.
 * - γ: Prior scale accumulator.
 *
 * Returns: the posterior hyperparameters `μ'`, `Λ'`, `α'` and `γ'`.
 */
function update_lazy_multivariate_normal_inverse_gamma_multivariate_gaussian(
    x:Expression<Real[_]>, ν:Expression<Real[_]>, Λ:Expression<Real[_,_]>,
    α:Expression<Real>, γ:Expression<Real>) -> (Expression<Real[_]>,
    Expression<Real[_,_]>, Expression<Real>, Expression<Real>) {
  let D <- x.length();
  let Λ' <- rank_update(Λ, identity(rows(Λ)));
  let ν' <- ν + x;
  let α' <- α + 0.5*D;
  let γ' <- γ + 0.5*dot(x);
  return (box(ν'), box(Λ'), box(α'), box(γ'));
}
