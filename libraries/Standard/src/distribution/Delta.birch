/**
 * Delta distribution, representing a distribution on a discrete space with
 * all probability mass at one location.
 */
final class Delta(μ:Expression<Integer>) < Discrete {
  /**
   * Location.
   */
  μ:Expression<Integer> <- μ;

  function supportsLazy() -> Boolean {
    return false;
  }

  function simulate() -> Integer {
    return simulate_delta(μ.value());
  }
  
//  function simulateLazy() -> Integer? {
//    return simulate_delta(μ.get());
//  }
  
  function logpdf(x:Integer) -> Real {
    return logpdf_delta(x, μ.value());
  }

//  function logpdfLazy(x:Expression<Integer>) -> Expression<Real>? {
//    return logpdf_lazy_delta(x, μ);
//  }

  function lower() -> Integer? {
    return μ.value();
  }
  
  function upper() -> Integer? {
    return μ.value();
  }

  function write(buffer:Buffer) {
    prune();
    buffer.set("class", "Delta");
    buffer.set("μ", μ);
  }
}

/**
 * Create delta distribution.
 *
 * - μ: Location.
 */
function Delta(μ:Expression<Integer>) -> Distribution<Integer> {
  m:Discrete?;
  if (m <- μ.graftDiscrete())? {
    return DiscreteDelta(m!);
  } else {
    return construct<Delta>(μ);
  }
}

/**
 * Create delta distribution.
 *
 * - μ: Location.
 */
function Delta(μ:Integer) -> Distribution<Integer> {
  return Delta(box(μ));
}
