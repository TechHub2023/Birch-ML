/**
 * Delta distribution convolved with a bounded discrete distribution.
 *
 * @param p Bounded discrete distribution.
 */
final class DiscreteDeltaDistribution(p:BoundedDiscreteDistribution) <
    BoundedDiscreteDistribution {
  /**
   * Distribution.
   */
  p:BoundedDiscreteDistribution <- p;

  override function supportsLazy() -> Boolean {
    return p.supportsLazy();
  }

  override function simulate() -> Integer! {
    return p.simulate();
  }

  override function simulateLazy() -> Integer!? {
    return p.simulateLazy();
  }

  override function logpdf(x:Integer!) -> Real! {
    return p.logpdf(x);
  }

  override function logpdfLazy(x:Integer!) -> Real!? {
    return p.logpdfLazy(x);
  }

  override function update(x:Integer!) -> Delay? {
    return Delta(x);
  }

  override function updateLazy(x:Expression<Integer>) -> Delay? {
    return Delta(x);
  }

  override function hoist() -> Expression<Real>? {
    return p.hoist();
  }

  override function lower() -> Integer!? {
		return p.lower();
  }
  
  override function upper() -> Integer!? {
	  return p.upper();
  }

  override function constant() {
    super.constant();
    p.constant();
  }
}

/**
 * Create delta distribution.
 */
function Delta(μ:Random<Integer>) -> Distribution<Integer> {
	if μ.hasNext() && μ.getNext().isBoundedDiscrete() {
  	μ.prune();
    let p <- construct<DiscreteDeltaDistribution>(
        μ.getNext().getBoundedDiscrete()!);
    μ.setNext(p);
    return p;
  } else {
  	return wrap_delta(μ);
  }
}
