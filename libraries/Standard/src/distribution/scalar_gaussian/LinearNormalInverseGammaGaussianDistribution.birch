/**
 * Linear-normal-inverse-gamma-Gaussian distribution.
 */
final class LinearNormalInverseGammaGaussianDistribution<Arg1,Arg2,Arg3,Arg4,
    Arg5,Arg6,Arg7>(a:Arg1, ν:Arg2, λ:Arg3, α:Arg4, β:Arg5, c:Arg6,
    s2:Arg7) < Distribution<Real> {
  /**
   * Scale.
   */
  a:Arg1 <- a;
    
  /**
   * Precision times mean.
   */
  ν:Arg2 <- ν;

  /**
   * Precision.
   */
  λ:Arg3 <- λ;

  /**
   * Shape.
   */
  α:Arg4 <- α;

  /**
   * Scale.
   */
  β:Arg5 <- β;

  /**
   * Offset.
   */
  c:Arg6 <- c;

  /**
   * Likelihood variance scale.
   */
  s2:Arg7 <- s2;

  override function supportsLazy() -> Boolean {
    return true;
  }

  override function simulate() -> Real! {
    return simulate_linear_normal_inverse_gamma_gaussian(value(a), value(ν),
        value(λ), value(α), value(β), value(c), value(s2));
  }

  override function simulateLazy() -> Real!? {
    return simulate_linear_normal_inverse_gamma_gaussian(eval(a), eval(ν),
        eval(λ), eval(α), eval(β), eval(c), eval(s2));
  }
  
  override function logpdf(x:Real!) -> Real! {
    return logpdf_linear_normal_inverse_gamma_gaussian(x, value(a), value(ν),
        value(λ), value(α), value(β), value(c), value(s2));
  }

  override function logpdfLazy(x:Expression<Real>) -> Expression<Real>? {
    return box(logpdf_linear_normal_inverse_gamma_gaussian(x, a, ν, λ, α, β,
        c, s2));
  }

  override function update(x:Real!) -> Delay? {
    return update_linear_normal_inverse_gamma_gaussian(x, value(a), value(ν),
        value(λ), value(α), value(β), value(c), value(s2));
  }

  override function updateLazy(x:Expression<Real>) -> Delay? {
    return update_linear_normal_inverse_gamma_gaussian(x, a, ν, λ, α, β,
        c, s2);
  }

  override function cdf(x:Real!) -> Real!? {
    return cdf_linear_normal_inverse_gamma_gaussian(x, value(a), value(ν),
        value(λ), value(α), value(β), value(c), value(s2));
  }

  override function quantile(P:Real!) -> Real!? {
    return quantile_linear_normal_inverse_gamma_gaussian(P, value(a),
        value(ν), value(λ), value(α), value(β), value(c), value(s2));
  }
}

function LinearNormalInverseGammaGaussian<Arg1,Arg2,Arg3,Arg4,Arg5,Arg6,Arg7>(
    a:Arg1, ν:Arg2, λ:Arg3, α:Arg4, β:Arg5, c:Arg6, s2:Arg7)  -> {
  return wrap_linear_normal_inverse_gamma_gaussian(a, ν, λ, α, β, c, s2);
}
function wrap_linear_normal_inverse_gamma_gaussian<Arg1,Arg2,Arg3,Arg4,Arg5,
    Arg6,Arg7>(a:Arg1, ν:Arg2, λ:Arg3, α:Arg4, β:Arg5, c:Arg6, s2:Arg7)  -> {
  return make_linear_normal_inverse_gamma_gaussian(wrap(a), wrap(ν), wrap(λ),
      wrap(α), wrap(β), wrap(c), wrap(s2));
}
function make_linear_normal_inverse_gamma_gaussian<Arg1,Arg2,Arg3,Arg4,Arg5,
    Arg6,Arg7>(a:Arg1, ν:Arg2, λ:Arg3, α:Arg4, β:Arg5, c:Arg6, s2:Arg7)  -> {
  return construct<LinearNormalInverseGammaGaussianDistribution<Arg1,Arg2,
      Arg3,Arg4,Arg5,Arg6,Arg7>>(a, ν, λ, α, β, c, s2);
}

/**
 * Create Gaussian distribution.
 */
function Gaussian<Arg1,Arg2>(μ:Add<Multiply<Arg1,Random<Real>>,Arg2>,
    σ2:Random<Real>) -> Distribution<Real> {
  return Gaussian(μ, 1.0*σ2);
}

/**
 * Create Gaussian distribution.
 */
function Gaussian<Arg1,Arg2,Arg3>(μ:Add<Multiply<Arg1,Random<Real>>,Arg2>,
    σ2:Multiply<Random<Real>,Arg3>) -> Distribution<Real> {
  return Gaussian(μ, σ2.r*σ2.l);
}

/**
 * Create Gaussian distribution.
 */
function Gaussian<Arg1,Arg2,Arg3>(μ:Add<Multiply<Arg1,Random<Real>>,Arg2>,
    σ2:Multiply<Arg3,Random<Real>>) -> Distribution<Real> {
  μ.l.r.prune();
  if μ.l.r.hasNormalInverseGamma() && μ.l.r.isGrandChildOf(σ2.r) {
    let (ν, λ, α, β) <- μ.l.r.getNormalInverseGamma();
    let p <- LinearNormalInverseGammaGaussian(μ.l.l, ν, λ, α, β, μ.r, σ2.l);
    μ.l.r.graft(p, 1);
    σ2.r.graftReplace(p, 2);
    μ.l.r.join(σ2.r);
    return p;
  } else {
    σ2.r.prune();
    if σ2.r.hasInverseGamma() {
      let (α, β) <- σ2.r.getInverseGamma();
      let p <- NormalInverseGamma(μ/σ2.l, 1.0/σ2.l, α, β);
      σ2.r.graft(p);
      return p;
    } else if μ.l.r.hasGaussian() {
      let (m, s2) <- μ.l.r.getGaussian();
      let p <- LinearGaussianGaussian(μ.l.l, m, s2, μ.r, σ2);
      μ.l.r.graft(p);
      return p;
    } else {
      return Gaussian(wrap(μ), wrap(σ2));
    }
  }
}

/**
 * Create Gaussian distribution.
 */
function Gaussian<Arg1,Arg2>(μ:Add<Multiply<Arg1,Random<Real>>,Arg2>,
    σ2:Multiply<Random<Real>,Random<Real>>) -> Distribution<Real> {
  μ.l.r.prune();
  if μ.l.r.hasNormalInverseGamma() && μ.l.r.isGrandChildOf(σ2.r) {
    let (ν, λ, α, β) <- μ.l.r.getNormalInverseGamma();
    let p <- LinearNormalInverseGammaGaussian(μ.l.l, ν, λ, α, β, μ.r, σ2.l);
    μ.l.r.graft(p, 1);
    σ2.r.graftReplace(p, 2);
    μ.l.r.join(σ2.r);
    return p;
  } else if μ.l.r.hasNormalInverseGamma() && μ.l.r.isGrandChildOf(σ2.l) {
    let (ν, λ, α, β) <- μ.l.r.getNormalInverseGamma();
    let p <- LinearNormalInverseGammaGaussian(μ.l.l, ν, λ, α, β, μ.r, σ2.r);
    μ.l.r.graft(p, 1);
    σ2.l.graftReplace(p, 2);
    μ.l.r.join(σ2.l);
    return p;
  } else {
    σ2.l.prune();
    σ2.r.prune();
    if σ2.l.hasInverseGamma() {
      let (α, β) <- σ2.l.getInverseGamma();
      let p <- NormalInverseGamma(μ/σ2.r, 1.0/σ2.r, α, β);
      σ2.l.graft(p);
      return p;
    } else if σ2.r.hasInverseGamma() {
      let (α, β) <- σ2.r.getInverseGamma();
      let p <- NormalInverseGamma(μ/σ2.l, 1.0/σ2.l, α, β);
      σ2.r.graft(p);
      return p;
    } else if μ.l.r.hasGaussian() {
      let (m, s2) <- μ.l.r.getGaussian();
      let p <- LinearGaussianGaussian(μ.l.l, m, s2, μ.r, σ2);
      μ.l.r.graft(p);
      return p;
    } else {
      return Gaussian(wrap(μ), wrap(σ2));
    }
  }
}

/**
 * Create Gaussian distribution.
 */
function Gaussian<Arg>(μ:Multiply<Arg,Random<Real>>, σ2:Random<Real>) ->
    Distribution<Real> {
  return Gaussian(μ + 0.0, σ2);
}

/**
 * Create Gaussian distribution.
 */
function Gaussian<Arg1,Arg2>(μ:Multiply<Arg1,Random<Real>>,
    σ2:Multiply<Arg2,Random<Real>>) -> Distribution<Real> {
  return Gaussian(μ + 0.0, σ2);
}

/**
 * Create Gaussian distribution.
 */
function Gaussian<Arg1,Arg2>(μ:Multiply<Arg1,Random<Real>>,
    σ2:Multiply<Random<Real>,Arg2>) -> Distribution<Real> {
  return Gaussian(μ + 0.0, σ2);
}

/**
 * Create Gaussian distribution.
 */
function Gaussian<Arg>(μ:Multiply<Arg,Random<Real>>,
    σ2:Multiply<Random<Real>,Random<Real>>) -> Distribution<Real> {
  return Gaussian(μ + 0.0, σ2);
}

/*
 * Simulate a Gaussian distribution with a normal inverse-gamma prior.
 *
 * - a: Scale.
 * - ν: Precision times mean.
 * - λ: Precision.
 * - α: Variance shape.
 * - β: Variance scale.
 * - c: Offset.
 * - s2: Likelihood variance scale.
 */
function simulate_linear_normal_inverse_gamma_gaussian<Arg1,Arg2,Arg3,Arg4,
    Arg5,Arg6,Arg7>(a:Arg1, ν:Arg2, λ:Arg3, α:Arg4, β:Arg5, c:Arg6,
    s2:Arg7) -> {
  return simulate_student_t(2.0*α, a*ν/λ + c, (β/α)*(s2 + a*a/λ));
}

/*
 * Observe a Gaussian variate with a normal inverse-gamma prior with linear
 * transformation.
 *
 * - x: The variate.
 * - a: Scale.
 * - ν: Precision times mean.
 * - λ: Precision.
 * - α: Variance shape.
 * - β: Variance scale.
 * - c: Offset.
 * - s2: Likelihood variance scale.
 *
 * Returns: the log probability density.
 */
function logpdf_linear_normal_inverse_gamma_gaussian<Arg1,Arg2,Arg3,Arg4,Arg5,
    Arg6,Arg7,Arg8>(x:Arg1, a:Arg2, ν:Arg3, λ:Arg4, α:Arg5, β:Arg6, c:Arg7,
    s2:Arg8) -> {
  return logpdf_student_t(x, 2.0*α, a*ν/λ + c, (β/α)*(s2 + a*a/λ));
}

/*
 * Update the parameters of a normal inverse-gamma distribution with a
 * Gaussian likelihood.
 *
 * - x: The variate.
 * - a: Scale.
 * - ν: Prior precision times mean.
 * - λ: Prior precision.
 * - α: Prior variance shape.
 * - β: Prior variance scale.
 * - c: Offset.
 * - s2: Likelihood variance scale.
 *
 * Returns: the posterior hyperparameters `ν'`, `λ'`, `α'` and `β'`.
 */
function update_linear_normal_inverse_gamma_gaussian<Arg1,Arg2,Arg3,Arg4,Arg5,
    Arg6,Arg7,Arg8>(x:Arg1, a:Arg2, ν:Arg3, λ:Arg4, α:Arg5, β:Arg6, c:Arg7,
    s2:Arg8) -> {
  let l <- 1.0/s2;
  let y <- x - c;
  let z <- l*y;
  let λ' <- λ + a*l*a;
  let ν' <- ν + a*z;
  let α' <- α + 0.5;
  let β' <- β + 0.5*(y*z + ν*ν/λ - ν'*ν'/λ');
  return NormalInverseGamma(ν', λ', α', β');
}

/*
 * CDF of a Gaussian variate with a normal inverse-gamma prior with linear
 * transformation.
 *
 * - x: The variate.
 * - a: Scale.
 * - ν: Precision times mean.
 * - λ: Precision.
 * - α: Variance shape.
 * - β: Variance scale.
 * - c: Offset.
 * - s2: Likelihood variance scale.
 *
 * Return: the cumulative probability.
 */
function cdf_linear_normal_inverse_gamma_gaussian<Arg1,Arg2,Arg3,Arg4,Arg5,
    Arg6,Arg7,Arg8>(x:Arg1, a:Arg2, ν:Arg3, λ:Arg4, α:Arg5, β:Arg6, c:Arg7,
    s2:Arg8) -> {
  return cdf_student_t(x, 2.0*α, a*ν/λ + c, (β/α)*(s2 + a*a/λ));
}

/*
 * Quantile of a Gaussian distribution with a normal inverse-gamma prior with
 * linear transformation.
 *
 * - P: The cumulative probability.
 * - a: Scale.
 * - ν: Precision times mean.
 * - λ: Precision.
 * - α: Variance shape.
 * - β: Variance scale.
 * - c: Offset.
 * - s2: Likelihood variance scale.
 *
 * Return: the quantile.
 */
function quantile_linear_normal_inverse_gamma_gaussian<Arg1,Arg2,Arg3,Arg4,
    Arg5,Arg6,Arg7,Arg8>(P:Arg1, a:Arg2, ν:Arg3, λ:Arg4, α:Arg5, β:Arg6,
    c:Arg7, s2:Arg8) -> {
  return quantile_student_t(P, 2.0*α, a*ν/λ + c, (β/α)*(s2 + a*a/λ));
}
