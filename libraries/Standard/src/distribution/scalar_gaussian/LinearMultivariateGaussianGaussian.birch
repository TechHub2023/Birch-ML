/**
 * Multivariate linear-Gaussian-Gaussian distribution.
 */
final class LinearMultivariateGaussianGaussian<Arg1,Arg2,Arg3,Arg4,Arg5>(
    a:Arg1, μ:Arg2, Σ:Arg3, c:Arg4, s2:Arg5) < Distribution<Real> {
  /**
   * Scale.
   */
  a:Arg1 <- a;
    
  /**
   * Prior mean.
   */
  μ:Arg2 <- μ;

  /**
   * Prior covariance.
   */
  Σ:Arg3 <- Σ;

  /**
   * Offset.
   */
  c:Arg4 <- c;
  
  /**
   * Likelihood covariance.
   */
  s2:Arg5 <- s2;

  override function supportsLazy() -> Boolean {
    return true;
  }

  override function simulate() -> Real {
    return simulate_gaussian(value(dot(a, μ) + c), value(dot(a, Σ*a) + s2));
  }

  override function simulateLazy() -> Real? {
    return simulate_gaussian(peak(dot(a, μ) + c), peak(dot(a, Σ*a) + s2));
  }
  
  override function logpdf(x:Real) -> Real {
    return logpdf_gaussian(x, value(dot(a, μ) + c), value(dot(a, Σ*a) + s2));
  }

  override function logpdfLazy(x:Expression<Real>) -> Expression<Real>? {
    return box(logpdf_gaussian(x, dot(a, μ) + c, dot(a, Σ*a) + s2));
  }

  override function update(x:Real) -> Delay? {
    return update_linear_multivariate_gaussian_gaussian(x, value(a), value(μ),
        value(Σ), value(c), value(s2));
  }

  override function updateLazy(x:Expression<Real>) -> Delay? {
    return update_linear_multivariate_gaussian_gaussian(x, a, μ, Σ, c, s2);
  }

  override function cdf(x:Real) -> Real? {
    return cdf_gaussian(x, value(dot(a, μ) + c), value(dot(a, Σ*a) + s2));
  }

  override function quantile(P:Real) -> Real? {
    return quantile_gaussian(P, value(dot(a, μ) + c), value(dot(a, Σ*a) + s2));
  }

  override function isGaussian() -> Boolean {
    return true;
  }

  override function getGaussian() -> (Expression<Real>, Expression<Real>)? {
    return box(dot(a, μ) + c, dot(a, Σ*a) + s2);
  }
}

function LinearMultivariateGaussianGaussian<Arg1,Arg2,Arg3,Arg4,Arg5>(a:Arg1,
    μ:Arg2, Σ:Arg3, c:Arg4, σ2:Arg5) -> {
  return construct<LinearMultivariateGaussianGaussian<Arg1,Arg2,Arg3,Arg4,
      Arg5>>(a, μ, Σ, c, σ2);
}

/**
 * Create Gaussian distribution.
 */
function Gaussian<Arg1,Arg2,Arg3>(μ:DotAdd<Arg1,Random<Real[_]>,Arg2>,
    σ2:Arg3) -> Distribution<Real> {
  if μ.m!.hasMultivariateGaussian() {
    μ.m!.prune();
    let (m, S) <- μ.m!.getMultivariateGaussian();
    let p <- LinearMultivariateGaussianGaussian(μ.l!, m, S, μ.r!, σ2);
    μ.m!.graft(p);
    return p;
  } else {
    return construct<Gaussian<DotAdd<Arg1,Random<Real[_]>,Arg2>,Arg3>>(μ, σ2);
  }
}

/**
 * Create Gaussian distribution.
 */
function Gaussian<Arg1,Arg2>(μ:Dot<Arg1,Random<Real[_]>>, σ2:Arg2) ->
    Distribution<Real> {
  if μ.r!.hasMultivariateGaussian() {
    μ.r!.prune();
    let (m, S) <- μ.r!.getMultivariateGaussian();
    let p <- LinearMultivariateGaussianGaussian(μ.l!, m, S, 0.0, σ2);
    μ.r!.graft(p);
    return p;
  } else {
    return construct<Gaussian<Dot<Arg1,Random<Real[_]>>,Arg2>>(μ, σ2);
  }
}

/*
 * Update the parameters of a multivariate Gaussian distribution with a 
 * linear transformation involving a dot product, and a multivariate Gaussian
 * likelihood.
 *
 * - x: The variate.
 * - a: Scale.
 * - μ: Prior mean.
 * - Σ: Prior covariance.
 * - c: Offset.
 * - s2: Likelihood covariance.
 *
 * Returns: the posterior hyperparameters `μ'` and `Σ'`.
 */
function update_linear_multivariate_gaussian_gaussian<Arg1,Arg2,Arg3,Arg4,
    Arg5,Arg6>(x:Arg1, a:Arg2, μ:Arg3, Σ:Arg4, c:Arg5, s2:Arg6) -> {
  let k' <- Σ*a/(dot(a, Σ*a) + s2);
  let μ' <- μ + k'*(x - dot(a, μ) - c);
  let Σ' <- Σ - outer(k', a)*Σ;
  return MultivariateGaussian(μ', Σ');
}
