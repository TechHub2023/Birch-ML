/*
 * Simulate a matrix $t$-distribution variate.
 *
 * - k: Degrees of freedom.
 * - M: Mean.
 * - U: Spread.
 * - Ψ: Spread.
 */
function simulate_matrix_t<Arg1,Arg2,Arg3,Arg4>(k:Arg1, M:Arg2, U:Arg3,
    Ψ:Arg4) -> {
  return simulate_matrix_gaussian(M, U, simulate_inverse_wishart(Ψ, k));
}

/*
 * Observe a matrix $t$-distribution variate with location
 * and scale.
 *
 * - X: The variate.
 * - k: Degrees of freedom.
 * - M: Mean.
 * - U: Among-row covariance.
 * - V: Among-column covariance.
 *
 * Returns: the log probability density.
 */
function logpdf_matrix_t<Arg1,Arg2,Arg3,Arg4,Arg5>(X:Arg1, k:Arg2,
    M:Arg3, U:Arg4, V:Arg5) -> {
  let n <- rows(X);
  let p <- columns(X);
  let a <- 0.5*(k + p + n - 1.0);
  let b <- 0.5*(k + n - 1.0);
  let Z <- X - M;
  return lgamma(a, n) - 0.5*p*n*log(π) - lgamma(b, n) -
      0.5*p*lcholdet(chol(U)) - 0.5*n*lcholdet(chol(V)) -
      a*ldet(diagonal(1.0, n) +
      cholsolve(chol(U), Z)*cholsolve(chol(V), transpose(Z)));
}
