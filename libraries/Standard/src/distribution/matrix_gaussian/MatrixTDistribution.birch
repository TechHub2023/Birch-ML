/*
 * Simulate a matrix $t$-distribution variate.
 *
 * - k: Degrees of freedom.
 * - M: Mean.
 * - U: Among-row covariance.
 * - Ψ: Among-column spread.
 */
function simulate_matrix_t<Arg1,Arg2,Arg3,Arg4>(k:Arg1, M:Arg2, U:Arg3,
    Ψ:Arg4) -> {
  let V <- chol(simulate_inverse_wishart(Ψ, k));
  return simulate_matrix_gaussian(M, U, V);
}

/*
 * Observe a matrix $t$-distribution variate with location and scale.
 *
 * - X: The variate.
 * - k: Degrees of freedom.
 * - M: Mean.
 * - U: Among-row covariance.
 * - Ψ: Among-column spread.
 *
 * Returns: the log probability density.
 */
function logpdf_matrix_t<Arg1,Arg2,Arg3,Arg4,Arg5>(X:Arg1, k:Arg2, M:Arg3,
    U:Arg4, Ψ:Arg5) -> {
  let n <- rows(X);
  let p <- columns(X);
  let a <- wrap(0.5*k + 0.5*(p + n - 1));
  let b <- 0.5*k + 0.5*(p - 1);  // only used once, no need to wrap
  let Z <- wrap(X - M);
  return lgamma(a, p) - 0.5*p*n*log(π) - lgamma(b, p) - 0.5*p*lcholdet(U) -
      0.5*n*lcholdet(Ψ) - a*ldet(diagonal(1.0, n) +
      cholsolve(U, Z)*cholsolve(Ψ, transpose(Z)));
}
