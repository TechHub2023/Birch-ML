/**
 * Matrix Gaussian variate with linear transformation of
 * matrix-normal-inverse-Wishart prior.
 */
final class LinearMatrixNormalInverseWishartMatrixGaussianDistribution<Arg1,Arg2,Arg3,
    Arg4,Arg5,Arg6,Arg7>(A:Arg1, M:Arg2, Σ:Arg3, Ψ:Arg4, k:Arg5, C:Arg6,
    U:Arg7) < Distribution<Real[_,_]> {
  /**
   * Scale.
   */
  A:Arg1 <- A;

  /**
   * Mean.
   */
  M:Arg2 <- M;

  /**
   * Covariance.
   */
  Σ:Arg3 <- Σ;
  
  /**
   * Among-column covariance scale.
   */
  Ψ:Arg4 <- Ψ;
  
  /**
   * Among-column covariance degrees of freedom.
   */
  k:Arg5 <- k;

  /**
   * Offset.
   */
  C:Arg6 <- C;

  /**
   * Among-row covariance.
   */
  U:Arg7 <- U;

  override function supportsLazy() -> Boolean {
    return true;
  }

  override function simulate() -> Real[_,_] {
    return simulate_scaled_matrix_normal_inverse_wishart_matrix_gaussian(
        value(A), value(M), value(Σ), value(Ψ), value(k), value(U)) +
        value(C);
  }

  override function simulateLazy() -> Real[_,_]? {
    return simulate_scaled_matrix_normal_inverse_wishart_matrix_gaussian(
        eval(A), eval(M), eval(Σ), eval(Ψ), eval(k), eval(U)) + eval(C);
  }
  
  override function logpdf(X:Real[_,_]) -> Real! {
    return logpdf_scaled_matrix_normal_inverse_wishart_matrix_gaussian(
        X - value(C), value(A), value(M), value(Σ), value(Ψ), value(k),
        value(U));
  }

  override function logpdfLazy(X:Expression<Real[_,_]>) -> Expression<Real>? {
    return box(logpdf_scaled_matrix_normal_inverse_wishart_matrix_gaussian(
        box(X - C), A, M, Σ, Ψ, k, U));
  }

  override function update(X:Real[_,_]) -> Delay? {
    return update_scaled_matrix_normal_inverse_wishart_matrix_gaussian(
        X - value(C), value(A), value(M), value(Σ), value(Ψ), value(k),
        value(U));
  }

  override function updateLazy(X:Expression<Real[_,_]>) -> Delay? {
    return update_scaled_matrix_normal_inverse_wishart_matrix_gaussian(
        box(X - C), A, M, Σ, Ψ, k, U);
  }
}

function LinearMatrixNormalInverseWishartMatrixGaussian<Arg1,Arg2,Arg3,
    Arg4,Arg5,Arg6,Arg7>(A:Arg1, M:Arg2, Σ:Arg3, Ψ:Arg4, k:Arg5, C:Arg6,
    U:Arg7) -> {
  return wrap_linear_matrix_normal_inverse_wishart_matrix_gaussian(A, M,
      chol(Σ), chol(Ψ), k, C, chol(U));
}
function wrap_linear_matrix_normal_inverse_wishart_matrix_gaussian<Arg1,Arg2,
    Arg3,Arg4,Arg5,Arg6,Arg7>(A:Arg1, M:Arg2, Σ:Arg3, Ψ:Arg4, k:Arg5, C:Arg6,
    U:Arg7) -> {
  return make_linear_matrix_normal_inverse_wishart_matrix_gaussian(wrap(A),
      wrap(M), wrap(Σ), wrap(Ψ), wrap(k), wrap(C), wrap(U));
}
function make_linear_matrix_normal_inverse_wishart_matrix_gaussian<Arg1,Arg2,
    Arg3,Arg4,Arg5,Arg6,Arg7>(A:Arg1, M:Arg2, Σ:Arg3, Ψ:Arg4, k:Arg5, C:Arg6,
    U:Arg7) -> {
  return construct<LinearMatrixNormalInverseWishartMatrixGaussianDistribution<
      Arg1,Arg2,Arg3,Arg4,Arg5,Arg6,Arg7>>(A, M, Σ, Ψ, k, C, U);
}

/**
 * Create matrix Gaussian distribution.
 */
function MatrixGaussian<Arg1,Arg2,Arg3>(M:OpAdd<OpMul<Arg1,Random<Real[_,_]>>,
    Arg2>, U:Arg3, V:Random<Real[_,_]>) -> Distribution<Real[_,_]> {
  M.l.r.prune();
  if M.l.r.hasMatrixNormalInverseWishart() && M.l.r.isGrandChildOf(V) {
    let (M1, Σ, Ψ, k) <- M.l.r.getMatrixNormalInverseWishart();
    let p <- wrap_linear_matrix_normal_inverse_wishart_matrix_gaussian(M.l.l,
        M1, Σ, Ψ, k, M.r, chol(U));
    M.l.r.graft(p, 1);
    V.graftReplace(p, 2);
    M.l.r.join(V);
    return p;
  } else {
    V.prune();
    if V.hasInverseWishart() {
      let (Ψ, k) <- V.getInverseWishart();
      let p <- wrap_matrix_normal_inverse_wishart(M, chol(U), Ψ, k);
      V.graft(p);
      return p;
    } else {
      return wrap_matrix_gaussian(M, chol(U), chol(V));
    }
  }
}
