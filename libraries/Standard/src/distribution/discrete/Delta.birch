/**
 * Delta distribution, representing a distribution on a discrete space with
 * all probability mass at one location.
 *
 * - μ: Location.
 */
final class Delta(μ:Expression<Integer>) < Discrete {
  /**
   * Location.
   */
  μ:Expression<Integer> <- μ;

  function supportsLazy() -> Boolean {
    return false;
  }

  function simulate() -> Integer {
    return simulate_delta(μ.value());
  }
  
//  function simulateLazy() -> Integer? {
//    return simulate_delta(μ.eval());
//  }
  
  function logpdf(x:Integer) -> Real {
    return logpdf_delta(x, μ.value());
  }

//  function logpdfLazy(x:Expression<Integer>) -> Expression<Real>? {
//    return logpdf_lazy_delta(x, μ);
//  }

  function lower() -> Integer? {
    return μ.value();
  }
  
  function upper() -> Integer? {
    return μ.value();
  }

  function write(buffer:Buffer) {
    prune();
    buffer.set("class", "Delta");
    buffer.set("μ", μ);
  }
}

/**
 * Create delta distribution.
 */
function Delta<Middle>(μ:Middle) -> Distribution<Integer> {
  return construct<Delta>(box(μ));
}

/**
 * Create delta distribution.
 */
function Delta(μ:Add<Random<Integer>,Random<Integer>>) -> Distribution<Integer> {
  let l1 <- μ.l!.graftBoundedDiscrete();
  let r1 <- μ.r!.graftBoundedDiscrete();
  if l1? && r1? {
    return AddBoundedDiscrete(l1!, r1!);
  } else {
    return construct<Delta>(box(μ));
  }
}

/**
 * Create delta distribution.
 */
function Delta(μ:Subtract<Random<Integer>,Random<Integer>>) -> Distribution<Integer> {
  let l1 <- μ.l!.graftBoundedDiscrete();
  let r1 <- μ.r!.graftBoundedDiscrete();
  if l1? && r1? {
    return SubtractBoundedDiscrete(l1!, r1!);
  } else {
    return construct<Delta>(box(μ));
  }
}

/*
 * Simulate a delta distribution.
 *
 * - μ: Location.
 */
function simulate_delta(μ:Integer) -> Integer {
  return μ;
}

/*
 * Observe a delta variate.
 *
 * - x: The variate.
 * - μ: Location.
 *
 * Returns: the log probability mass.
 */
function logpdf_delta(x:Integer, μ:Integer) -> Real {
  if x == μ {
    return 0.0;
  } else {
    return -inf;
  }
}
