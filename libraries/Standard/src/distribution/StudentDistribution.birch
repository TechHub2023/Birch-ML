hpp{{
#include <boost/math/distributions/students_t.hpp>
}}

/**
 * Student's $t$-distribution.
 */
final class StudentDistribution<Arg1,Arg2,Arg3>(k:Arg1, μ:Arg2, σ2:Arg3) <
    Distribution<Real> {
  /**
   * Degrees of freedom.
   */
  k:Arg1 <- k;

  /**
   * Location parameter.
   */
  μ:Arg2 <- μ;

  /**
   * Square scale parameter.
   */
  σ2:Arg3 <- σ2;
  
  override function simulate() -> Real! {
    return simulate_student_t(global.value(k), global.value(μ), global.value(σ2));
  }

  override function simulateLazy() -> Real! {
    return simulate_student_t(global.eval(k), global.eval(μ), global.eval(σ2));
  }
  
  override function logpdf(x:Real!) -> Real! {
    return logpdf_student_t(x, global.value(k), global.value(μ), global.value(σ2));
  }

  override function logpdfLazy(x:Real!) -> Real! {
    return logpdf_student_t(x, global.eval(k), global.eval(μ), global.eval(σ2));
  }

  override function hoist() -> Expression<Real> {
    return box(logpdf_student_t(random(), k, μ, σ2));
  }

  override function cdf(x:Real!) -> Real!? {
    return cdf_student_t(x, global.value(k), global.value(μ), global.value(σ2));
  }

  override function quantile(P:Real!) -> Real!? {
    return quantile_student_t(P, global.value(k), global.value(μ), global.value(σ2));
  }

  function doConstant() {
    super.constant();
    global.constant(k);
    global.constant(μ);
    global.constant(σ2);
  }

  override function write(buffer:Buffer) {
    buffer.set("class", "Student");
    buffer.set("k", global.value(k));
    buffer.set("μ", global.value(μ));
    buffer.set("σ2", global.value(σ2));
  }
}

/**
 * Create Student's $t$-distribution.
 */
function Student<Arg1,Arg2,Arg3>(k:Arg1, μ:Arg2, σ2:Arg3) ->
    Distribution<Real> {
  return wrap_student(k, μ, σ2);
}
function wrap_student<Arg1,Arg2,Arg3>(k:Arg1, μ:Arg2, σ2:Arg3) -> {
  return make_student(wrap(k), wrap(μ), wrap(σ2));
}
function make_student<Arg1,Arg2,Arg3>(k:Arg1, μ:Arg2, σ2:Arg3) -> {
  return construct<StudentDistribution<Arg1,Arg2,Arg3>>(k, μ, σ2);
}

/**
 * Create Student's $t$-distribution.
 */
function Student<Arg>(k:Arg) -> Distribution<Real> {
  return Student(k, 0.0, 1.0);
}

/*
 * Simulate a Student's $t$-distribution with location and scale.
 *
 * @param k Degrees of freedom.
 * @param μ Location.
 * @param σ2 Squared scale.
 */
function simulate_student_t<Arg1,Arg2,Arg3>(k:Arg1, μ:Arg2, σ2:Arg3) -> {
  let u <- simulate_chi_squared(k);
  return simulate_gaussian(μ, σ2*k/u);
}

/*
 * Observe a Student's $t$ variate.
 *
 * @param x The variate.
 * @param k Degrees of freedom.
 *
 * @return the log probability density.
 */
function logpdf_student_t<Arg1,Arg2>(x:Arg1, k:Arg2) -> {
  let a <- 0.5*k + 0.5;
  let b <- 0.5*k;
  return lgamma(a) - lgamma(b) - 0.5*log(π*k) - a*log1p(x*x/k);
}

/*
 * Observe a Student's $t$ variate with location and scale.
 *
 * @param x The variate.
 * @param k Degrees of freedom.
 * @param μ Location.
 * @param σ2 Scale.
 *
 * @return the log probability density.
 */
function logpdf_student_t<Arg1,Arg2,Arg3,Arg4>(x:Arg1, k:Arg2, μ:Arg3,
    σ2:Arg4) -> {
  let a <- 0.5*k + 0.5;
  let b <- 0.5*k;
  let z <- (x - μ)/sqrt(σ2);
  return lgamma(a) - lgamma(b) - 0.5*log(k*π*σ2) - a*log1p(z*z/k);
}

/*
 * CDF of a Student's $t$ variate.
 *
 * @param x The variate.
 * @param k Degrees of freedom.
 *
 * @return the cumulative probability.
 */
function cdf_student_t<Arg1,Arg2>(x:Arg1, k:Arg2) -> {
  return ibeta(0.5*k, 0.5*k, 0.5*x/sqrt(pow(x, 2.0) + k) + 0.5);
}

/*
 * CDF of a Student's $t$ variate with location and scale.
 *
 * @param x The variate.
 * @param k Degrees of freedom.
 * @param μ Location.
 * @param σ2 Squared scale.
 *
 * @return the cumulative probability.
 */
function cdf_student_t<Arg1,Arg2,Arg3,Arg4>(x:Arg1, k:Arg2, μ:Arg3,
    σ2:Arg4) -> {
  return cdf_student_t((x - μ)/sqrt(σ2), k);
}

/*
 * Quantile of a Student's $t$ distribution.
 *
 * @param P The cumulative probability.
 * @param k Degrees of freedom.
 *
 * @return the quantile.
 */
function quantile_student_t<Arg1,Arg2>(P:Arg1, k:Arg2) -> {
  cpp{{
  return boost::math::quantile(boost::math::students_t_distribution<Real>(
      wait(k)), wait(P));
  }}
}

/*
 * Quantile of a Student's $t$ distribution with location and scale.
 *
 * @param P The cumulative probability.
 * @param k Degrees of freedom.
 * @param μ Location.
 * @param σ2 Squared scale.
 *
 * @return the quantile.
 */
function quantile_student_t<Arg1,Arg2,Arg3,Arg4>(P:Arg1, k:Arg2, μ:Arg3,
    σ2:Arg4) -> {
  return quantile_student_t(P, k)*sqrt(σ2) + μ;
}
