hpp{{
#include <boost/math/distributions/uniform.hpp>
}}

/**
 * Uniform distribution.
 */
final class UniformDistribution<Arg1,Arg2>(l:Arg1, u:Arg2) < Distribution<Real> {
  /**
   * Lower bound.
   */
  l:Arg1 <- l;
  
  /**
   * Upper bound.
   */
  u:Arg2 <- u;

  override function supportsLazy() -> Boolean {
    return true;
  }

  override function simulate() -> Real! {
    return simulate_uniform(value(l), value(u));
  }

  override function simulateLazy() -> Real!? {
    return simulate_uniform(peek(l), peek(u));
  }

  override function logpdf(x:Real!) -> Real! {
    return logpdf_uniform(x, value(l), value(u));
  }

  override function logpdfLazy(x:Expression<Real>) -> Expression<Real>? {
    return box(logpdf_uniform(x, l, u));
  }

  override function cdf(x:Real!) -> Real!? {
    return cdf_uniform(x, value(l), value(u));
  }

  override function quantile(P:Real!) -> Real!? {
    return quantile_uniform(P, value(l), value(u));
  }

  override function lower() -> Real!? {
    return value(l);
  }
  
  override function upper() -> Real!? {
    return value(u);
  }

  override function write(buffer:Buffer) {
    buffer.set("class", "Uniform");
    buffer.set("l", value(l));
    buffer.set("u", value(u));
  }
}

/**
 * Create a uniform distribution.
 */
function Uniform<Arg1,Arg2>(l:Arg1, u:Arg2) -> {
  return wrap_uniform(wrap(l), wrap(u));
}
function wrap_uniform<Arg1,Arg2>(l:Arg1, u:Arg2) -> {
  return construct<UniformDistribution<Arg1,Arg2>>(l, u);
}

hpp{{
using numbirch::simulate_uniform;
}}

/*
 * Observe a uniform variate.
 *
 * - x: The variate.
 * - l: Lower bound of interval.
 * - u: Upper bound of interval.
 *
 * Returns: the log probability density.
 */
function logpdf_uniform<Arg1,Arg2,Arg3>(x:Arg1, l:Arg2, u:Arg3) -> {
  return if_then_else(l <= x && x <= u, -log(u - l), -inf);
}

/*
 * CDF of a uniform variate.
 *
 * - x: The variate.
 * - l: Lower bound of interval.
 * - u: Upper bound of interval.
 *
 * Return: the cumulative probability.
 */
function cdf_uniform<Arg1,Arg2,Arg3>(x:Arg1, l:Arg2, u:Arg3) -> {
  return if_then_else(x <= l, 0.0, if_then_else(x > u, 1.0, (x - l)/(u - l)));
}

/*
 * Quantile of a uniform distribution.
 *
 * - P: The cumulative probability.
 * - l: Lower bound of interval.
 * - u: Upper bound of interval.
 *
 * Return: the quantile.
 */
function quantile_uniform<Arg1,Arg2,Arg3>(P:Arg1, l:Arg2, u:Arg3) -> {
  return l + P*(u - l);
}
