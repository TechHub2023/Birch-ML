cpp{{
#include <boost/math/distributions/gamma.hpp>
}}

/**
 * Gamma distribution.
 */
final class GammaDistribution<Arg1,Arg2>(k:Arg1, θ:Arg2) < Distribution<Real> {
  /**
   * Shape.
   */
  k:Arg1 <- k;
  
  /**
   * Scale.
   */
  θ:Arg2 <- θ;

  override function supportsLazy() -> Boolean {
    return true;
  }

  override function simulate() -> Real {
    return simulate_gamma(value(k), value(θ));
  }

  override function simulateLazy() -> Real? {
    return simulate_gamma(peek(k), peek(θ));
  }
  
  override function logpdf(x:Real) -> Real {
    return logpdf_gamma(x, value(k), value(θ));
  }

  override function logpdfLazy(x:Expression<Real>) -> Expression<Real>? {
    return box(logpdf_gamma(x, k, θ));
  }

  override function cdf(x:Real) -> Real? {
    return cdf_gamma(x, value(k), value(θ));
  }

  override function quantile(P:Real) -> Real? {
    return quantile_gamma(P, value(k), value(θ));
  }

  override function lower() -> Real? {
    return 0.0;
  }

  override function isGamma() -> Boolean {
    return true;
  }

  override function getGamma() -> (Expression<Real>, Expression<Real>)? {
    return box(k, θ);
  }

  override function write(buffer:Buffer) {
    buffer.set("class", "Gamma");
    buffer.set("k", value(k));
    buffer.set("θ", value(θ));
  }
}

/**
 * Create gamma distribution.
 */
function Gamma<Arg1,Arg2>(k:Arg1, θ:Arg2) -> {
  return construct<GammaDistribution<Arg1,Arg2>>(k, θ);
}

/*
 * Simulate a gamma distribution.
 *
 * - k: Shape.
 * - θ: Scale.
 */
function simulate_gamma(k:Real, θ:Real) -> Real {
  assert 0.0 < k;
  assert 0.0 < θ;
  cpp{{
  return std::gamma_distribution<Real>(k, θ)(rng);
  }}
}

/*
 * Observe a gamma variate.
 *
 * - x: The variate.
 * - k: Shape.
 * - θ: Scale.
 *
 * Returns: the log probability density.
 */
function logpdf_gamma<Arg1,Arg2,Arg3>(x:Arg1, k:Arg2, θ:Arg3) -> {
  return if_then_else(0.0 < x, (k - 1.0)*log(x) - x/θ - lgamma(k) - k*log(θ),
      -inf);
}

/*
 * CDF of a gamma variate.
 *
 * - x: The variate.
 * - k: Shape.
 * - θ: Scale.
 *
 * Return: the cumulative probability.
 */
function cdf_gamma(x:Real, k:Real, θ:Real) -> Real {
  assert 0.0 < k;
  assert 0.0 < θ;
  return if_then_else(0.0 < x, lower_inc_gamma(k, x/θ), 0.0);
}

/*
 * Quantile of a gamma distribution.
 *
 * - P: The cumulative probability.
 * - k: Shape.
 * - θ: Scale.
 *
 * Return: the quantile.
 */
function quantile_gamma(P:Real, k:Real, θ:Real) -> Real {
  assert 0.0 < k;
  assert 0.0 < θ;
  cpp{{
  return boost::math::quantile(boost::math::gamma_distribution<>(k, θ), P);
  }}
}
