hpp{{
#include <boost/math/distributions/pareto.hpp>
}}

/**
 * Gamma-exponential distribution.
 */
final class GammaExponentialDistribution<Arg1,Arg2>(k:Arg1, θ:Arg2) <
    Distribution<Real> {
  /**
   * Shape.
   */
  k:Arg1 <- k;

  /**
   * Scale.
   */
  θ:Arg2 <- θ;

  override function supportsLazy() -> Boolean {
    return true;
  }

  override function simulate() -> Real! {
    return simulate_lomax(1.0/value(θ), value(k));
  }

  override function simulateLazy() -> Real!? {
    return simulate_lomax(1.0/peek(θ), peek(k));
  }

  override function logpdf(x:Real!) -> Real! {
    return logpdf_lomax(x, 1.0/value(θ), value(k));
  }

  override function logpdfLazy(x:Expression<Real>) -> Expression<Real>? {
    return box(logpdf_lomax(x, 1.0/θ, k));
  }

  override function update(x:Real!) -> Delay? {
    return update_gamma_exponential(x, value(k), value(θ));
  }

  override function updateLazy(x:Expression<Real>) -> Delay? {
    return update_gamma_exponential(x, k, θ);
  }

  override function cdf(x:Real!) -> Real!? {
    return cdf_lomax(x, 1.0/value(θ), value(k));
  }

  override function quantile(P:Real!) -> Real!? {
    return quantile_lomax(P, 1.0/value(θ), value(k));
  }

  override function lower() -> Real!? {
    return 0.0;
  }
}

function GammaExponential<Arg1,Arg2>(k:Arg1, θ:Arg2) -> {
  return construct<GammaExponentialDistribution<Arg1,Arg2>>(k, θ);
}

/**
 * Create Exponential distribution.
 */
function Exponential(λ:Random<Real>) -> Distribution<Real> {
  λ.prune();
  if λ.hasGamma() {
    let (k, θ) <- λ.getGamma();
    let p <- GammaExponential(k, θ);
    λ.graft(p);
    return p;
  } else {
    return construct<ExponentialDistribution<Random<Real>>>(λ);
  }
}

/*
 * Simulate a Lomax distribution.
 *
 * - λ: Scale.
 * - α: Shape.
 */
function simulate_lomax<Arg1,Arg2>(λ:Arg1, α:Arg2) -> {
  let u <- simulate_uniform(0.0, 1.0);
  return λ*(pow(u, -1.0/α)-1.0);
}

/*
 * Observe of a Lomax variate.
 *
 * - x: The variate.
 * - λ: Scale.
 * - α: Shape.
 *
 * Return: the log probability density.
 */
function logpdf_lomax<Arg1,Arg2,Arg3>(x:Arg1, λ:Arg2, α:Arg3) -> {
  return if_then_else(0.0 <= x, log(α) - log(λ) - (α + 1.0)*log1p(x/λ), -inf);
}

/*
 * Update the parameters of a Gamma distribution with an exponential
 * likelihood.
 *
 * - x: The variate.
 * - k: Prior shape.
 * - θ: Prior scale.
 *
 * Returns: the posterior hyperparameters `k'` and `θ'`.
 */
function update_gamma_exponential<Arg1,Arg2,Arg3>(x:Arg1, k:Arg2, θ:Arg3) -> {
  return Gamma(k + 1.0, θ/(1.0 + x*θ));
}

/*
 * CDF of a Lomax variate.
 *
 * - x: The variate.
 * - λ: Scale.
 * - α: Shape.
 *
 * Return: the cumulative probability.
 */
function cdf_lomax<Arg1,Arg2,Arg3>(x:Arg1, λ:Arg2, α:Arg3) -> {
  return if_then_else(0.0 < x, 1.0 - pow((1.0 + x/λ), -α), 0.0);
}

/*
 * Quantile of a Lomax distribution.
 *
 * - P: The cumulative probability.
 * - λ: Scale.
 * - α: Shape.
 *
 * Return: the quantile.
 */
function quantile_lomax<Arg1,Arg2,Arg3>(P:Arg1, λ:Arg2, α:Arg3) -> {
  cpp{{
  return boost::math::quantile(boost::math::pareto_distribution<Real>(
      wait(λ), wait(α)), wait(P)) - wait(λ);
  }}
}
