hpp{{
#include <boost/math/distributions/poisson.hpp>
}}

/**
 * Poisson distribution.
 */
final class PoissonDistribution<Arg>(λ:Arg) < DiscreteDistribution {
  /**
   * Rate.
   */
  λ:Arg <- λ;

  override function supportsLazy() -> Boolean {
    return true;
  }

  override function simulate() -> Integer! {
    return simulate_poisson(value(λ));
  }

  override function simulateLazy() -> Integer!? {
    return simulate_poisson(peek(λ));
  }

  override function logpdf(x:Integer!) -> Real! {
    return logpdf_poisson(x, value(λ));
  }

  override function logpdfLazy(x:Expression<Integer>) -> Expression<Real>? {
    return box(logpdf_poisson(x, λ));
  }

  override function cdf(x:Integer!) -> Real!? {
    return cdf_poisson(x, value(λ));
  }

  override function quantile(P:Real!) -> Integer!? {
    return quantile_poisson(P, value(λ));
  }

  override function lower() -> Integer!? {
    return 0;
  }

  override function write(buffer:Buffer) {
    buffer.set("class", "Poisson");
    buffer.set("λ", value(λ));
  }
}

/**
 * Create Poisson distribution.
 */
function Poisson<Arg>(λ:Arg) -> Distribution<Integer> {
  return wrap_poisson(wrap(λ));
}
function wrap_poisson<Arg>(λ:Arg) -> Distribution<Integer> {
  return construct<PoissonDistribution<Arg>>(λ);
}

/*
 * Simulate a Poisson distribution.
 *
 * - λ: Rate.
 */
function simulate_poisson<Arg1>(λ:Arg1) -> {
  cpp{{
  return std::poisson_distribution<Integer>(wait(λ))(rng);
  }}
}

/*
 * Observe a Poisson variate.
 *
 * - x: The variate.
 * - λ: Rate.
 *
 * Returns: the log probability mass.
 */
function logpdf_poisson<Arg1,Arg2>(x:Arg1, λ:Arg2) -> {
  return x*log(λ) - λ - lfact(x);
}

/*
 * CDF of a Poisson variate.
 *
 * - x: The variate.
 * - λ: Rate.
 *
 * Return: the cumulative probability.
 */
function cdf_poisson<Arg1,Arg2>(x:Arg1, λ:Arg2) -> {
  return gamma_q(floor(x + 1.0), λ);
}

/*
 * Quantile of a Poisson distribution.
 *
 * - P: The cumulative probability.
 * - λ: Rate.
 *
 * Return: the quantile.
 */
function quantile_poisson<Arg1,Arg2>(P:Arg1, λ:Arg2) -> {
  cpp{{
  return boost::math::quantile(boost::math::poisson_distribution<Real>(
      wait(λ)), wait(P));
  }}
}
