/**
 * Multivariate Gaussian with linear transformation of a
 * multivariate-normal-inverse-Wishart prior.
 */
class MultivariateNormalInverseWishartMultivariateGaussianDistribution<
    Arg1,Arg2,Arg3,Arg4,Arg5,Arg6,Arg7>(a:Arg1, μ:Arg2, σ2:Arg3, Ψ:Arg4,
    k:Arg5, c:Arg6, ω2:Arg7) < MultivariateTDistribution<Arg5,
    Expression<Real[_]>,Expression<Real[_,_]>>(k, box(a*μ + c),
    box(sqrt(σ2*a*a + ω2)*Ψ)) {
  /**
   * Scale.
   */
  a:Arg1 <- a;

  /**
   * Mean.
   */
  μ:Arg2 <- μ;

  /**
   * Scale.
   */
  σ2:Arg3 <- σ2;
  
  /**
   * Spread.
   */
  Ψ:Arg4 <- Ψ;

  /**
   * Offset.
   */
  c:Arg6 <- c;

  /**
   * Likelihood scale.
   */
  ω2:Arg7 <- ω2;

  override function supportsLazy() -> Boolean {
    return true;
  }

  override function update(x:Real[_]) -> Delay? {
    return update_multivariate_normal_inverse_wishart_multivariate_gaussian(
        x, value(a), value(μ), value(σ2), value(Ψ), value(this.k), value(c),
        value(ω2), value(super.μ));
  }

  override function updateLazy(x:Expression<Real[_]>) -> Delay? {
    return update_multivariate_normal_inverse_wishart_multivariate_gaussian(
        x, a, μ, σ2, Ψ, this.k, c, ω2, super.μ);
  }

  override function constant() {
    super.constant();
    global.constant(a);
    global.constant(μ);
    global.constant(σ2);
    global.constant(Ψ);
    global.constant(c);
    global.constant(ω2);
  }
}

function MultivariateNormalInverseWishartMultivariateGaussian<Arg1,Arg2,
    Arg3,Arg4,Arg5,Arg6,Arg7>(a:Arg1, μ:Arg2, σ2:Arg3, Ψ:Arg4, k:Arg5, c:Arg6,
    ω2:Arg7) -> Distribution<Real[_]> {
  return wrap_multivariate_normal_inverse_wishart_multivariate_gaussian(a, μ,
      σ2, chol(Ψ), k, c, ω2);
}
function wrap_multivariate_normal_inverse_wishart_multivariate_gaussian<
    Arg1,Arg2,Arg3,Arg4,Arg5,Arg6,Arg7>(a:Arg1, μ:Arg2, σ2:Arg3, Ψ:Arg4,
    k:Arg5, c:Arg6, ω2:Arg7) -> {
  return make_multivariate_normal_inverse_wishart_multivariate_gaussian(
      wrap(a), wrap(μ), wrap(σ2), wrap(Ψ), wrap(k), wrap(c), wrap(ω2));
}
function make_multivariate_normal_inverse_wishart_multivariate_gaussian<
    Arg1,Arg2,Arg3,Arg4,Arg5,Arg6,Arg7>(a:Arg1, μ:Arg2, σ2:Arg3, Ψ:Arg4,
    k:Arg5, c:Arg6, ω2:Arg7) -> {
  return construct<MultivariateNormalInverseWishartMultivariateGaussianDistribution<
      Arg1,Arg2,Arg3,Arg4,Arg5,Arg6,Arg7>>(a, μ, σ2, Ψ, k, c, ω2);
}

/**
 * Create multivariate Gaussian distribution.
 */
function MultivariateGaussian<Arg1,Arg2,Arg3>(
      μ:OpAdd<OpMul<Arg1,Random<Real[_]>>,Arg2>,
      Σ:OpMul<Arg3,Random<Real[_,_]>>) -> Distribution<Real[_]> {
  μ.l.r.prune();
  if μ.l.r.hasMultivariateNormalInverseWishart() && μ.l.r == Σ.r.next(2) {
    let (μ1, σ2, Ψ, k) <- μ.l.r.getMultivariateNormalInverseWishart();
    let p <- wrap_multivariate_normal_inverse_wishart_multivariate_gaussian(
        μ.l.l, μ1, σ2, Ψ, k, μ.r, Σ.l);
    μ.l.r.graft(p);
    μ.l.r.join(Σ.r);
    return p;
  } else {
    Σ.r.prune();
    if Σ.r.hasInverseWishart() {
      let (Ψ, k) <- Σ.r.getInverseWishart();
      let p <- wrap_multivariate_normal_inverse_wishart(μ, Σ.l, Ψ, k);
      Σ.r.graft(p);
      return p;
    } else {
      return wrap_multivariate_gaussian(μ, Σ);
    }
  }
}

/**
 * Create multivariate Gaussian distribution.
 */
function MultivariateGaussian<Arg1,Arg2>(μ:OpAdd<Random<Real[_]>,Arg1>,
    Σ:OpMul<Arg2,Random<Real[_,_]>>) -> Distribution<Real[_]> {
  return MultivariateGaussian(1.0*μ.l + μ.r, Σ);
}

/**
 * Create multivariate Gaussian distribution.
 */
function MultivariateGaussian<Arg1,Arg2>(μ:OpMul<Arg1,Random<Real[_]>>,
    Σ:OpMul<Arg2,Random<Real[_,_]>>) -> Distribution<Real[_]> {
  return MultivariateGaussian(μ + 0.0, Σ);
}

/**
 * Create multivariate Gaussian distribution.
 */
function MultivariateGaussian<Arg1>(μ:Random<Real[_]>,
    Σ:OpMul<Arg1,Random<Real[_,_]>>) -> Distribution<Real[_]> {
  return MultivariateGaussian(1.0*μ + 0.0, Σ);
}

/**
 * Create multivariate Gaussian distribution.
 */
function MultivariateGaussian<Arg1,Arg2>(
      μ:OpAdd<OpMul<Arg1,Random<Real[_]>>,Arg2>, Σ:Random<Real[_,_]>) ->
      Distribution<Real[_]> {
  return MultivariateGaussian(μ, 1.0*Σ);
}

/**
 * Create multivariate Gaussian distribution.
 */
function MultivariateGaussian<Arg1>(μ:OpAdd<Random<Real[_]>,Arg1>,
    Σ:Random<Real[_,_]>) -> Distribution<Real[_]> {
  return MultivariateGaussian(1.0*μ.l + μ.r, 1.0*Σ);
}

/**
 * Create multivariate Gaussian distribution.
 */
function MultivariateGaussian<Arg1>(μ:OpMul<Arg1,Random<Real[_]>>,
    Σ:Random<Real[_,_]>) -> Distribution<Real[_]> {
  return MultivariateGaussian(μ + 0.0, 1.0*Σ);
}

/**
 * Create multivariate Gaussian distribution.
 */
function MultivariateGaussian(μ:Random<Real[_]>, Σ:Random<Real[_,_]>) ->
    Distribution<Real[_]> {
  return MultivariateGaussian(1.0*μ + 0.0, 1.0*Σ);
}

/*
 * Update the parameters of a Gaussian variate with linear transformation
 * of multivariate-normal-inverse-Wishart prior.
 *
 * - x: The variate.
 * - a: Scale.
 * - μ: Prior precision times mean multivariate.
 * - σ2: Prior precision.
 * - Ψ: Prior variance shape.
 * - k: Prior degrees of freedom.
 * - c: Offset.
 * - ω2: Among-row covariance.
 * - N: Precomputed marginal mean.
 *
 * Returns: the posterior hyperparameters `μ'`, `σ2'`, `Ψ'` and `k'`.
 */
function update_multivariate_normal_inverse_wishart_multivariate_gaussian<
    Arg1,Arg2,Arg3,Arg4,Arg5,Arg6,Arg7,Arg8,Arg9>(x:Arg1, a:Arg2, μ:Arg3,
    σ2:Arg4, Ψ:Arg5, k:Arg6, c:Arg7, ω2:Arg8, N:Arg9) -> {
  let σ2' <- wrap(1.0/(1.0/σ2 + a*a/ω2));
  let μ' <- wrap(σ2'*(μ/σ2 + a*(x - c)/ω2));
  let Ψ' <- chol(triouter(Ψ) + outer(x - c)/ω2 + outer(μ)/σ2 - outer(μ')/σ2');
  let k' <- k + 1;
  return wrap_multivariate_normal_inverse_wishart(μ', σ2', Ψ', k');
}
