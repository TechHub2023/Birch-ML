/**
 * Multivariate Gaussian distribution with linear transformation of
 * matrix-normal-inverse-Wishart prior.
 */
final class ScaledMatrixNormalInverseWishartMultivariateGaussian<Arg1,Arg2,
    Arg3,Arg4,Arg5>(a:Arg1, N:Arg2, Λ:Arg3, Ψ:Arg4, k:Arg5) <
    Distribution<Real[_]> {
  /**
   * Scale.
   */
  a:Arg1 <- a;

  /**
   * Among-row precision times mean.
   */
  N:Arg2 <- N;

  /**
   * Among-row precision.
   */
  Λ:Arg3 <- Λ;
  
  /**
   * Among-column covariance scale.
   */
  Ψ:Arg4 <- Ψ;
  
  /**
   * Among-column covariance degrees of freedom.
   */
  k:Arg5 <- k;

  override function supportsLazy() -> Boolean {
    return true;
  }

  override function simulate() -> Real[_] {
    return simulate_scaled_matrix_normal_inverse_wishart_multivariate_gaussian(
        value(a), value(N), value(Λ), value(Ψ), value(k));
  }

  override function simulateLazy() -> Real[_]? {
    return simulate_scaled_matrix_normal_inverse_wishart_multivariate_gaussian(
        peak(a), peak(N), peak(Λ), peak(Ψ), peak(k));
  }
  
  override function logpdf(x:Real[_]) -> Real {
    return logpdf_scaled_matrix_normal_inverse_wishart_multivariate_gaussian(
        x, value(a), value(N), value(Λ), value(Ψ), value(k));
  }

  override function logpdfLazy(x:Expression<Real[_]>) -> Expression<Real>? {
    return box(logpdf_scaled_matrix_normal_inverse_wishart_multivariate_gaussian(
        x, a, N, Λ, Ψ, k));
  }

  override function update(x:Real[_]) -> Delay? {
    return update_scaled_matrix_normal_inverse_wishart_multivariate_gaussian(
        x, value(a), value(N), value(Λ), value(Ψ), value(k));
  }

  override function updateLazy(x:Expression<Real[_]>) -> Delay? {
    return update_scaled_matrix_normal_inverse_wishart_multivariate_gaussian(
        x, a, N, Λ, Ψ, k);
  }
}

function ScaledMatrixNormalInverseWishartMultivariateGaussian<Arg1,Arg2,
    Arg3,Arg4,Arg5>(a:Arg1, N:Arg2, Λ:Arg3, Ψ:Arg4, k:Arg5) -> {
  return construct<ScaledMatrixNormalInverseWishartMultivariateGaussian<Arg1,
      Arg2,Arg3,Arg4,Arg5>>(a, N, Λ, Ψ, k);
}

/**
 * Create multivariate Gaussian distribution.
 */
function MultivariateGaussian<Arg>(μ:Dot<Arg,Random<Real[_,_]>>,
    Σ:Random<Real[_,_]>) -> Distribution<Real[_]> {
  if μ.r.hasMatrixNormalInverseWishart(Σ) {
    μ.r.prune();
    let (N, Λ, Ψ, k) <- μ.r.getMatrixNormalInverseWishart();
    let p <- ScaledMatrixNormalInverseWishartMultivariateGaussian(μ.l, N,
        Λ, Ψ, k);
    μ.r.graft(p);
    return p;
  } else if Σ.hasInverseWishart() {
    return construct<MultivariateGaussian<Dot<Arg,Random<Real[_,_]>>,
        Random<Real[_,_]>>>(μ, Σ);
  } else {
      return construct<MultivariateGaussian<Dot<Arg,Random<Real[_,_]>>,
          Random<Real[_,_]>>>(μ, Σ);
  }
}

/*
 * Simulate a Gaussian distribution with linear transformation of a
 * matrix-normal-inverse-Wishart prior.
 *
 * - a: Scale.
 * - N: Precision times mean matrix.
 * - Λ: Precision.
 * - Ψ: Variance shape.
 * - k: Degrees of freedom.
 */
function simulate_scaled_matrix_normal_inverse_wishart_multivariate_gaussian(
    a:Real[_], N:Real[_,_], Λ:Real[_,_], Ψ:Real[_,_], k:Real) -> Real[_] {
  let S <- cholinv(Λ);
  let M <- S*N;
  let σ2 <- 1.0 + dot(a, S*a);
  return simulate_multivariate_t(k, dot(a, M), σ2*Ψ);
}

/*
 * Observe a Gaussian variate with linear transformation of a
 * matrix-normal-inverse-Wishart prior.
 *
 * - x: The variate.
 * - a: Scale.
 * - N: Prior precision times mean matrix.
 * - Λ: Prior precision.
 * - Ψ: Prior variance shape.
 * - k: Prior degrees of freedom.
 *
 * Returns: the log probability density.
 */
function logpdf_scaled_matrix_normal_inverse_wishart_multivariate_gaussian<
    Arg1,Arg2,Arg3,Arg4,Arg5,Arg6>(x:Arg1, a:Arg2, N:Arg3, Λ:Arg4, Ψ:Arg5,
    k:Arg6) -> {
  let S <- cholinv(Λ);
  let M <- S*N;
  let σ2 <- 1.0 + dot(a, S*a);
  return logpdf_multivariate_t(x, k, dot(a, M), σ2*Ψ);
}

/*
 * Update the parameters of a Gaussian variate with linear transformation
 * of matrix-normal-inverse-Wishart prior.
 *
 * - x: The variate.
 * - a: Scale.
 * - N: Prior precision times mean matrix.
 * - Λ: Prior precision.
 * - Ψ: Prior variance shape.
 * - k: Prior degrees of freedom.
 *
 * Returns: the posterior hyperparameters `N'`, `Λ'`, `Ψ'` and `k'`.
 */
function update_scaled_matrix_normal_inverse_wishart_multivariate_gaussian<
    Arg1,Arg2,Arg3,Arg4,Arg5,Arg6>(x:Arg1, a:Arg2, N:Arg3, Λ:Arg4, Ψ:Arg5,
    k:Arg6) -> {
  let Λ' <- Λ + outer(a);
  let N' <- N + outer(a, x);
  let M <- cholsolve(Λ, N);
  let M' <- cholsolve(Λ', N');
  let Ψ' <- Ψ + outer(x) + inner(M, N) - inner(M', N');
  let k' <- k + 1.0;
  return MatrixNormalInverseWishart(N', Λ', Ψ', k');
}
