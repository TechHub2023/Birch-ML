hpp{{
#include <boost/math/distributions/uniform.hpp>
}}

/**
 * Uniform integer distribution.
 */
final class UniformIntegerDistribution<Arg1,Arg2>(l:Arg1, u:Arg2) <
    BoundedDiscreteDistribution {
  /**
   * Lower bound.
   */
  l:Arg1 <- l;

  /**
   * Upper bound.
   */
  u:Arg2 <- u;

  override function supportsLazy() -> Boolean {
    return false;
  }

  override function simulate() -> Integer! {
    return simulate_uniform_int(value(l), value(u));
  }

  override function logpdf(x:Integer!) -> Real! {
    return logpdf_uniform_int(x, value(l), value(u));
  }

  override function cdf(x:Integer!) -> Real!? {
    return cdf_uniform_int(x, value(l), value(u));
  }

  override function quantile(P:Real!) -> Integer!? {
    return quantile_uniform_int(P, value(l), value(u));
  }
  
  override function lower() -> Integer!? {
    return value(l);
  }
  
  override function upper() -> Integer!? {
    return value(u);
  }

  override function constant() {
    super.constant();
    global.constant(l);
    global.constant(u);
  }

  override function write(buffer:Buffer) {
    buffer.set("class", "UniformInteger");
    buffer.set("l", value(l));
    buffer.set("u", value(u));
  }
}

/**
 * Create uniform distribution over integers.
 */
function UniformInteger<Arg1,Arg2>(l:Arg1, u:Arg2) -> Distribution<Integer> {
  return wrap_uniform_integer(l, u);
}
function wrap_uniform_integer<Arg1,Arg2>(l:Arg1, u:Arg2) -> {
  return make_uniform_integer(wrap(l), wrap(u));
}
function make_uniform_integer<Arg1,Arg2>(l:Arg1, u:Arg2) -> {
  return construct<UniformIntegerDistribution<Arg1,Arg2>>(l, u);
}

hpp{{
using numbirch::simulate_uniform_int;
}}

/*
 * Observe an integer uniform variate.
 *
 * - x: The variate.
 * - l: Lower bound of interval.
 * - u: Upper bound of interval.
 *
 * Returns: the log probability mass.
 */
function logpdf_uniform_int<Arg1,Arg2,Arg3>(x:Arg1, l:Arg2, u:Arg3) -> {
  return if_then_else(x >= l && x <= u, -log1p(u - l), -inf);
}

/*
 * CDF of a uniform integer variate.
 *
 * - x: The variate.
 * - l: Lower bound of interval.
 * - u: Upper bound of interval.
 *
 * Return: the cumulative probability.
 */
function cdf_uniform_int<Arg1,Arg2,Arg3>(x:Arg1, l:Arg2, u:Arg3) -> {
  return if_then_else(x < l, 0.0, if_then_else(x <= u, (x - l + 1.0)/
      (u - l + 1.0), 1.0));
}

/*
 * Quantile of a uniform integer distribution.
 *
 * - P: The cumulative probability.
 * - l: Lower bound of interval.
 * - u: Upper bound of interval.
 *
 * Return: the quantile.
 */
function quantile_uniform_int(P:Real, l:Integer, u:Integer) -> Integer {
  let P1 <- wait(P);
  let l1 <- wait(l);
  let u1 <- wait(u);
  return l1 + cast<Integer>(P1*(u1 - l1));
}
