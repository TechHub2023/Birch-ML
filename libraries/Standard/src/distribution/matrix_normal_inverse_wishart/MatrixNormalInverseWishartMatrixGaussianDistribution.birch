/**
 * Matrix normal-inverse-Wishart-Gaussian distribution.
 */
final class MatrixNormalInverseWishartMatrixGaussianDistribution<Arg1,Arg2,Arg3,Arg4,
    Arg5>(N:Arg1, Λ:Arg2, Ψ:Arg3, k:Arg4, U:Arg5) < Distribution<Real[_,_]> {
  /**
   * Among-row precision times mean.
   */
  N:Arg1 <- N;

  /**
   * Among-row precision.
   */
  Λ:Arg2 <- Λ;
  
  /**
   * Among-column covariance scale.
   */
  Ψ:Arg3 <- Ψ;
  
  /**
   * Among-column covariance degrees of freedom.
   */
  k:Arg4 <- k;

  /**
   * Among-row covariance.
   */
  U:Arg5 <- U;

  override function supportsLazy() -> Boolean {
    return true;
  }

  override function simulate() -> Real[_,_] {
    return simulate_matrix_normal_inverse_wishart_matrix_gaussian(value(N),
        value(Λ), value(Ψ), value(k), value(U));
  }

  override function simulateLazy() -> Real[_,_]? {
    return simulate_matrix_normal_inverse_wishart_matrix_gaussian(peek(N),
        peek(Λ), peek(Ψ), peek(k), peek(U));
  }
  
  override function logpdf(X:Real[_,_]) -> Real {
    return logpdf_matrix_normal_inverse_wishart_matrix_gaussian(X, value(N),
        value(Λ), value(Ψ), value(k), value(U));
  }

  override function logpdfLazy(X:Expression<Real[_,_]>) -> Expression<Real>? {
    return box(logpdf_matrix_normal_inverse_wishart_matrix_gaussian(X, N, Λ,
        Ψ, k, U));
  }

  override function update(X:Real[_,_]) -> Delay? {
    return update_matrix_normal_inverse_wishart_matrix_gaussian(X, value(N),
        value(Λ), value(Ψ), value(k), value(U));
  }

  override function updateLazy(X:Expression<Real[_,_]>) -> Delay? {
    return update_matrix_normal_inverse_wishart_matrix_gaussian(X, N, Λ,
        Ψ, k, U);
  }
}

function MatrixNormalInverseWishartMatrixGaussian<Arg1,Arg2,Arg3,Arg4,
    Arg5>(N:Arg1, Λ:Arg2, Ψ:Arg3, k:Arg4, U:Arg5) -> {
  return construct<MatrixNormalInverseWishartMatrixGaussianDistribution<Arg1,Arg2,Arg3,
      Arg4,Arg5>>(N, Λ, Ψ, k, U);
}

/**
 * Create matrix Gaussian distribution.
 */
function MatrixGaussian<Arg>(M:Random<Real[_,_]>, U:Arg,
    V:Random<Real[_,_]>) -> Distribution<Real[_,_]> {
  M.prune();
  if M.hasMatrixNormalInverseWishart() && M.isGrandChildOf(V) {
    let (N, Λ, Ψ, k) <- M.getMatrixNormalInverseWishart();
    let p <- MatrixNormalInverseWishartMatrixGaussian(N, Λ, Ψ, k, U);
    M.graft(p, 1);
    V.graftReplace(p, 2);
    M.join(V);
    return p;
  } else {
    V.prune();
    if V.hasInverseWishart() {
      let (Ψ, k) <- V.getInverseWishart();
      let Λ <- cholinv(U);
      let p <- MatrixNormalInverseWishart(Λ*M, Λ, Ψ, k);
      V.graft(p);
      return p;
    } else {
      return construct<MatrixGaussianDistribution<Random<Real[_,_]>,Arg,Random<Real[_,_]>>>(
          M, U, V);
    }
  }
}

/*
 * Simulate a Gaussian distribution with matrix-normal-inverse-Wishart prior.
 *
 * - N: Precision times mean matrix.
 * - Λ: Precision.
 * - Ψ: Among-column covariance shape.
 * - k: Among-column covariance degrees of freedom.
 * - U: Among-row covariance.
 */
function simulate_matrix_normal_inverse_wishart_matrix_gaussian(N:Real[_,_],
    Λ:Real[_,_], Ψ:Real[_,_], k:Real, U:Real[_,_]) -> Real[_,_] {
  let S <- cholinv(Λ);
  let M <- S*N;
  let Σ <- U + S;
  return simulate_matrix_t(k, M, Σ, Ψ);
}

/*
 * Observe a Gaussian variate with matrix-normal-inverse-Wishart prior.
 *
 * - X: The variate.
 * - N: Precision times mean matrix.
 * - Λ: Precision.
 * - Ψ: Among-column covariance shape.
 * - k: Among-column covariance degrees of freedom.
 * - U: Among-row covariance.
 *
 * Returns: the log probability density.
 */
function logpdf_matrix_normal_inverse_wishart_matrix_gaussian<Arg1,Arg2,
    Arg3,Arg4,Arg5,Arg6>(X:Arg1, N:Arg2, Λ:Arg3, Ψ:Arg4, k:Arg5, U:Arg6) -> {
  let S <- cholinv(Λ);
  let M <- S*N;
  let Σ <- U + S;
  return logpdf_matrix_t(X, k, M, Σ, Ψ);
}

/*
 * Update the parameters of a Gaussian variate with
 * matrix-normal-inverse-Wishart prior.
 *
 * - X: The variate.
 * - N: Prior precision times mean matrix.
 * - Λ: Prior precision.
 * - Ψ: Prior among-column covariance shape.
 * - k: Prior among-column covariance degrees of freedom.
 * - U: Among-row covariance.
 *
 * Returns: the posterior hyperparameters `N'`, `Λ'`, `V'` and `k'`.
 */
function update_matrix_normal_inverse_wishart_matrix_gaussian<Arg1,Arg2,
    Arg3,Arg4,Arg5,Arg6>(X:Arg1, N:Arg2, Λ:Arg3, Ψ:Arg4, k:Arg5, U:Arg6) -> {
  let n <- rows(X);
  let L <- cholinv(U);
  let Z <- L*X;
  let Λ' <- Λ + L;
  let N' <- N + Z;
  let M <- cholsolve(Λ, N);
  let M' <- cholsolve(Λ', N');
  let Ψ' <- Ψ + inner(X, Z) + inner(M, N) - inner(M', N');
  let k' <- k + n;
  return MatrixNormalInverseWishart(N', Λ', Ψ', k');
}
