/**
 * Delta function on a linear transformation of a discrete random
 * variate.
 */
final class LinearDiscrete(a:Expression<Integer>, μ:Discrete,
    c:Expression<Integer>) < Discrete {
  /**
   * Scale. Should be 1 or -1 to ensure integer-invertible.
   */
  a:Expression<Integer> <- a;
    
  /**
   * Location.
   */
  μ:Discrete <- μ;

  /**
   * Offset.
   */
  c:Expression<Integer> <- c;

  function supportsLazy() -> Boolean {
    return false;
  }

  function simulate() -> Integer {
    return simulate_delta(a.value()*μ.simulate() + c.value());
  }

//  function simulateLazy() -> Integer? {
//    return simulate_delta(a.eval()*μ.simulateLazy()! + c.eval());
//  }
  
  function logpdf(x:Integer) -> Real {
    return μ.logpdf((x - c.value())/a.value()) - log(abs(Real(a.value())));
  }

//  function logpdfLazy(x:Expression<Integer>) -> Expression<Real>? {
//    return μ.logpdfLazy((x - c)/a) - log(abs(Real(a)));
//  }
  
  function update(x:Integer) {
    μ.clamp((x - c.value())/a.value());
  }

//  function updateLazy(x:Expression<Integer>) {
//
//  }

  function cdf(x:Integer) -> Real? {
    return μ.cdf((x - c.value())/a.value());
  }

  function lower() -> Integer? {
    let l <- μ.lower();
    if l? {
      l <- a.value()*l! + c.value();
    }
    return l;
  }
  
  function upper() -> Integer? {
    let u <- μ.upper();
    if u? {
      u <- a.value()*u! + c.value();
    }
    return u;
  }

  function link() {
    μ.setChild(this);
  }
  
  function unlink() {
    μ.releaseChild(this);
  }
}

function LinearDiscrete(a:Expression<Integer>, μ:Discrete,
    c:Expression<Integer>) -> LinearDiscrete {
  m:LinearDiscrete(a, μ, c);
  m.link();
  return m;
}
