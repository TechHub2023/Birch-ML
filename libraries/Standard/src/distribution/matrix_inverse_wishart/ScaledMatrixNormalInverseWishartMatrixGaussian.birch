/**
 * Matrix Gaussian variate with linear transformation of
 * matrix-normal-inverse-Wishart prior.
 */
final class ScaledMatrixNormalInverseWishartMatrixGaussian<Arg1,Arg2,Arg3,
    Arg4,Arg5,Arg6>(A:Arg1, N:Arg2, Λ:Arg3, Ψ:Arg4, k:Arg5, U:Arg6) <
    Distribution<Real[_,_]> {
  /**
   * Scale.
   */
  A:Arg1 <- A;

  /**
   * Among-row precision times mean.
   */
  N:Arg2 <- N;

  /**
   * Among-row precision.
   */
  Λ:Arg3 <- Λ;
  
  /**
   * Among-column covariance scale.
   */
  Ψ:Arg4 <- Ψ;
  
  /**
   * Among-column covariance degrees of freedom.
   */
  k:Arg5 <- k;

  /**
   * Among-row covariance.
   */
  U:Arg6 <- U;

  override function supportsLazy() -> Boolean {
    return true;
  }

  override function simulate() -> Real[_,_] {
    return simulate_scaled_matrix_normal_inverse_wishart_matrix_gaussian(
        value(A), value(N), value(Λ), value(Ψ), value(k), value(U));
  }

  override function simulateLazy() -> Real[_,_]? {
    return simulate_scaled_matrix_normal_inverse_wishart_matrix_gaussian(
        peak(A), peak(N), peak(Λ), peak(Ψ), peak(k), peak(U));
  }
  
  override function logpdf(X:Real[_,_]) -> Real {
    return logpdf_scaled_matrix_normal_inverse_wishart_matrix_gaussian(
        X, value(A), value(N), value(Λ), value(Ψ), value(k), value(U));
  }

  override function logpdfLazy(X:Expression<Real[_,_]>) -> Expression<Real>? {
    return box(logpdf_scaled_matrix_normal_inverse_wishart_matrix_gaussian(
        X, A, N, Λ, Ψ, k, U));
  }

  override function update(X:Real[_,_]) -> Delay? {
    return update_scaled_matrix_normal_inverse_wishart_matrix_gaussian(X,
        value(A), value(N), value(Λ), value(Ψ), value(k), value(U));
  }

  override function updateLazy(X:Expression<Real[_,_]>) -> Delay? {
    return update_scaled_matrix_normal_inverse_wishart_matrix_gaussian(X,
        A, N, Λ, Ψ, k, U);
  }
}

function ScaledMatrixNormalInverseWishartMatrixGaussian<Arg1,Arg2,Arg3,
    Arg4,Arg5,Arg6>(A:Arg1, N:Arg2, Λ:Arg3, Ψ:Arg4, k:Arg5, U:Arg6) -> {
  return construct<ScaledMatrixNormalInverseWishartMatrixGaussian<Arg1,Arg2,
      Arg3,Arg4,Arg5,Arg6>>(A, N, Λ, Ψ, k, U);
}

/**
 * Create matrix Gaussian distribution.
 */
function MatrixGaussian<Arg1,Arg2>(M:Multiply<Arg1,Random<Real[_,_]>>,
    U:Arg2, V:Random<Real[_,_]>) -> Distribution<Real[_,_]> {
  if M.hasMatrixNormalInverseWishart(V) {
    M.prune();
    let (N, Λ, Ψ, k) <- M.getMatrixNormalInverseWishart();
    let p <- ScaledMatrixNormalInverseWishartMatrixGaussian(M.l, N, Λ, Ψ,
        k, U);
    M.graft(p);
    return p;
  } else if V.hasInverseWishart() {
    V.prune();
    let (Ψ, k) <- V.getInverseWishart();
    let p <- MatrixNormalInverseWishart(M, U, Ψ, k);
    V.graft(p);
    return p;
  } else {
    return construct<MatrixGaussian<Multiply<Arg1,Random<Real[_,_]>>,Arg2,
        Random<Real[_,_]>>>(M, U, V);
  }
}

/*
 * Simulate a Gaussian distribution with linear transformation of as
 * matrix-normal-inverse-Wishart prior.
 *
 * - A: Scale.
 * - N: Precision times mean matrix.
 * - Λ: Precision.
 * - Ψ: Variance shape.
 * - k: Degrees of freedom.
 * - U: Among-row covariance.
 */
function simulate_scaled_matrix_normal_inverse_wishart_matrix_gaussian(
    A:Real[_,_], N:Real[_,_], Λ:Real[_,_], Ψ:Real[_,_], k:Real,
    U:Real[_,_]) -> Real[_,_] {
  let S <- cholinv(Λ);
  let M <- A*S*N;
  let Σ <- U + outer(A*S, A);
  return simulate_matrix_student_t(k, M, Σ, Ψ);
}

/*
 * Observe a Gaussian variate with linear transformation of a
 * matrix-normal-inverse-Wishart prior.
 *
 * - X: The variate.
 * - A: Scale.
 * - N: Prior precision times mean matrix.
 * - Λ: Prior precision.
 * - Ψ: Prior variance shape.
 * - k: Prior degrees of freedom.
 * - U: Among-row covariance.
 *
 * Returns: the log probability density.
 */
function logpdf_scaled_matrix_normal_inverse_wishart_matrix_gaussian<Arg1,
    Arg2,Arg3,Arg4,Arg5,Arg6,Arg7>(X:Arg1, A:Arg2, N:Arg3, Λ:Arg4, Ψ:Arg5,
    k:Arg6, U:Arg7) -> {
  let S <- cholinv(Λ);
  let M <- A*S*N;
  let Σ <- U + outer(A*S, A);
  return logpdf_matrix_student_t(X, k, M, Σ, Ψ);
}

/*
 * Update the parameters of a Gaussian variate with linear transformation
 * of matrix-normal-inverse-Wishart prior.
 *
 * - X: The variate.
 * - A: Scale.
 * - N: Prior precision times mean matrix.
 * - Λ: Prior precision.
 * - Ψ: Prior variance shape.
 * - k: Prior degrees of freedom.
 * - U: Among-row covariance.
 *
 * Returns: the posterior hyperparameters `N'`, `Λ'`, `Ψ'` and `k'`.
 */
function update_scaled_matrix_normal_inverse_wishart_matrix_gaussian<Arg1,
    Arg2,Arg3,Arg4,Arg5,Arg6,Arg7>(X:Arg1, A:Arg2, N:Arg3, Λ:Arg4, Ψ:Arg5,
    k:Arg6, U:Arg7) -> {
  let n <- rows(X);
  let L <- cholinv(U);
  let Z <- L*X;
  let Λ' <- Λ + inner(A, L*A);
  let N' <- N + inner(A, Z);
  let M <- cholsolve(Λ, N);
  let M' <- cholsolve(Λ', N');
  let Ψ' <- Ψ + inner(X, Z) + inner(M, N) - inner(M', N');
  let k' <- k + n;
  return MatrixNormalInverseWishart(N', Λ', Ψ', k');
}
