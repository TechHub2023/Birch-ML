/*
 * Distribution over the first of a pair of random variables given their sum.
 * This is produced by AddDiscreteDelta.update() once the sum is realized. In
 * turn, AddDiscreteConstrained.update() produces a Delta distribution over
 * the second of the pair once the first is realized.
 */
final class AddDiscreteConstrained(p:Real[_], l:Integer, y:Integer) <
    BoundedDiscrete {
  /**
   * Probabilities of values of the first of the pair, starting from `l`.
   */
  p:Real[_] <- p;

  /**
   * Lower bound on the value of the first of the pair.
   */
  l:Integer <- l;

  /**
   * Sum of the pair.
   */
  y:Integer <- y;
  
  override function simulate() -> Integer {
    return simulate_categorical(p) + l - 1;
  }
  
  override function logpdf(x:Integer) -> Real {
    return logpdf_categorical(x - l + 1, p);
  }
  
  override function update(x:Integer) -> Delay? {
    return Delta(y - x);
  }
  
  override function lower() -> Integer? {
    return l;
  }
  
  override function upper() -> Integer? {
    return l + length(p) - 1;
  }
}
