/**
 * Delta distribution on sum of two bounded discrete random variables.
 */
final class AddDiscreteDelta(p:BoundedDiscrete, q:BoundedDiscrete) <
    BoundedDiscrete {
  /**
   * Distribution over first random variable.
   */
  p:BoundedDiscrete <- p;

  /**
   * Distribution over second random variable.
   */
  q:BoundedDiscrete <- q;

  /**
   * Value at which probabilities have been enumerated.
   */
  x:Integer?;
  
  /**
   * If enumerated, then the lower bound on the support of `p`.
   */
  l:Integer <- 0;

  /**
   * If enumerated, then the upper bound on the support of `p`.
   */
  u:Integer <- 0;

  /**
   * If enumerated, then the probabilities across the support of `p`, starting
   * from `l`.
   */
  z:Real[_];
  
  /**
   * If enumerated, then the sum of `z`.
   */
  Z:Real <- 0.0;
  
  function enumerate(x:Integer) {
    if !this.x? || this.x! != x {
      l <- max(p.lower()!, x - q.upper()!);
      u <- min(p.upper()!, x - q.lower()!);
      if l <= u {
        /* distribution over possible pairs that produce the given sum */
        z <- vector_lambda(\(i:Integer) -> {
              let n <- l + i - 1;
              return p.logpdf(n) + q.logpdf(x - n);
            }, u - l + 1);
        Z <- log_sum_exp(z);
        z <- norm_exp(z);
      } else {
        Z <- -inf;
        z <- vector(0.0, 0);
      }
      this.x <- x;
    }
  }

  override function simulate() -> Integer {
    return p.simulate() + q.simulate();
  }
  
  override function logpdf(x:Integer) -> Real {
    enumerate(x);
    return Z;
  }
  
  override function update(x:Integer) -> Delay? {
    enumerate(x);
    return construct<AddDiscreteConstrained>(z, l, x);
  }
  
  override function lower() -> Integer? {
    return p.lower()! + q.lower()!;
  }
  
  override function upper() -> Integer? {
    return p.upper()! + q.upper()!;
  }
}

/**
 * Create delta distribution.
 */
function Delta(μ:Add<Random<Integer>,Random<Integer>>) ->
    Distribution<Integer> {
  μ.l.prune();
  μ.r.prune();
  if μ.l.hasBoundedDiscrete() && μ.r.hasBoundedDiscrete() {
    let p <- construct<AddDiscreteDelta>(μ.l.getBoundedDiscrete(),
        μ.r.getBoundedDiscrete());
    μ.l.graft(p, 1);
    μ.r.graft(p, 2);
    μ.l.join(μ.r);
    return p;
	} else if μ.l.hasBoundedDiscrete() {
    let r ~ Delta(μ.r);
    return Delta(μ.l + r);
  } else if μ.r.hasBoundedDiscrete() {
    let l ~ Delta(μ.l);
    return Delta(l + μ.r);
  } else {
		return construct<Delta<Add<Random<Integer>,Random<Integer>>>>(μ);
	}
}

/**
 * Create delta distribution.
 */
function Delta<Arg>(μ:Add<Random<Integer>,Arg>) -> {
  let r ~ Delta(μ.r);
  return Delta(μ.l + r);
}

/**
 * Create delta distribution.
 */
function Delta<Arg>(μ:Add<Arg,Random<Integer>>) -> {
  let l ~ Delta(μ.l);
  return Delta(l + μ.r);
}

/**
 * Create delta distribution.
 */
function Delta<Arg1,Arg2>(μ:Add<Arg1,Arg2>) -> {
  let l ~ Delta(μ.l);
  let r ~ Delta(μ.r);
  return Delta(l + r);
}
