/**
 * Delta function on a sum of two bounded discrete distributions.
 */
final class AddDiscreteDelta(x1:BoundedDiscrete, x2:BoundedDiscrete) <
    BoundedDiscrete {
  /**
   * First node.
   */
  x1:BoundedDiscrete <- x1;

  /**
   * Second node.
   */
  x2:BoundedDiscrete <- x2;
  
  /**
   * Value for which conditional probabilities have been enumerated.
   */
  x:Integer?;
  
  /**
   * If clamped, then the lower bound of `x1`.
   */
  x0:Integer;
  
  /**
   * If clamped, then the probabilities of all possible values of `x1`,
   * starting from `x0`.
   */
  z:Real[_];
  
  /**
   * If clamped, then the sum of `z`.
   */
  Z:Real;
  
  function enumerate(x:Integer) {
    if !this.x? || this.x! != x {
      let l <- max(x1.lower()!, x - x2.upper()!);
      let u <- min(x1.upper()!, x - x2.lower()!);
    
      x0 <- l;
      Z <- 0.0;
      if l <= u {
        /* distribution over possible pairs that produce the given sum */
        z <- vector(0.0, u - l + 1);
        for n in l..u {
          z[n - l + 1] <- exp(x1.logpdf(n) + x2.logpdf(x - n));
          Z <- Z + z[n - l + 1];
        }
      }
      this.x <- x;
    }
  }

//  function supportsLazy() -> Boolean {
//    return x1.supportsLazy() && x2.supportsLazy();
//  }

  function simulate() -> Integer {
    return simulate_delta(x1.simulate() + x2.simulate());
  }
  
//  function simulateLazy() -> Integer? {
//    return simulate_delta(x1.simulateLazy()! + x2.simulateLazy()!);
//  }
  
  function logpdf(x:Integer) -> Real {
    enumerate(x);
    return log(Z);
  }

//  function logpdfLazy(x:Expression<Integer>) -> Expression<Real>? {
//    enumerate(x);
//    return log(Z);
//  }
  
  function update(x:Integer) {
    /* choose a pair with the given sum and clamp parents */
    enumerate(x);
    let n <- simulate_categorical(z, Z) + x0 - 1;
    x1.clamp(n);
    x2.clamp(x - n);
  }

//  function updateLazy(x:Expression<Integer>) {
//
//  }

  function cdf(x:Integer) -> Real? {
    let P <- 0.0;
    for n in lower()!..x {
      P <- P + pdf(n);
    }
    return P;
  }
  
  function lower() -> Integer? {
    return x1.lower()! + x2.lower()!;
  }
  
  function upper() -> Integer? {
    return x1.upper()! + x2.upper()!;
  }
  
  function link() {
    x1.setChild(this);
    x2.setChild(this);
  }
  
  function unlink() {
    x1.releaseChild(this);
    x2.releaseChild(this);
  }
}

function AddDiscreteDelta(x1:BoundedDiscrete, x2:BoundedDiscrete) -> AddDiscreteDelta {
  m:AddDiscreteDelta(x1, x2);
  m.link();
  return m;
}

/**
 * Create delta distribution.
 */
function Delta(μ:Add<Random<Integer>,Random<Integer>>) -> Distribution<Integer> {
  let l1 <- μ.l!.graftBoundedDiscrete();
  let r1 <- μ.r!.graftBoundedDiscrete();
  if l1? && r1? {
    return AddDiscreteDelta(l1!, r1!);
  } else {
    let l2 <- μ.l!.graftDiscrete();
    let r2 <- μ.r!.graftDiscrete();
    if l2? {
      return LinearDiscreteDelta(1, l2!, μ.r!);
    } else if r2? {
      return LinearDiscreteDelta(1, r2!, μ.l!);
    } else {
      return construct<Delta>(box(μ));
    }
  }
}
