/**
 * Multivariate Gaussian distribution with linear transformation of
 * matrix-normal-inverse-Wishart prior.
 */
class ScaledMatrixNormalInverseWishartMultivariateGaussianDistribution<
    Arg1,Arg2,Arg3,Arg4,Arg5>(a:Arg1, M:Arg2, Σ:Arg3, Ψ:Arg4, k:Arg5) <
    MultivariateTDistribution<Arg5,Expression<Real[_]>,Expression<Real[_,_]>>(
    k, box(inner(M, a)), box(sqrt(1.0 + dot(triinner(Σ, a)))*Ψ)) {
  /**
   * Scale.
   */
  a:Arg1 <- a;

  /**
   * Mean.
   */
  M:Arg2 <- M;

  /**
   * Covariance.
   */
  Σ:Arg3 <- Σ;
  
  /**
   * Among-column covariance scale.
   */
  Ψ:Arg4 <- Ψ;
  
  override function supportsLazy() -> Boolean {
    return true;
  }

  override function update(x:Real[_]) -> Delay? {
    return update_scaled_matrix_normal_inverse_wishart_multivariate_gaussian(
        x, value(a), value(M), value(Σ), value(Ψ), value(super.k),
        value(super.μ));
  }

  override function updateLazy(x:Expression<Real[_]>) -> Delay? {
    return update_scaled_matrix_normal_inverse_wishart_multivariate_gaussian(
        x, a, M, Σ, Ψ, super.k, super.μ);
  }
}

function ScaledMatrixNormalInverseWishartMultivariateGaussian<Arg1,Arg2,
    Arg3,Arg4,Arg5>(a:Arg1, M:Arg2, Σ:Arg3, Ψ:Arg4, k:Arg5) -> {
  return wrap_scaled_matrix_normal_inverse_wishart_multivariate_gaussian(a,
      M, chol(Σ), chol(Ψ), k);
}
function wrap_scaled_matrix_normal_inverse_wishart_multivariate_gaussian<Arg1,
    Arg2,Arg3,Arg4,Arg5>(a:Arg1, M:Arg2, Σ:Arg3, Ψ:Arg4, k:Arg5) -> {
  return make_scaled_matrix_normal_inverse_wishart_multivariate_gaussian(
      wrap(a), wrap(M), wrap(Σ), wrap(Ψ), wrap(k));
}
function make_scaled_matrix_normal_inverse_wishart_multivariate_gaussian<Arg1,
    Arg2,Arg3,Arg4,Arg5>(a:Arg1, M:Arg2, Σ:Arg3, Ψ:Arg4, k:Arg5) -> {
  return construct<ScaledMatrixNormalInverseWishartMultivariateGaussianDistribution<
      Arg1,Arg2,Arg3,Arg4,Arg5>>(a, M, Σ, Ψ, k);
}

/**
 * Create multivariate Gaussian distribution.
 */
function MultivariateGaussian<Arg>(μ:Inner<Random<Real[_,_]>,Arg>,
    Σ:Random<Real[_,_]>) -> Distribution<Real[_]> {
  μ.l.prune();
  if μ.l.hasMatrixNormalInverseWishart() && μ.l.isGrandChildOf(Σ) {
    let (M, Σ1, Ψ, k) <- μ.l.getMatrixNormalInverseWishart();
    let p <- wrap_scaled_matrix_normal_inverse_wishart_multivariate_gaussian(
        μ.r, M, Σ1, Ψ, k);
    μ.l.graft(p, 1);
    Σ.graftReplace(p, 2);
    μ.l.join(Σ);
    return p;
  } else {
    Σ.prune();
    // if Σ.hasInverseWishart() {
    // 
    // } else {
      return wrap_multivariate_gaussian(μ, chol(Σ));
    // }
  }
}

/*
 * Update the parameters of a Gaussian variate with linear transformation
 * of matrix-normal-inverse-Wishart prior.
 *
 * - x: The variate.
 * - a: Scale.
 * - M: Mean.
 * - Σ: Covariance.
 * - Ψ: Prior variance shape.
 * - k: Prior degrees of freedom.
 * - N: Precomputed marginal mean.
 *
 * Returns: the posterior hyperparameters `M'`, `Σ'`, `Ψ'` and `k'`.
 */
function update_scaled_matrix_normal_inverse_wishart_multivariate_gaussian<
    Arg1,Arg2,Arg3,Arg4,Arg5,Arg6,Arg7>(x:Arg1, a:Arg2, M:Arg3, Σ:Arg4,
    Ψ:Arg5, k:Arg6, N:Arg7) -> {
  let b <- wrap(triinner(Σ, a));
  let c <- wrap(trimul(Σ, b));
  let l' <- wrap(c/(1.0 + dot(b)));
  let M' <- wrap(outer(M, l', x - N));
  let Σ' <- wrap(chol(triouter(Σ) - outer(l', c)));  ///@todo cholupdate()
  let Ψ' <- chol(triouter(Ψ) + outer(x) + inner(trisolve(Σ, M)) -
      inner(trisolve(Σ', M')));
  let k' <- k + 1.0;
  return wrap_matrix_normal_inverse_wishart(M', Σ', Ψ', k');
}
