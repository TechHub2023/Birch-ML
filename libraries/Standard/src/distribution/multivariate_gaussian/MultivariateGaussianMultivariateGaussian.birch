/**
 * Multivariate Gaussian-Gaussian distribution.
 */
final class MultivariateGaussianMultivariateGaussian(m:MultivariateGaussian,
    S:Expression<Real[_,_]>) < MultivariateGaussian(m.μ, box(m.Σ + S)) {
  /**
   * Mean.
   */
  m:MultivariateGaussian <- m;

  /**
   * Likelihood covariance.
   */
  S:Expression<Real[_,_]> <- S;

  function update(x:Real[_]) {
    (m.μ, m.Σ) <- box(update_multivariate_gaussian_multivariate_gaussian(x, m.μ.value(), m.Σ.value(), S.value()));
  }

  function updateLazy(x:Expression<Real[_]>) {
    (m.μ, m.Σ) <- update_lazy_multivariate_gaussian_multivariate_gaussian(x, m.μ, m.Σ, S);
  }

  function link() {
    m.setChild(this);
  }
  
  function unlink() {
    m.releaseChild(this);
  }
}

function MultivariateGaussianMultivariateGaussian<Right>(μ:MultivariateGaussian, Σ:Right) -> {
  m:MultivariateGaussianMultivariateGaussian(μ, Σ);
  m.link();
  return m;
}

/*
 * Update the parameters of a multivariate Gaussian distribution with a
 * multivariate Gaussian likelihood.
 *
 * - x: The variate.
 * - μ: Prior mean.
 * - Σ: Prior covariance.
 * - S: Likelihood covariance.
 *
 * Returns: the posterior hyperparameters `μ'` and `Σ'`.
 */
function update_multivariate_gaussian_multivariate_gaussian(x:Real[_],
    μ:Real[_], Σ:Real[_,_], S:Real[_,_]) -> (Real[_], Real[_,_]) {
  let K' <- transpose(solve(Σ + S, Σ));
  let μ' <- μ + K'*(x - μ);
  let Σ' <- Σ - K'*Σ;
  return (μ', Σ');
}

/*
 * Update the parameters of a multivariate Gaussian distribution with a
 * multivariate Gaussian likelihood.
 *
 * - x: The variate.
 * - μ: Prior mean.
 * - Σ: Prior covariance.
 * - S: Likelihood covariance.
 *
 * Returns: the posterior hyperparameters `μ'` and `Σ'`.
 */
function update_lazy_multivariate_gaussian_multivariate_gaussian(x:Expression<Real[_]>,
    μ:Expression<Real[_]>, Σ:Expression<Real[_,_]>, S:Expression<Real[_,_]>) ->
    (Expression<Real[_]>, Expression<Real[_,_]>) {
  let K' <- transpose(solve(Σ + S, Σ));
  let μ' <- μ + K'*(x - μ);
  let Σ' <- Σ - K'*Σ;
  return (box(μ'), box(Σ'));
}
