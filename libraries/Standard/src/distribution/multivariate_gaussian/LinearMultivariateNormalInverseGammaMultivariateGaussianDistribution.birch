/**
 * Linear-normal-inverse-gamma-Gaussian distribution where
 * components have independent and identical variance.
 */
final class LinearMultivariateNormalInverseGammaMultivariateGaussianDistribution<Arg1,
    Arg2,Arg3,Arg4,Arg5,Arg6,Arg7>(A:Arg1, μ:Arg2, Σ:Arg3, k:Arg4, β:Arg5,
    c:Arg6, Ω:Arg7) <
    ScaledMultivariateNormalInverseGammaMultivariateGaussianDistribution<Arg1,
    Arg2,Arg3,Arg4,Arg5,Arg7>(A, μ, Σ, k, β, Ω) {
  /**
   * Offset.
   */
  c:Arg6 <- c;

  override function supportsLazy() -> Boolean {
    return true;
  }

  override function simulate() -> Real[_] {
    return super.simulate() + value(c);
  }

  override function simulateLazy() -> Real[_]? {
    return super.simulateLazy()! + eval(c);
  }
  
  override function logpdf(x:Real[_]) -> Real! {
    return super.logpdf(x - value(c));
  }

  override function logpdfLazy(x:Expression<Real[_]>) -> Expression<Real>? {
    return super.logpdfLazy(box(x - c));
  }

  override function update(x:Real[_]) -> Delay? {
    return super.update(x - value(c));
  }

  override function updateLazy(x:Expression<Real[_]>) -> Delay? {
    return super.updateLazy(box(x - c));
  }
}

function LinearMultivariateNormalInverseGammaMultivariateGaussian<Arg1,
    Arg2,Arg3,Arg4,Arg5,Arg6,Arg7>(A:Arg1, μ:Arg2, Σ:Arg3, α:Arg4, β:Arg5,
    c:Arg6, Ω:Arg7) -> {
  return wrap_linear_multivariate_normal_inverse_gamma_multivariate_gaussian(
      A, μ, chol(Σ), 2.0*α, β, c, chol(Ω));
}
function wrap_linear_multivariate_normal_inverse_gamma_multivariate_gaussian<
    Arg1,Arg2,Arg3,Arg4,Arg5,Arg6,Arg7>(A:Arg1, μ:Arg2, Σ:Arg3, k:Arg4,
    β:Arg5, c:Arg6, Ω:Arg7) -> {
  return make_linear_multivariate_normal_inverse_gamma_multivariate_gaussian(
        wrap(A), wrap(μ), wrap(Σ), wrap(k), wrap(β), wrap(c), wrap(Ω));
}
function make_linear_multivariate_normal_inverse_gamma_multivariate_gaussian<
    Arg1,Arg2,Arg3,Arg4,Arg5,Arg6,Arg7>(A:Arg1, μ:Arg2, Σ:Arg3, k:Arg4,
    β:Arg5, c:Arg6, Ω:Arg7) -> {
  return construct<LinearMultivariateNormalInverseGammaMultivariateGaussianDistribution<
      Arg1,Arg2,Arg3,Arg4,Arg5,Arg6,Arg7>>(A, μ, Σ, k, β, c, Ω);
}

/**
 * Create multivariate Gaussian distribution.
 */
function MultivariateGaussian<Arg1,Arg2,Arg3>(μ:OpAdd<OpMul<Arg1,
    Random<Real[_]>>,Arg2>, Ω:OpMul<Arg3,Random<Real>>) ->
    Distribution<Real[_]> {
  μ.l.r.prune();
  if μ.l.r.hasMultivariateNormalInverseGamma() && μ.l.r.isGrandChildOf(Ω.r) {
    let (m, Σ, k, β) <- μ.l.r.getMultivariateNormalInverseGamma();
    let p <- wrap_linear_multivariate_normal_inverse_gamma_multivariate_gaussian(
        μ.l.l, m, Σ, k, β, μ.r, chol(Ω.l));
    μ.l.r.graft(p, 1);
    Ω.r.graftReplace(p, 2);
    μ.l.r.join(Ω.r);
    return p;
  } else {
    Ω.r.prune();
    if Ω.r.hasInverseGamma() {
      let (α, β) <- Ω.r.getInverseGamma();
      let p <- wrap_multivariate_normal_inverse_gamma(μ, chol(Ω.l), 2.0*α, β);
      Ω.r.graft(p);
      return p;
    } else {
      return wrap_multivariate_gaussian(μ, chol(Ω));
    }
  }
}

/**
 * Create multivariate Gaussian distribution.
 */
function MultivariateGaussian<Arg1,Arg2,Arg3>(μ:OpAdd<OpMul<Arg1,
    Random<Real[_]>>,Arg2>, Ω:OpMul<Random<Real>,Arg3>) ->
    Distribution<Real[_]> {
  μ.l.r.prune();
  if μ.l.r.hasMultivariateNormalInverseGamma() && μ.l.r.isGrandChildOf(Ω.l) {
    let (m, Σ, k, β) <- μ.l.r.getMultivariateNormalInverseGamma();
    let p <- wrap_linear_multivariate_normal_inverse_gamma_multivariate_gaussian(
        μ.l.l, m, Σ, k, β, μ.r, chol(Ω.r));
    μ.l.r.graft(p, 1);
    Ω.r.graftReplace(p, 2);
    μ.l.r.join(Ω.r);
    return p;
  } else if Ω.l.hasInverseGamma() {
    Ω.l.prune();
    let (α, β) <- Ω.l.getInverseGamma();
    let p <- wrap_multivariate_normal_inverse_gamma(μ, chol(Ω.r), 2.0*α, β);
    Ω.l.graft(p);
    return p;
  } else {
    return wrap_multivariate_gaussian(μ, chol(Ω));
  }
}
