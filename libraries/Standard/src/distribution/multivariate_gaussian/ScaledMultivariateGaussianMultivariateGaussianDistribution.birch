/**
 * Multivariate linear-Gaussian-Gaussian distribution.
 */
final class ScaledMultivariateGaussianMultivariateGaussianDistribution<Arg1,Arg2,Arg3,
    Arg4>(A:Arg1, μ:Arg2, Σ:Arg3, S:Arg4) < Distribution<Real[_]> {
  /**
   * Scale.
   */
  A:Arg1 <- A;
    
  /**
   * Mean of mean.
   */
  μ:Arg2 <- μ;

  /**
   * Covariance of mean.
   */
  Σ:Arg3 <- Σ;
  
  /**
   * Covariance.
   */
  S:Arg4 <- S;

  override function supportsLazy() -> Boolean {
    return true;
  }

  override function simulate() -> Real[_] {
    return simulate_scaled_multivariate_gaussian_multivariate_gaussian(
        value(A), value(μ), value(Σ), value(S));
  }

  override function simulateLazy() -> Real[_]? {
    return simulate_scaled_multivariate_gaussian_multivariate_gaussian(
        eval(A), eval(μ), eval(Σ), eval(S));
  }
  
  override function logpdf(x:Real[_]) -> Real! {
    return logpdf_scaled_multivariate_gaussian_multivariate_gaussian(x,
        value(A), value(μ), value(Σ), value(S));
  }

  override function logpdfLazy(x:Expression<Real[_]>) -> Expression<Real>? {
    return box(logpdf_scaled_multivariate_gaussian_multivariate_gaussian(x,
        A, μ, Σ, S));
  }

  override function update(x:Real[_]) -> Delay? {
    return update_scaled_multivariate_gaussian_multivariate_gaussian(x,
        value(A), value(μ), value(Σ), value(S));
  }

  override function updateLazy(x:Expression<Real[_]>) -> Delay? {
    return update_scaled_multivariate_gaussian_multivariate_gaussian(x, A, μ,
        Σ, S);
  }

  override function isMultivariateGaussian() -> Boolean {
    return true;
  }

  override function getMultivariateGaussian() -> (Expression<Real[_]>,
      Expression<Real[_,_]>)? {
    return box(A*μ, chol(outer(A*Σ) + triouter(S)));
  }
}

function ScaledMultivariateGaussianMultivariateGaussian<Arg1,Arg2,Arg3,Arg4>(
    A:Arg1, μ:Arg2, Σ:Arg3, S:Arg4) -> {
  return wrap_scaled_multivariate_gaussian_multivariate_gaussian(A, μ,
      chol(Σ), chol(S));
}
function wrap_scaled_multivariate_gaussian_multivariate_gaussian<Arg1,Arg2,
    Arg3,Arg4>(A:Arg1, μ:Arg2, Σ:Arg3, S:Arg4) -> {
  return make_scaled_multivariate_gaussian_multivariate_gaussian(wrap(A),
      wrap(μ), wrap(Σ), wrap(S));
}
function make_scaled_multivariate_gaussian_multivariate_gaussian<Arg1,Arg2,
    Arg3,Arg4>(A:Arg1, μ:Arg2, Σ:Arg3, S:Arg4) -> {
  return construct<ScaledMultivariateGaussianMultivariateGaussianDistribution<
      Arg1,Arg2,Arg3,Arg4>>(A, μ, Σ, S);
}

/**
 * Create multivariate Gaussian distribution.
 */
function MultivariateGaussian<Arg1,Arg2>(μ:Multiply<Arg1,Random<Real[_]>>,
    Σ:Arg2) -> Distribution<Real[_]> {
  μ.r.prune();
  if μ.r.hasMultivariateGaussian() {
    let (m, S) <- μ.r.getMultivariateGaussian();
    let p <- wrap_scaled_multivariate_gaussian_multivariate_gaussian(μ.l, m,
        S, chol(Σ));
    μ.r.graft(p);
    return p;
  } else {
    return wrap_multivariate_gaussian(μ, chol(Σ));
  }
}

/*
 * Simulate a multivariate Gaussian distribution with a linear transformation
 * and multivariate Gaussian likelihood.
 *
 * - A: Scale.
 * - μ: Prior mean.
 * - Σ: Prior covariance.
 * - S: Likelihood covariance.
 */
function simulate_scaled_multivariate_gaussian_multivariate_gaussian<Arg1,
    Arg2,Arg3,Arg4>(A:Arg1, μ:Arg2, Σ:Arg3, S:Arg4) -> {
  ///@todo A*Σ involves lower-triangular matrix on right, optimize
  return simulate_multivariate_gaussian(A*μ, chol(outer(A*Σ) + outer(S)));
}

/*
 * Observe a multivariate Gaussian variate with a linear transformation
 * and multivariate Gaussian likelihood.
 *
 * - A: Scale.
 * - μ: Prior mean.
 * - Σ: Prior covariance.
 * - S: Likelihood covariance.
 *
 * Returns: the log probability density.
 */
function logpdf_scaled_multivariate_gaussian_multivariate_gaussian<Arg1,Arg2,
    Arg3,Arg4,Arg5>(x:Arg1, A:Arg2, μ:Arg3, Σ:Arg4, S:Arg5) -> {
  ///@todo A*Σ involves lower-triangular matrix on right, optimize
  return logpdf_multivariate_gaussian(x, A*μ, chol(outer(A*Σ) + outer(S)));
}

/*
 * Update the parameters of a multivariate Gaussian distribution with a 
 * linear transformation and multivariate Gaussian likelihood.
 *
 * - x: The variate.
 * - A: Scale.
 * - μ: Prior mean.
 * - Σ: Prior covariance.
 * - Ω: Likelihood covariance.
 *
 * Returns: the posterior hyperparameters `μ'` and `Σ'`.
 */
function update_scaled_multivariate_gaussian_multivariate_gaussian<Arg1,
    Arg2,Arg3,Arg4,Arg5>(x:Arg1, A:Arg2, μ:Arg3, Σ:Arg4, Ω:Arg5) -> {
  let S <- wrap(triouter(Σ));
  let K' <- wrap(S*inner(A, cholinv(chol(outer(A*Σ) + triouter(Ω)))));
  let μ' <- μ + K'*(x - A*μ);
  let Σ' <- chol(S - K'*A*S);
  return wrap_multivariate_gaussian(μ', Σ');
}
