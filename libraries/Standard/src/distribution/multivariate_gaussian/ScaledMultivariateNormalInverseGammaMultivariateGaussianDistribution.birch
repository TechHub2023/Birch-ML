/**
 * Scaled-normal-inverse-gamma-Gaussian distribution where
 * components have independent and identical variance.
 */
class ScaledMultivariateNormalInverseGammaMultivariateGaussianDistribution<
    Arg1,Arg2,Arg3,Arg4,Arg5,Arg6>(A:Arg1, μ:Arg2, Σ:Arg3, k:Arg4, β:Arg5,
    Ω:Arg6) < MultivariateTDistribution<Arg4,Expression<Real[_]>,
    Expression<Real[_,_]>>(k, box(A*μ), box(chol((2.0*β)*(A*outer(triouter(Σ),
    A) + triouter(Ω))))) {
  /**
   * Scale.
   */
  A:Arg1 <- A;

  /**
   * Mean.
   */
  μ:Arg2 <- μ;

  /**
   * Covariance.
   */
  Σ:Arg3 <- Σ;

  /**
   * Variance scale.
   */
  β:Arg5 <- β;

  /**
   * Covariance scale.
   */
  Ω:Arg6 <- Ω;

  override function supportsLazy() -> Boolean {
    return true;
  }

  override function update(x:Real[_]) -> Delay? {
    return update_scaled_multivariate_normal_inverse_gamma_multivariate_gaussian(
        x, value(A), value(μ), value(Σ), value(super.k), value(β), value(Ω),
        value(super.μ), value(super.Σ));
  }

  override function updateLazy(x:Expression<Real[_]>) -> Delay? {
    return update_scaled_multivariate_normal_inverse_gamma_multivariate_gaussian(
        x, A, μ, Σ, super.k, β, Ω, super.μ, super.Σ);
  }
}

function ScaledMultivariateNormalInverseGammaMultivariateGaussian<Arg1,Arg2,
    Arg3,Arg4,Arg5,Arg6>(A:Arg1, μ:Arg2, Σ:Arg3, α:Arg4, β:Arg5, Ω:Arg6) -> {
  return wrap_scaled_multivariate_normal_inverse_gamma_multivariate_gaussian(
      A, μ, chol(Σ), 2.0*α, β, chol(Ω));
}
function wrap_scaled_multivariate_normal_inverse_gamma_multivariate_gaussian<
    Arg1,Arg2,Arg3,Arg4,Arg5,Arg6>(A:Arg1, μ:Arg2, Σ:Arg3, k:Arg4, β:Arg5,
    Ω:Arg6) -> {
  return make_scaled_multivariate_normal_inverse_gamma_multivariate_gaussian(
      wrap(A), wrap(μ), wrap(Σ), wrap(k), wrap(β), wrap(Ω));
}
function make_scaled_multivariate_normal_inverse_gamma_multivariate_gaussian<
    Arg1,Arg2,Arg3,Arg4,Arg5,Arg6>(A:Arg1, μ:Arg2, Σ:Arg3, k:Arg4, β:Arg5,
    Ω:Arg6) -> {
  return construct<ScaledMultivariateNormalInverseGammaMultivariateGaussianDistribution<
      Arg1,Arg2,Arg3,Arg4,Arg5,Arg6>>(A, μ, Σ, k, β, Ω);
}

/**
 * Create multivariate Gaussian distribution.
 */
function MultivariateGaussian<Arg1,Arg2>(μ:OpMul<Arg1,Random<Real[_]>>,
    Ω:OpMul<Arg2,Random<Real>>) -> Distribution<Real[_]> {
  μ.r.prune();
  if μ.r.hasMultivariateNormalInverseGamma() && μ.r.isGrandChildOf(Ω.r) {
    let (μ1, Σ, k, β) <- μ.r.getMultivariateNormalInverseGamma();
    let p <- wrap_scaled_multivariate_normal_inverse_gamma_multivariate_gaussian(
        μ.l, μ1, Σ, k, β, chol(Ω.l));
    μ.r.graft(p, 1);
    Ω.r.graftReplace(p, 2);
    μ.r.join(Ω.r);
    return p;
  } else {
    Ω.r.prune();
    if Ω.r.hasInverseGamma() {
      let (α, β) <- Ω.r.getInverseGamma();
      let p <- wrap_multivariate_normal_inverse_gamma(μ, chol(Ω.l), 2.0*α, β);
      Ω.r.graft(p);
      return p;
    } else {
      return wrap_multivariate_gaussian(μ, chol(Ω));
    }
  }
}

/**
 * Create multivariate Gaussian distribution.
 */
function MultivariateGaussian<Arg1,Arg2>(μ:OpMul<Arg1,Random<Real[_]>>,
    Ω:OpMul<Random<Real>,Arg2>) -> Distribution<Real[_]> {
  μ.r.prune();
  if μ.r.hasMultivariateNormalInverseGamma() && μ.r.isGrandChildOf(Ω.l) {
    let (μ1, Σ, k, β) <- μ.r.getMultivariateNormalInverseGamma();
    let p <- wrap_scaled_multivariate_normal_inverse_gamma_multivariate_gaussian(
        μ.l, μ1, Σ, k, β, chol(Ω.r));
    μ.r.graft(p, 1);
    Ω.l.graftReplace(p, 2);
    μ.r.join(Ω.l);
    return p;
  } else {
    Ω.l.prune();
    if Ω.l.hasInverseGamma() {
      let (α, β) <- Ω.l.getInverseGamma();
      let p <- wrap_multivariate_normal_inverse_gamma(μ, chol(Ω.r), 2.0*α, β);
      Ω.l.graft(p);
      return p;
    } else {
      return wrap_multivariate_gaussian(μ, chol(Ω));
    }
  }
}

/*
 * Update the parameters of a normal inverse-gamma distribution with a
 * linear transformation and multivariate Gaussian likelihood.
 *
 * - x: The variate.
 * - A: Scale.
 * - μ: Prior mean.
 * - Σ: Prior covariance.
 * - Ω: Covariance scale.
 * - k: Prior degrees of freedom of the inverse-gamma ($k = 2\alpha$).
 * - β: Prior scale of the inverse-gamma.
 * - m: Precomputed marginal mean.
 * - T: Precomputed marginal covariance.
 *
 * Returns: the posterior hyperparameters `μ'`, `Σ'`, `k'` and `β'`.
 */
function update_scaled_multivariate_normal_inverse_gamma_multivariate_gaussian<
    Arg1,Arg2,Arg3,Arg4,Arg5,Arg6,Arg7,Arg8,Arg9>(x:Arg1, A:Arg2, μ:Arg3,
    Σ:Arg4, k:Arg5, β:Arg6, Ω:Arg7, m:Arg8, T:Arg9) -> {
  let n <- length(x);
  let S <- wrap(triouter(Σ));
  let K' <- wrap(2.0*β*S*inner(A, cholinv(T)));
  let μ' <- wrap(μ + K'*(x - m));
  let Σ' <- wrap(chol(S - K'*A*S));
  let k' <- k + n;
  let β' <- β + 0.5*(dot(trisolve(Ω, x)) + dot(trisolve(Σ, μ)) -
      dot(trisolve(Σ', μ')));
  return wrap_multivariate_normal_inverse_gamma(μ', Σ', k', β');
}
