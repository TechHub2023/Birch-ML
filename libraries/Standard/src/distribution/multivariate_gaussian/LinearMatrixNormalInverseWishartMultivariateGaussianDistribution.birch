/**
 * Multivariate Gaussian distribution with linear transformation of
 * matrix-normal-inverse-Wishart prior.
 */
final class LinearMatrixNormalInverseWishartMultivariateGaussianDistribution<
    Arg1,Arg2,Arg3,Arg4,Arg5,Arg6>(a:Arg1, M:Arg2, Σ:Arg3, Ψ:Arg4, k:Arg5,
    c:Arg6) < Distribution<Real[_]> {
  /**
   * Scale.
   */
  a:Arg1 <- a;

  /**
   * Mean.
   */
  M:Arg2 <- M;

  /**
   * Covariance.
   */
  Σ:Arg3 <- Σ;

  /**
   * Among-column covariance scale.
   */
  Ψ:Arg4 <- Ψ;

  /**
   * Among-column covariance degrees of freedom.
   */
  k:Arg5 <- k;

  /**
   * Offset.
   */
  c:Arg6 <- c;

  override function supportsLazy() -> Boolean {
    return true;
  }

  override function simulate() -> Real[_] {
    return simulate_scaled_matrix_normal_inverse_wishart_multivariate_gaussian(
        value(a), value(M), value(Σ), value(Ψ), value(k)) + value(c);
  }

  override function simulateLazy() -> Real[_]? {
    return simulate_scaled_matrix_normal_inverse_wishart_multivariate_gaussian(
        eval(a), eval(M), eval(Σ), eval(Ψ), eval(k)) + eval(c);
  }
  
  override function logpdf(x:Real[_]) -> Real! {
    return logpdf_scaled_matrix_normal_inverse_wishart_multivariate_gaussian(
        x - value(c), value(a), value(M), value(Σ), value(Ψ), value(k));
  }

  override function logpdfLazy(x:Expression<Real[_]>) -> Expression<Real>? {
    return box(logpdf_scaled_matrix_normal_inverse_wishart_multivariate_gaussian(
        box(x - c), a, M, Σ, Ψ, k));
  }

  override function update(x:Real[_]) -> Delay? {
    return update_scaled_matrix_normal_inverse_wishart_multivariate_gaussian(
         x - value(c), value(a), value(M), value(Σ), value(Ψ), value(k));
  }

  override function updateLazy(x:Expression<Real[_]>) -> Delay? {
    return update_scaled_matrix_normal_inverse_wishart_multivariate_gaussian(
        box(x - c), a, M, Σ, Ψ, k);
  }
}

function LinearMatrixNormalInverseWishartMultivariateGaussian<Arg1,Arg2,
    Arg3,Arg4,Arg5,Arg6>(a:Arg1, M:Arg2, Σ:Arg3, Ψ:Arg4, k:Arg5, c:Arg6) -> {
  return wrap_linear_matrix_normal_inverse_wishart_multivariate_gaussian(a, M,
      chol(Σ), chol(Ψ), k, c);
}
function wrap_linear_matrix_normal_inverse_wishart_multivariate_gaussian<Arg1,
    Arg2,Arg3,Arg4,Arg5,Arg6>(a:Arg1, M:Arg2, Σ:Arg3, Ψ:Arg4, k:Arg5,
    c:Arg6) -> {
  return make_linear_matrix_normal_inverse_wishart_multivariate_gaussian(
        wrap(a), wrap(M), wrap(Σ), wrap(Ψ), wrap(k), wrap(c));
}
function make_linear_matrix_normal_inverse_wishart_multivariate_gaussian<Arg1,
    Arg2,Arg3,Arg4,Arg5,Arg6>(a:Arg1, M:Arg2, Σ:Arg3, Ψ:Arg4, k:Arg5,
    c:Arg6) -> {
  return construct<LinearMatrixNormalInverseWishartMultivariateGaussianDistribution<
      Arg1,Arg2,Arg3,Arg4,Arg5,Arg6>>(a, M, Σ, Ψ, k, c);
}

/**
 * Create multivariate Gaussian distribution.
 */
function MultivariateGaussian<Arg1,Arg2>(
    μ:Add<Inner<Random<Real[_,_]>,Arg1>,Arg2>, Σ:Random<Real[_,_]>) ->
    Distribution<Real[_]> {
  μ.l.l.prune();
  if μ.l.l.hasMatrixNormalInverseWishart()&& μ.l.l.isGrandChildOf(Σ) {
    let (M, Σ1, Ψ, k) <- μ.l.l.getMatrixNormalInverseWishart();
    let p <- wrap_linear_matrix_normal_inverse_wishart_multivariate_gaussian(
        μ.l.r, M, Σ1, Ψ, k, μ.r);
    μ.l.l.graft(p, 1);
    Σ.graftReplace(p, 2);
    μ.l.l.join(Σ);
    return p;
  } else {
    Σ.prune();
    // if Σ.hasInverseWishart() {
    //
    // } else {
      return wrap_multivariate_gaussian(μ, chol(Σ));
    // }
  }
}
