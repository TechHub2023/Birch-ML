/**
 * Multivariate normal-inverse-gamma-Gaussian distribution.
 */
final class MultivariateNormalInverseGammaMultivariateGaussian<Arg1,Arg2,Arg3,
    Arg4,Arg5>(ν:Arg1, Λ:Arg2, α:Arg3, β:Arg4, Σ:Arg5) <
    Distribution<Real[_]> {
  /**
   * Precision times mean.
   */
  ν:Arg1 <- ν;

  /**
   * Precision.
   */
  Λ:Arg2 <- Λ;

  /**
   * Variance shape.
   */
  α:Arg3 <- α;

  /**
   * Variance scale.
   */
  β:Arg4 <- β;

  /**
   * Covariance.
   */
  Σ:Arg5 <- Σ;

  override function supportsLazy() -> Boolean {
    return true;
  }

  override function simulate() -> Real[_] {
    return simulate_multivariate_normal_inverse_gamma_multivariate_gaussian(
        value(ν), value(Λ), value(α), value(β), value(Σ));
  }

  override function simulateLazy() -> Real[_]? {
    return simulate_multivariate_normal_inverse_gamma_multivariate_gaussian(
        peak(ν), peak(Λ), peak(α), peak(β), peak(Σ));
  }
  
  override function logpdf(x:Real[_]) -> Real {
    return logpdf_multivariate_normal_inverse_gamma_multivariate_gaussian(
        x, value(ν), value(Λ), value(α), value(β), value(Σ));
  }

  override function logpdfLazy(x:Expression<Real[_]>) -> Expression<Real>? {
    return box(logpdf_multivariate_normal_inverse_gamma_multivariate_gaussian(
        x, ν, Λ, α, β, Σ));
  }

  override function update(x:Real[_]) -> Delay? {
    return update_multivariate_normal_inverse_gamma_multivariate_gaussian(
        x, value(ν), value(Λ), value(α), value(β), value(Σ));
  }

  override function updateLazy(x:Expression<Real[_]>) -> Delay? {
    return update_multivariate_normal_inverse_gamma_multivariate_gaussian(
        x, ν, Λ, α, β, Σ);
  }
}

function MultivariateNormalInverseGammaMultivariateGaussian<Arg1,Arg2,Arg3,
    Arg4,Arg5>(ν:Arg1, Λ:Arg2, α:Arg3, β:Arg4, Σ:Arg5) -> {
  return construct<MultivariateNormalInverseGammaMultivariateGaussian<Arg1,
      Arg2,Arg3,Arg4,Arg5>>(ν, Λ, α, β, Σ);
}

/**
 * Create multivariate Gaussian distribution.
 */
function MultivariateGaussian<Arg>(μ:Random<Real[_]>,
    Σ:Multiply<Arg,Random<Real>>) -> Distribution<Real[_]> {
  if μ.hasMultivariateNormalInverseGamma(Σ.r!) {
    μ.prune();
    let (ν, Λ, α, β) <- μ.getMultivariateNormalInverseGamma();
    let p <- MultivariateNormalInverseGammaMultivariateGaussian(ν, Λ, α, β,
        Σ.l!);
    μ.graft(p);
    return p;
  } else if Σ.r!.hasInverseGamma() {
    Σ.r!.prune();
    let (α, β) <- Σ.r!.getInverseGamma();
    let Λ <- cholinv(Σ.l!);
    let p <- MultivariateNormalInverseGamma(Λ*μ, Λ, α, β);
    Σ.r!.graft(p);
    return p;
  } else {
    return construct<MultivariateGaussian<Random<Real[_]>,
        Multiply<Arg,Random<Real>>>>(μ, Σ);
  }
}

/**
 * Create multivariate Gaussian distribution.
 */
function MultivariateGaussian<Arg>(μ:Random<Real[_]>,
    Σ:Multiply<Random<Real>,Arg>) -> Distribution<Real[_]> {
  if μ.hasMultivariateNormalInverseGamma(Σ.l!) {
    μ.prune();
    let (ν, Λ, α, β) <- μ.getMultivariateNormalInverseGamma();
    let p <- MultivariateNormalInverseGammaMultivariateGaussian(ν, Λ, α, β,
        Σ.r!);
    μ.graft(p);
    return p;
  } else if Σ.l!.hasInverseGamma() {
    Σ.l!.prune();
    let (α, β) <- Σ.l!.getInverseGamma();
    let Λ <- cholinv(Σ.r!);
    let p <- MultivariateNormalInverseGamma(Λ*μ, Λ, α, β);
    Σ.l!.graft(p);
    return p;
  } else {
    return construct<MultivariateGaussian<Random<Real[_]>,
        Multiply<Arg,Random<Real>>>>(μ, Σ);
  }
}

/*
 * Simulate a multivariate Gaussian distribution with a multivariate normal
 * inverse-gamma prior.
 *
 * - ν: Precision times mean.
 * - Λ: Precision.
 * - Σ: Covariance scale.
 * - α: Shape of the inverse-gamma.
 * - β: Scale of the inverse-gamma.
 */
function simulate_multivariate_normal_inverse_gamma_multivariate_gaussian(
    ν:Real[_], Λ:Real[_,_], α:Real, β:Real, Σ:Real[_,_]) -> Real[_] {
  let S <- cholinv(Λ);
  let μ <- S*ν;
  return simulate_multivariate_student_t(2.0*α, μ, 2.0*β*(Σ + S));
}

/*
 * Observe a multivariate Gaussian variate with a multivariate normal
 * inverse-gamma prior.
 *
 * - x: The variate.
 * - ν: Precision times mean.
 * - Λ: Precision.
 * - Σ: Covariance scale.
 * - α: Shape of the inverse-gamma.
 * - β: Scale of the inverse-gamma.
 *
 * Returns: the log probability density.
 */
function logpdf_multivariate_normal_inverse_gamma_multivariate_gaussian<
    Arg1,Arg2,Arg3,Arg4,Arg5,Arg6>(x:Arg1, ν:Arg2, Λ:Arg3, α:Arg5, β:Arg6,
    Σ:Arg4) -> {
  let S <- cholinv(Λ);
  let μ <- S*ν;
  return logpdf_multivariate_student_t(x, 2.0*α, μ, 2.0*β*(Σ + S));
}

/*
 * Update the parameters of a normal inverse-gamma distribution with a
 * multivariate Gaussian likelihood.
 *
 * - x: The variate.
 * - ν: Prior precision times mean.
 * - Λ: Prior precision.
 * - Σ: Covariance scale.
 * - α: Prior shape of the inverse-gamma.
 * - β: Prior scale of the inverse-gamma.
 *
 * Returns: the posterior hyperparameters `μ'`, `Λ'`, `α'` and `β'`.
 */
function update_multivariate_normal_inverse_gamma_multivariate_gaussian<
    Arg1,Arg2,Arg3,Arg4,Arg5,Arg6>(x:Arg1, ν:Arg2, Λ:Arg3, α:Arg4, β:Arg5,
    Σ:Arg6) -> {
  let n <- length(x);
  let L <- cholinv(Σ);
  let z <- L*x;
  let Λ' <- Λ + L;
  let ν' <- ν + z;
  let μ <- cholsolve(Λ, ν);
  let μ' <- cholsolve(Λ', ν');
  let α' <- α + 0.5*n;
  let β' <- β + 0.5*dot(x, z) + 0.5*dot(μ, ν) - 0.5*dot(μ', ν');
  return MultivariateNormalInverseGamma(ν', Λ', α', β');
}
