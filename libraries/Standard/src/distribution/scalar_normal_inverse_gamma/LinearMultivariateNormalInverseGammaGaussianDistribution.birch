/**
 * Multivariate linear-normal-inverse-gamma-Gaussian distribution.
 */
final class LinearMultivariateNormalInverseGammaGaussianDistribution<Arg1,
    Arg2,Arg3,Arg4,Arg5,Arg6,Arg7>(a:Arg1, ν:Arg2, Λ:Arg3, α:Arg4, β:Arg5,
    c:Arg6, s2:Arg7) < Distribution<Real> {
  /**
   * Scale.
   */
  a:Arg1 <- a;

  /**
   * Prior precision times mean.
   */
  ν:Arg2 <- ν;

  /**
   * Prior precision.
   */
  Λ:Arg3 <- Λ;

  /**
   * Prior shape.
   */
  α:Arg4 <- α;

  /**
   * Prior scale.
   */
  β:Arg5 <- β;

  /**
   * Offset.
   */
  c:Arg6 <- c;

  /**
   * Likelihood variance scale.
   */
  s2:Arg7 <- s2;

  override function supportsLazy() -> Boolean {
    return true;
  }

  override function simulate() -> Real {
    return simulate_linear_multivariate_normal_inverse_gamma_gaussian(
        value(a), value(ν), value(Λ), value(c), value(α), value(β),
        value(s2));
  }

  override function simulateLazy() -> Real? {
    return simulate_linear_multivariate_normal_inverse_gamma_gaussian(
        peek(a), peek(ν), peek(Λ), peek(c), peek(α), peek(β), peek(s2));
        
  }
  
  override function logpdf(x:Real) -> Real {
    return logpdf_linear_multivariate_normal_inverse_gamma_gaussian(x,
        value(a), value(ν), value(Λ), value(c), value(α), value(β),
        value(s2));
  }

  override function logpdfLazy(x:Expression<Real>) -> Expression<Real>? {
    return box(logpdf_linear_multivariate_normal_inverse_gamma_gaussian(x, a,
        ν, Λ, c, α, β, s2));
  }

  override function update(x:Real) -> Delay? {
    return update_linear_multivariate_normal_inverse_gamma_gaussian(x,
        value(a), value(ν), value(Λ), value(c), value(α), value(β),
        value(s2));
  }

  override function updateLazy(x:Expression<Real>) -> Delay? {
    return update_linear_multivariate_normal_inverse_gamma_gaussian(x, a,
        ν, Λ, c, α, β, s2);
  }

  override function cdf(x:Real) -> Real? {
    return cdf_linear_multivariate_normal_inverse_gamma_gaussian(x, value(a),
        value(ν), value(Λ), value(c), value(α), value(β), value(s2));
  }

  override function quantile(P:Real) -> Real? {
    return quantile_linear_multivariate_normal_inverse_gamma_gaussian(P,
        value(a), value(ν), value(Λ), value(c), value(α), value(β),
        value(s2));
  }
}

function LinearMultivariateNormalInverseGammaGaussian<Arg1,Arg2,Arg3,Arg4,
    Arg5,Arg6,Arg7>(a:Arg1, ν:Arg2, Λ:Arg3, α:Arg4, β:Arg5, c:Arg6,
    s2:Arg7) -> {
  return construct<LinearMultivariateNormalInverseGammaGaussianDistribution<
      Arg1,Arg2,Arg3,Arg4,Arg5,Arg6,Arg7>>(a, ν, Λ, α, β, c, s2);
}

/**
 * Create Gaussian distribution.
 */
function Gaussian<Arg1,Arg2>(μ:Add<Dot<Arg1,Random<Real[_]>>,Arg2>,
    σ2:Random<Real>) -> Distribution<Real> {
  return Gaussian(μ, 1.0*σ2);
}

/**
 * Create Gaussian distribution.
 */
function Gaussian<Arg1,Arg2,Arg3>(μ:Add<Dot<Arg1,Random<Real[_]>>,Arg2>,
    σ2:Multiply<Random<Real>,Arg3>) -> Distribution<Real> {
  return Gaussian(μ, σ2.r*σ2.l);
}

/**
 * Create Gaussian distribution.
 */
function Gaussian<Arg1,Arg2,Arg3>(μ:Add<Dot<Arg1,Random<Real[_]>>,Arg2>,
    σ2:Multiply<Arg3,Random<Real>>) -> Distribution<Real> {
  μ.l.r.prune();
  if μ.l.r.hasMultivariateNormalInverseGamma() && μ.l.r.isGrandChildOf(σ2.r) {
    let (ν, Λ, α, β) <- μ.l.r.getMultivariateNormalInverseGamma();
    let p <- LinearMultivariateNormalInverseGammaGaussian(μ.l.l, ν, Λ, α, β,
        μ.r, σ2.l);
    μ.l.r.graft(p, 1);
    σ2.r.graftReplace(p, 2);
    μ.l.r.join(σ2.r);
    return p;
  } else {
    σ2.r.prune();
    if σ2.r.hasInverseGamma() {
      let (α, β) <- σ2.r.getInverseGamma();
      let p <- NormalInverseGamma(μ/σ2.l, 1.0/σ2.l, α, β);
      σ2.r.graft(p);
      return p;
    } else if μ.l.r.hasMultivariateGaussian() {
      let (m, S) <- μ.l.r.getMultivariateGaussian();
      let p <- LinearMultivariateGaussianGaussian(μ.l.l, m, S, μ.r, σ2);
      μ.l.r.graft(p);
      return p;
    } else {
      return construct<GaussianDistribution<Add<Dot<Arg1,Random<Real[_]>>,
          Arg2>,Multiply<Arg3,Random<Real>>>>(μ, σ2);
    }
  }
}

/**
 * Create Gaussian distribution.
 */
function Gaussian<Arg1,Arg2>(μ:Add<Dot<Arg1,Random<Real[_]>>,Arg2>,
    σ2:Multiply<Random<Real>,Random<Real>>) -> Distribution<Real> {
  μ.l.r.prune();
  if μ.l.r.hasMultivariateNormalInverseGamma() && μ.l.r.isGrandChildOf(σ2.r) {
    let (ν, Λ, α, β) <- μ.l.r.getMultivariateNormalInverseGamma();
    let p <- LinearMultivariateNormalInverseGammaGaussian(μ.l.l, ν, Λ, α, β,
        μ.r, σ2.l);
    μ.l.r.graft(p, 1);
    σ2.r.graftReplace(p, 2);
    μ.l.r.join(σ2.r);
    return p;
  } else if μ.l.r.hasMultivariateNormalInverseGamma() &&
      μ.l.r.isGrandChildOf(σ2.l) {
    let (ν, Λ, α, β) <- μ.l.r.getMultivariateNormalInverseGamma();
    let p <- LinearMultivariateNormalInverseGammaGaussian(μ.l.l, ν, Λ, α, β,
        μ.r, σ2.r);
    μ.l.r.graft(p, 1);
    σ2.l.graftReplace(p, 2);
    μ.l.r.join(σ2.l);
    return p;
  } else {
    σ2.l.prune();
    σ2.r.prune();
    if σ2.l.hasInverseGamma() {
      let (α, β) <- σ2.l.getInverseGamma();
      let p <- NormalInverseGamma(μ/σ2.r, 1.0/σ2.r, α, β);
      σ2.l.graft(p);
      return p;
    } else if σ2.r.hasInverseGamma() {
      let (α, β) <- σ2.r.getInverseGamma();
      let p <- NormalInverseGamma(μ/σ2.l, 1.0/σ2.l, α, β);
      σ2.r.graft(p);
      return p;
    } else if μ.l.r.hasMultivariateGaussian() {
      let (m, S) <- μ.l.r.getMultivariateGaussian();
      let p <- LinearMultivariateGaussianGaussian(μ.l.l, m, S, μ.r, σ2);
      μ.l.r.graft(p);
      return p;
    } else {
      return construct<GaussianDistribution<Add<Dot<Arg1,Random<Real[_]>>,
          Arg2>,Multiply<Random<Real>,Random<Real>>>>(μ, σ2);
    }
  }
}

/**
 * Create Gaussian distribution.
 */
function Gaussian<Arg>(μ:Dot<Arg,Random<Real[_]>>, σ2:Random<Real>) ->
    Distribution<Real> {
  return Gaussian(μ + 0.0, σ2);
}

/**
 * Create Gaussian distribution.
 */
function Gaussian<Arg1,Arg2>(μ:Dot<Arg1,Random<Real[_]>>,
    σ2:Multiply<Arg2,Random<Real>>) -> Distribution<Real> {
  return Gaussian(μ + 0.0, σ2);
}

/**
 * Create Gaussian distribution.
 */
function Gaussian<Arg1,Arg2>(μ:Dot<Arg1,Random<Real[_]>>,
    σ2:Multiply<Random<Real>,Arg2>) -> Distribution<Real> {
  return Gaussian(μ + 0.0, σ2);
}

/**
 * Create Gaussian distribution.
 */
function Gaussian<Arg>(μ:Dot<Arg,Random<Real[_]>>,
    σ2:Multiply<Random<Real>,Random<Real>>) -> Distribution<Real> {
  return Gaussian(μ + 0.0, σ2);
}

/*
 * Simulate a Gaussian distribution with a linear transformation of a
 * multivariate linear normal inverse-gamma prior.
 *
 * - a: Scale.
 * - ν: Precision times mean.
 * - Λ: Precision.
 * - c: Offset.
 * - α: Variance shape.
 * - β: Variance scale.
 * - s2: Likelihood variance scale.
 */
function simulate_linear_multivariate_normal_inverse_gamma_gaussian(a:Real[_],
    ν:Real[_], Λ:Real[_,_], c:Real, α:Real, β:Real, s2:Real) -> Real {
  let S <- cholinv(Λ);
  return simulate_student_t(2.0*α, dot(a, S*ν) + c, (β/α)*(s2 + dot(a, S*a)));
}

/*
 * Observe a Gaussian variate with a multivariate linear normal inverse-gamma
 * prior with linear transformation.
 *
 * - x: The variate.
 * - a: Scale.
 * - ν: Precision times mean.
 * - Λ: Precision.
 * - c: Offset.
 * - α: Variance shape.
 * - β: Variance scale.
 * - s2: Likelihood variance scale.
 *
 * Returns: the log probability density.
 */
function logpdf_linear_multivariate_normal_inverse_gamma_gaussian<Arg1,Arg2,
    Arg3,Arg4,Arg5,Arg6,Arg7,Arg8>(x:Arg1, a:Arg2, ν:Arg3, Λ:Arg4, c:Arg5,
    α:Arg6, β:Arg7, s2:Arg8) -> {
  let S <- cholinv(Λ);
  let μ <- S*ν;
  return logpdf_student_t(x, 2.0*α, dot(a, μ) + c, (β/α)*(s2 + dot(a, S*a)));
}

/*
 * Update the parameters of a normal inverse-gamma distribution with a
 * linear transformation involving a dot product, and Gaussian likelihood.
 *
 * - x: The variate.
 * - A: Scale.
 * - ν: Prior precision times mean.
 * - Λ: Prior precision.
 * - c: Offset.
 * - α: Prior variance shape.
 * - β: Prior variance scale.
 * - s2: Likelihood variance scale.
 *
 * Returns: the posterior hyperparameters `μ'`, `Λ'`, `β'`, `α'` and `β'`.
 */
function update_linear_multivariate_normal_inverse_gamma_gaussian<Arg1,
    Arg2,Arg3,Arg4,Arg5,Arg6,Arg7,Arg8>(x:Arg1, a:Arg2, ν:Arg3, Λ:Arg4,
    c:Arg5, α:Arg6, β:Arg7, s2:Arg8) -> {
  let l <- 1.0/s2;
  let y <- x - c;
  let z <- l*y;
  let Λ' <- Λ + outer(a, l*a);
  let ν' <- ν + a*z;
  let μ <- cholsolve(Λ, ν);
  let μ' <- cholsolve(Λ', ν');
  let α' <- α + 0.5;
  let β' <- β + 0.5*(y*z + dot(μ, ν) - dot(μ', ν'));
  return MultivariateNormalInverseGamma(ν', Λ', α', β');
}

/*
 * CDF of a Gaussian variate with a multivariate linear normal inverse-gamma
 * prior with linear transformation.
 *
 * - x: The variate.
 * - a: Scale.
 * - ν: Precision times mean.
 * - Λ: Precision.
 * - c: Offset.
 * - α: Variance shape.
 * - β: Variance scale.
 * - s2: Likelihood variance scale.
 *
 * Return: the cumulative probability.
 */
function cdf_linear_multivariate_normal_inverse_gamma_gaussian(x:Real,
    a:Real[_], ν:Real[_], Λ:Real[_,_], c:Real, α:Real, β:Real, s2:Real) ->
    Real {
  let S <- cholinv(Λ);
  let μ <- S*ν;
  return cdf_student_t(x, 2.0*α, dot(a, μ) + c, (β/α)*(s2 + dot(a, S*a)));
}

/*
 * Quantile of a Gaussian distribution with a multivariate linear normal
 * inverse-gamma prior with linear transformation.
 *
 * - P: The cumulative probability.
 * - a: Scale.
 * - ν: Precision times mean.
 * - Λ: Precision.
 * - c: Offset.
 * - α: Variance shape.
 * - β: Variance scale.
 * - s2: Likelihood variance scale.
 *
 * Return: the quantile.
 */
function quantile_linear_multivariate_normal_inverse_gamma_gaussian(P:Real,
    a:Real[_], ν:Real[_], Λ:Real[_,_], c:Real, α:Real, β:Real, s2:Real) ->
    Real {
  let S <- cholinv(Λ);
  let μ <- S*ν;
  return quantile_student_t(P, 2.0*α, dot(a, μ) + c, (β/α)*(s2 +
      dot(a, S*a)));
}
