/**
 * Delta distribution, representing a distribution on a discrete space with
 * all probability mass at one location.
 *
 * @param μ Location.
 */
final class DeltaDistribution<Arg>(μ:Arg) < Distribution<Integer> {
  /**
   * Location.
   */
  μ:Arg <- μ;

  override function simulate() -> Integer! {
    return global.value(μ);
  }

  override function simulateLazy() -> Integer! {
    return global.eval(μ);
  }

  override function logpdf(x:Integer!) -> Real! {
    return where(x == global.value(μ), 0.0, -inf);
  }

  override function logpdfLazy(x:Integer!) -> Real! {
    return where(x == global.eval(μ), 0.0, -inf);
  }

  override function hoist() -> Expression<Real> {
    return box(where(random() == μ, 0.0, -inf));
  }

  override function lower() -> Integer!? {
    return global.value(μ);
  }
  
  override function upper() -> Integer!? {
    return global.value(μ);
  }

  function doConstant() {
    super.constant();
    global.constant(μ);
  }

  override function isBoundedDiscrete() -> Boolean {
    return !x?;
  }

  override function getBoundedDiscrete() -> (Expression<Real[_]>, Integer!)? {
    assert isBoundedDiscrete();
    return (box(vector(1.0, 1)), global.value(μ));
  }

  override function write(buffer:Buffer) {
    buffer.set("class", "Delta");
    buffer.set("μ", global.value(μ));
  }
}

/**
 * Create delta distribution.
 */
function Delta<Arg>(μ:Arg) -> Distribution<Integer> {
  return wrap_delta(μ);
}
function wrap_delta<Arg>(μ:Arg) -> {
  return make_delta(wrap(μ));
}
function make_delta<Arg>(μ:Arg) -> {
  return construct<DeltaDistribution<Arg>>(μ);
}
