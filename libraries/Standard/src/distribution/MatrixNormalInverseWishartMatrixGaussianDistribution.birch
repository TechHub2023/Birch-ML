/**
 * Matrix Gaussian distribution with linear transformation of
 * matrix-normal-inverse-Wishart prior.
 */
class MatrixNormalInverseWishartMatrixGaussianDistribution<Arg1,Arg2,Arg3,
    Arg4,Arg5,Arg6,Arg7>(A:Arg1, M:Arg2, Σ:Arg3, Ψ:Arg4, k:Arg5, C:Arg6,
    Φ:Arg7) < MatrixTDistribution<Arg5,Expression<Real[_,_]>,
    Expression<Real[_,_]>,Arg4>(k, box(A*M + C),
    box(chol(A*triouter(Σ)*transpose(A) + triouter(Φ))), Ψ) {
  /**
   * Scale.
   */
  A:Arg1 <- A;

  /**
   * Mean.
   */
  M:Arg2 <- M;

  /**
   * Among-row covariance.
   */
  Σ:Arg3 <- Σ;

  /**
   * Offset.
   */
  C:Arg6 <- C;

  /**
   * Among-row covariance.
   */
  Φ:Arg7 <- Φ;

  override function supportsLazy() -> Boolean {
    return true;
  }

  override function update(X:Real[_,_]) -> Delay? {
    return update_matrix_normal_inverse_wishart_matrix_gaussian(X,
        value(A), value(M), value(Σ), value(super.Ψ), value(super.k),
        value(C), value(Φ), value(super.M), value(super.Σ));
  }

  override function updateLazy(X:Expression<Real[_,_]>) -> Delay? {
    return update_matrix_normal_inverse_wishart_matrix_gaussian(X,
        A, M, Σ, super.Ψ, super.k, C, Φ, super.M, super.Σ);
  }

  override function constant() {
    super.constant();
    global.constant(A);
    global.constant(M);
    global.constant(Σ);
    global.constant(C);
    global.constant(Φ);
  }
}

function MatrixNormalInverseWishartMatrixGaussian<Arg1,Arg2,Arg3,
    Arg4,Arg5,Arg6,Arg7>(A:Arg1, M:Arg2, Σ:Arg3, Ψ:Arg4, k:Arg5, C:Arg6,
    Φ:Arg7) -> Distribution<Real[_,_]> {
  return wrap_matrix_normal_inverse_wishart_matrix_gaussian(A, M,
      chol(Σ), chol(Ψ), k, C, chol(Φ));
}
function wrap_matrix_normal_inverse_wishart_matrix_gaussian<Arg1,Arg2,
    Arg3,Arg4,Arg5,Arg6,Arg7>(A:Arg1, M:Arg2, Σ:Arg3, Ψ:Arg4, k:Arg5, C:Arg6,
    Φ:Arg7) -> {
  return make_matrix_normal_inverse_wishart_matrix_gaussian(wrap(A),
      wrap(M), wrap(Σ), wrap(Ψ), wrap(k), wrap(C), wrap(Φ));
}
function make_matrix_normal_inverse_wishart_matrix_gaussian<Arg1,Arg2,
    Arg3,Arg4,Arg5,Arg6,Arg7>(A:Arg1, M:Arg2, Σ:Arg3, Ψ:Arg4, k:Arg5, C:Arg6,
    Φ:Arg7) -> {
  return construct<MatrixNormalInverseWishartMatrixGaussianDistribution<
      Arg1,Arg2,Arg3,Arg4,Arg5,Arg6,Arg7>>(A, M, Σ, Ψ, k, C, Φ);
}

/**
 * Create matrix Gaussian distribution.
 */
function MatrixGaussian<Arg1,Arg2,Arg3>(M:OpAdd<OpMul<Arg1,Random<Real[_,_]>>,
    Arg2>, Σ:Arg3, Ψ:Random<Real[_,_]>) -> Distribution<Real[_,_]> {
  M.l.r.prune();
  if M.l.r.hasMatrixNormalInverseWishart() && M.l.r == Ψ.getNext(2) {
    let (M1, Σ1, Ψ1, k) <- M.l.r.getMatrixNormalInverseWishart();
    let p <- wrap_matrix_normal_inverse_wishart_matrix_gaussian(M.l.l,
        M1, Σ1, Ψ1, k, M.r, chol(Σ));
    M.l.r.setNext(p);
    Ψ.setNext(nil);
    M.l.r.setSide(Ψ);
    Ψ.setSide(M.l.r);
    return p;
  } else {
    Ψ.prune();
    if Ψ.hasInverseWishart() {
      let (Ψ1, k) <- Ψ.getInverseWishart();
      let p <- wrap_matrix_normal_inverse_wishart(M, chol(Σ), Ψ1, k);
      Ψ.setNext(p);
      return p;
    } else {
      return wrap_matrix_gaussian(M, chol(Σ), chol(Ψ));
    }
  }
}

/**
 * Create matrix Gaussian distribution.
 */
function MatrixGaussian<Arg1,Arg2>(M:OpMul<Arg1,Random<Real[_,_]>>, Σ:Arg2,
    Ψ:Random<Real[_,_]>) -> Distribution<Real[_,_]> {
  return MatrixGaussian(M + 0.0, Σ, Ψ);
}

/**
 * Create matrix Gaussian distribution.
 */
function MatrixGaussian<Arg>(M:Random<Real[_,_]>, Σ:Arg,
    Ψ:Random<Real[_,_]>) -> Distribution<Real[_,_]> {
  return MatrixGaussian(1.0*M + 0.0, Σ, Ψ);
}

/*
 * Update the parameters of a Gaussian variate with linear transformation
 * of matrix-normal-inverse-Wishart prior.
 *
 * - X: The variate.
 * - A: Scale.
 * - M: Prior precision times mean matrix.
 * - Σ: Prior precision.
 * - Ψ: Prior variance shape.
 * - k: Prior degrees of freedom.
 * - C: Offset.
 * - Φ: Among-row covariance.
 * - N: Precomputed marginal mean.
 * - T: Precomputed marginal among-row covariance.
 *
 * Returns: the posterior hyperparameters `M'`, `Σ'`, `Ψ'` and `k'`.
 */
function update_matrix_normal_inverse_wishart_matrix_gaussian<Arg1,Arg2,Arg3,
    Arg4,Arg5,Arg6,Arg7,Arg8,Arg9,Arg10>(X:Arg1, A:Arg2, M:Arg3, Σ:Arg4,
    Ψ:Arg5, k:Arg6, C:Arg7, Φ:Arg8, N:Arg9, T:Arg10) -> {
  let n <- rows(X);
  let Σ' <- wrap(chol(cholinv(chol(cholinv(Σ) + transpose(A)*cholinv(Φ)*A))));
  let M' <- wrap(triouter(Σ')*(cholsolve(Σ, M) + transpose(A)*cholsolve(Φ, X - C)));
  let Ψ' <- chol(triouter(Ψ) + inner(trisolve(Φ, X - C)) +
      inner(trisolve(Σ, M)) - inner(trisolve(Σ', M')));
  let k' <- k + n;
  return wrap_matrix_normal_inverse_wishart(M', Σ', Ψ', k');
}
