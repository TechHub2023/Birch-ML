/**
 * Multinomial distribution.
 */
final class MultinomialDistribution<Arg1,Arg2>(n:Arg1, ρ:Arg2) <
    Distribution<Integer[_]> {
  /**
   * Number of trials.
   */
  n:Arg1 <- n;

  /**
   * Category probabilities.
   */
  ρ:Arg2 <- ρ;

  override function supportsLazy() -> Boolean {
    return false;
  }

  override function simulate() -> Integer[_] {
    return simulate_multinomial(value(n), value(ρ));
  }
  
  override function logpdf(x:Integer[_]) -> Real {
    return logpdf_multinomial(x, value(n), value(ρ));
  }

  override function write(buffer:Buffer) {
    buffer.set("class", "Multinomial");
    buffer.set("n", value(n));
    buffer.set("ρ", value(ρ));
  }
}

/**
 * Create multinomial distribution.
 */
function Multinomial<Arg1,Arg2>(n:Arg1, ρ:Arg2) -> {
  return construct<MultinomialDistribution<Arg1,Arg2>>(n, ρ);
}

/*
 * Simulate a multinomial distribution.
 *
 * - n: Number of trials.
 * - ρ: Category probabilities. These should sum to one.
 *
 * This uses an $\mathcal{O}(N)$ implementation based on:
 *
 * Bentley, J. L. and J. B. Saxe (1979). Generating sorted lists of random
 * numbers. Technical Report 2450, Carnegie Mellon University, Computer
 * Science Department.
 */
function simulate_multinomial(n:Integer, ρ:Real[_]) -> Integer[_] {
  return simulate_multinomial(n, ρ, 1.0);
}

/*
 * Simulate a multinomial distribution.
 *
 * - n: Number of trials.
 * - ρ: Unnormalized category probabilities.
 * - Z: Sum of the unnormalized category probabilities.
 *
 * This uses an $\mathcal{O}(N)$ implementation based on:
 *
 * Bentley, J. L. and J. B. Saxe (1979). Generating sorted lists of random
 * numbers. Technical Report 2450, Carnegie Mellon University, Computer
 * Science Department.
 */
function simulate_multinomial(n:Integer, ρ:Real[_], Z:Real) -> Integer[_] {
  assert length(ρ) > 0;
  assert abs(sum(ρ) - Z) < 1.0e-6;

  let D <- length(ρ);
  let R <- ρ[D];
  let lnMax <- 0.0;
  let j <- D;
  let i <- n;
  let x <- vector(0, D);

  while i > 0 {
    let u <- simulate_uniform(0.0, 1.0);
    lnMax <- lnMax + log(u)/i;
    u <- Z*exp(lnMax);
    while u < Z - R {
      j <- j - 1;
      R <- R + ρ[j];
    }
    x[j] <- x[j] + 1;
    i <- i - 1;
  }
  while j > 1 {
    j <- j - 1;
    x[j] <- 0;
  }
  return x;
}

/*
 * Observe a multinomial variate.
 *
 * - x: The variate.
 * - n: Number of trials.
 * - ρ: Category probabilities.
 *
 * Returns: the log probability mass.
 */
function logpdf_multinomial(x:Integer[_], n:Integer, ρ:Real[_]) -> Real {
  assert length(x) == length(ρ);
  let m <- 0;
  let w <- lgamma(n + 1.0);
  for i in 1..length(x) {
    assert x[i] >= 0;
    assert ρ[i] >= 0.0;
    m <- m + x[i];
    w <- w + x[i]*log(ρ[i]) - lgamma(x[i] + 1.0);
  }
  if m == n {
    return w;
  } else {
    return -inf;
  }
}
