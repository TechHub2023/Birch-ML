struct SolveFunction {  
  function compute<LeftValue,RightValue>(l:LeftValue, r:RightValue) -> {
    return solve(l, r);
  }

  function grad<Gradient,LeftValue,RightValue>(d:Gradient, l:LeftValue,
      r:RightValue) -> {
    let dl <- outer(solve(transpose(l), -d), solve(l, r));
    let dr <- solve(transpose(l), d);
    return (dl, dr);
  }
}
struct Solve<Left,Right> = Binary<SolveFunction,Left,Right>;

/**
 * Solve a system of equations.
 */
function solve<Left,Right>(l:Left, r:Right) -> {
  return box(construct<Solve<Left,Right>>(l, r));
}

/**
 * Solve a system of equations.
 */
function solve(l:Real[_,_], r:Real[_]) -> Real[_] {
  cpp{{
  return l.toEigen().householderQr().solve(r.toEigen());
  }}
}

/**
 * Solve a system of equations.
 */
function solve(l:Real[_,_], r:Real[_,_]) -> Real[_,_] {
  cpp{{
  return l.toEigen().householderQr().solve(r.toEigen());
  }}
}
