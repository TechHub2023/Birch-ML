/**
 * Boxed and joined vector of expressions.
 *
 * - Value: Element type.
 */
struct VectorJoin<Value>(x:Expression<Value>[_]) {
  /**
   * Arguments.
   */
  x:Expression<Value>[_] <- x;

  function value() -> Value[_] {
    return transform(x, \(x':Expression<Value>) -> {
          return global.value(x');
        });
  }

  function peek() -> Value[_] {
    return transform(x, \(x':Expression<Value>) -> {
          return global.peek(x');
        });
  }

  function eval() -> Value[_] {
    return transform(x, \(x':Expression<Value>) -> {
          return global.eval(x');
        });
  }

  function reval() -> Value[_] {
    return transform(x, \(x':Expression<Value>) -> {
          return global.reval(x');
        });
  }
  
  function grad(d:Value[_]) {
    for_each(x, d, \(x':Expression<Value>, d':Value) {
          global.grad(x', d');
        });
  }

  function label(gen:Integer) {
    for_each(x, \(x':Expression<Value>) { global.label(x', gen); });
  }

  function constant(gen:Integer) {
    for_each(x, \(x':Expression<Value>) { global.constant(x', gen); });
  }

  function constant() {
    for_each(x, \(x':Expression<Value>) { global.constant(x'); });
  }
}

/**
 * Convert a vector of expressions into a vector form.
 */
function join<Value>(x:Expression<Value>[_]) -> {
  return box(construct<VectorJoin<Value>>(x));
}
