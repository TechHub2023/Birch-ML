struct Outer<Left,Right>(l:Left, r:Right) < Binary<Left,Right>(l, r) {
  //
}

function value<Left,Right>(x:Outer<Left,Right>) -> {
  return outer(value(x.l), value(x.r));
}

function peek<Left,Right>(x:Outer<Left,Right>) -> {
  return outer(peek(x.l), peek(x.r));
}

function eval<Left,Right>(x:Outer<Left,Right>) -> {
  return outer(eval(x.l), eval(x.r));
}

function grad<Left,Right,Gradient>(x:Outer<Left,Right>, d:Gradient) {
  grad(x.l, d*peek(x.r));
  grad(x.r, inner(d, peek(x.l)));
}

hpp{{
namespace birch {
/**
 * Outer product.
 */
template<class Left, class Right, std::enable_if_t<is_delay<Left>::value ||
    is_delay<Right>::value,int> = 0>
Outer<Left,Right> outer(Left&& l, Right&& r) {
  return construct<Outer<Left,Right>>(std::forward<Left>(l),
      std::forward<Right>(r));
}
}
}}
