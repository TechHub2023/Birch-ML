struct Subtract<Left,Right>(l:Left, r:Right) < Binary<Left,Right>(l, r) {
  //
}

function value<Left,Right>(x:Subtract<Left,Right>) -> {
  return value(x.l) - value(x.r);
}

function peek<Left,Right>(x:Subtract<Left,Right>) -> {
  return peek(x.l) - peek(x.r);
}

function eval<Left,Right>(x:Subtract<Left,Right>) -> {
  return eval(x.l) - eval(x.r);
}

function grad<Left,Right,Gradient>(x:Subtract<Left,Right>, d:Gradient) {
  grad(x.l, d);
  grad(x.r, -d);
}

hpp{{
namespace birch {
/**
 * Subtraction.
 */
template<class Left, class Right, std::enable_if_t<is_delay<Left>::value ||
    is_delay<Right>::value,int> = 0>
Subtract<Left,Right> operator-(Left&& l, Right&& r) {
  return construct<Subtract<Left,Right>>(std::forward<Left>(l),
      std::forward<Right>(r));
}
}
}}
