struct Floor<Middle>(m:Middle) < Unary<Middle>(m) {
  //
}

function value<Middle>(x:Floor<Middle>) -> {
  return floor(value(x.m));
}

function peek<Middle>(x:Floor<Middle>) -> {
  return floor(peek(x.m));
}

function eval<Middle>(x:Floor<Middle>) -> {
  return floor(eval(x.m));
}

function grad<Middle,Gradient>(x:Floor<Middle>, d:Gradient) {
  grad(x.m, 0.0);
}

/**
 * Round to largest integer value not greater than argument.
 */
function floor<Middle>(m:Middle) -> Floor<Middle> {
  cpp{{
  static_assert(is_form<Middle>::value, "argument types not supported");
  }}
  return construct<Floor<Middle>>(m);
}

/**
 * Round to largest integer value not greater than argument.
 */
function floor(x:Real) -> Real {
  cpp {{
  return std::floor(x);
  }}
}
