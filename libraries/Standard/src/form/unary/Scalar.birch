struct Scalar<Value,Middle>(m:Middle) < Unary<Middle>(m) {
  //
}

function value<Value,Middle>(x:Scalar<Value,Middle>) -> {
  return scalar<Value>(value(x.m));
}

function peek<Value,Middle>(x:Scalar<Value,Middle>) -> {
  return scalar<Value>(peek(x.m));
}

function eval<Value,Middle>(x:Scalar<Value,Middle>) -> {
  return scalar<Value>(eval(x.m));
}

function grad<Value,Middle,Gradient>(x:Scalar<Value,Middle>, d:Gradient) -> {
  grad(x.m, grad_scalar(d, peek(x.m)));
}

/**
 * Convert to scalar.
 */
function scalar<Value,Middle>(m:Middle) -> Scalar<Value,Middle> {
  return construct<Scalar<Value,Middle>>(m);
}

/**
 * Convert to scalar.
 */
function scalar<Value,Type>(m:Type[_]) -> Value {
  assert length(m) == 1;
  return scalar<Value>(m[1]);
}

/**
 * Convert to scalar.
 */
function scalar<Value,Type>(m:Type[_,_]) -> Value {
  assert rows(m) == 1 && columns(m) == 1;
  return scalar<Value>(m[1,1]);
}

/**
 * Convert to scalar.
 */
function scalar<Value>(m:Boolean) -> Value {
  cpp{{
  return static_cast<Value>(m);
  }}
}

/**
 * Convert to scalar.
 */
function scalar<Value>(m:Integer) -> Value {
  cpp{{
  return static_cast<Value>(m);
  }}
}

/**
 * Convert to scalar.
 */
function scalar<Value>(m:Real) -> Value {
  cpp{{
  return static_cast<Value>(m);
  }}
}

function grad_scalar<Gradient,Value>(d:Gradient, m:Value) -> {
  return scalar<Value>(d);
}

function grad_scalar<Gradient,Value>(d:Gradient, m:Value[_]) -> {
  return vector<Value>(d);
}

function grad_scalar<Gradient,Value>(d:Gradient, m:Value[_,_]) -> {
  return matrix<Value>(d);
}
