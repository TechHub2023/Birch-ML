struct Sum<Middle>(m:Middle) < Unary<Middle>(m) {
  //
}

function value<Middle>(x:Sum<Middle>) -> {
  return sum(value(x.m));
}

function peek<Middle>(x:Sum<Middle>) -> {
  return sum(peek(x.m));
}

function eval<Middle>(x:Sum<Middle>) -> {
  return sum(eval(x.m));
}

function grad<Middle,Gradient>(x:Sum<Middle>, d:Gradient) {
  grad(x.m, grad_sum(d, peek(x.m)));
}

/**
 * Sum.
 */
function sum<Middle>(m:Middle) -> Sum<Middle> {
  return construct<Sum<Middle>>(m);
}

/**
 * Sum scalar.
 */
function sum(x:Integer) -> Integer {
  return x;
}

/**
 * Sum scalar.
 */
function sum(x:Real) -> Real {
  return x;
}

/**
 * Sum vector.
 */
function sum(x:Integer[_]) -> Integer {
  return reduce(x, 0, \(x:Integer, y:Integer) -> { return x + y; });
}

/**
 * Sum matrix.
 */
function sum(X:Integer[_,_]) -> Integer {
  return reduce(X, 0, \(x:Integer, y:Integer) -> { return x + y; });
}

/**
 * Sum vector.
 */
function sum(x:Real[_]) -> Real {
  cpp{{
  return numbirch::sum(1, x.length(), x.data(), x.stride());
  }}
}

/**
 * Sum matrix.
 */
function sum(X:Real[_,_]) -> Real {
  cpp{{
  return numbirch::sum(X.rows(), X.columns(), X.data(), X.stride());
  }}
}

function grad_sum<Gradient,Value>(d:Gradient, m:Value) {
  return d;
}

function grad_sum<Gradient,Value>(d:Gradient, m:Value[_]) {
  return vector(d, length(m));
}

function grad_sum<Gradient,Value>(d:Gradient, m:Value[_,_]) {
  return matrix(d, rows(m), columns(m));
}
