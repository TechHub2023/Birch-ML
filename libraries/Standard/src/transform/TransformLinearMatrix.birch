/*
 * Linear transformation of a matrix random variate, as represented by its
 * associated distribution.
 *
 * - Value: Distribution type.
 */
class TransformLinearMatrix<Value>(A:Expression<Real[_,_]>, X:Value,
    C:Expression<Real[_,_]>) {
  /**
   * Scale.
   */
  A:Expression<Real[_,_]> <- A;
  
  /**
   * Delay node.
   */
  X:Value <- X;

  /**
   * Offset.
   */
  C:Expression<Real[_,_]> <- C;
    
  function rows() -> Integer {
    return C.rows();
  }
  
  function columns() -> Integer {
    return C.columns();
  }
    
  function leftMultiply(Y:Expression<Real[_,_]>) {
    A <- Y*A;
    C <- Y*C;
  }

  function leftMultiply(y:Expression<Real>) {
    A <- y*A;
    C <- y*C;
  }

  function add(Y:Expression<Real[_,_]>) {
    C <- C + Y;
  }

  function subtract(Y:Expression<Real[_,_]>) {
    C <- C - Y;
  }

  function multiply(y:Expression<Real>) {
    A <- A*y;
    C <- C*y;
  }
  
  function divide(y:Expression<Real>) {
    A <- A/y;
    C <- C/y;
  }

  function negate() {
    A <- -A;
    C <- -C;
  }
  
  function negateAndAdd(Y:Expression<Real[_,_]>) {
    A <- -A;
    C <- Y - C;
  }
}

function TransformLinearMatrix<Value>(A:Expression<Real[_,_]>, X:Value,
    C:Expression<Real[_,_]>) -> TransformLinearMatrix<Value> {
  return construct<TransformLinearMatrix<Value>>(A, X, C);
}

function TransformLinearMatrix<Value>(A:Expression<Real[_,_]>, X:Value) ->
    TransformLinearMatrix<Value> {
  return TransformLinearMatrix<Value>(A, X,
      box(matrix<Real>(0.0, A.rows(), X.columns())));
}
