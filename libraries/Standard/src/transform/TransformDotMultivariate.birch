/*
 * Linear transformation of a matrix random variate, as represented by
 * its associated distribution, which involves a dot product.
 *
 * - Value: Distribution type.
 */
class TransformDotMultivariate<Value>(a:Expression<Real[_]>, X:Value,
    c:Expression<Real[_]>) {
  /**
   * Scale.
   */
  a:Expression<Real[_]> <- a;
  
  /**
   * Distribution.
   */
  X:Value <- X;

  /**
   * Offset.
   */
  c:Expression<Real[_]> <- c;
   
  function multiply(y:Expression<Real>) {
    a <- a*y;
    c <- c*y;
  }

  function divide(y:Expression<Real>) {
    a <- a/y;
    c <- c/y;
  }

  function add(y:Expression<Real[_]>) {
    c <- c + y;
  }

  function subtract(y:Expression<Real[_]>) {
    c <- c - y;
  }

  function negate() {
    a <- -a;
    c <- -c;
  }
  
  function negateAndAdd(y:Expression<Real[_]>) {
    a <- -a;
    c <- y - c;
  }
}

function TransformDotMultivariate<Value>(a:Expression<Real[_]>, X:Value,
    c:Expression<Real[_]>) -> TransformDotMultivariate<Value> {
  return construct<TransformDotMultivariate<Value>>(a, X, c);
}

function TransformDotMultivariate<Value>(a:Expression<Real[_]>, X:Value) ->
    TransformDotMultivariate<Value> {
  return TransformDotMultivariate<Value>(a, X, box(vector(0.0, X.columns())));
}
