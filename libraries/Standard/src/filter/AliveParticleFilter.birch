/**
 * Alive particle filter. When propagating and weighting particles, the
 * alive particle filter continues until it obtains $N$ particles with
 * non-zero weight, rather than $N$ particles in total, as with the standard
 * particle filter.
 *
 * ```mermaid
 * classDiagram
 *    ParticleFilter <|-- AliveParticleFilter
 *    link ParticleFilter "../ParticleFilter/"
 *    link AliveParticleFilter "../AliveParticleFilter/"
 * ```
 */
class AliveParticleFilter < ParticleFilter {
  /**
   * Number of propagations for each particle, along with the extra particle
   * to be discarded.
   */
  p:Integer[_];

  override function simulate(t:Integer, input:Buffer) {
    a <- resample_systematic(w);  // normal resample intialize first attempt
    p <- vector(0, nparticles);

    let x0 <- x;
    let w0 <- w;
    parallel for n in 1..nparticles {
      do {
        x[n] <- global.copy(x0[a[n]]);
        p[n] <- p[n] + 1;

        let m <- x[n].m;
        let h <- x[n].h;
        with h {
          h.w <- 0.0;
          x[n].m.read(t, input);
          x[n].m.simulate(t);
          w[n] <- h.w;
        }
        if !isfinite(w[n]) {
          a[n] <- global.ancestor(w0);  // try again
        }
      } while !isfinite(w[n]);
    }
    collect();

    /* discard a random particle to debias (random, rather than last, as
     * particles are not exchangeable for all resamplers) */
    w[simulate_uniform_int(1, nparticles)] <- -inf;
  }
  
  override function resample(t:Integer) {
    /* proceed as though no resample, so just normalize weights, and let
     * propagate handle the rest */
    a <- iota(1, nparticles);
    w <- w - vector(lsum - log(nparticles), nparticles);
  }
  
  override function reduce() {
    super.reduce();
    npropagations <- sum(p);
  }
}
