/**
 * Event handler.
 *
 * - delaySampling: Enable delayed sampling.
 * - delayExpressions: Enable delayed expressions.
 *
 * Events are triggered as code executes. Each event is handled via a call to
 * the currently-installed event handler, which is an object of type Handler.
 *
 * The events are:
 *
 * | Code       | Triggers                          |
 * | -----------| ----------------------------------|
 * | `x ~ p`    | [handleAssume](#handleassume)     |
 * | `x <~ p`   | [handleSimulate](#handlesimulate) |
 * | `x ~> p`   | [handleObserve](#handleobserve)   |
 * | `factor w` | [handleFactor](#handlefactor)     |
 * 
 * After `x ~ p`, certain operations on `x` may further trigger events:
 *
 * | Code                   | Triggers                                    |
 * | ---------------------- | --------------------------------------------|
 * | `x.value()`/`x.eval()` | [handleDelaySimulate](#handledelaysimulate) |
 * | `x <- y`               | [handleDelayObserve](#handledelayobserve)   |
 *
 * Finally, any manipulations of a Random `x` that is an interior node on the
 * delayed sampling $M$-path will require pruning of the path, which will
 * trigger [handleDelayPrune](handledelayprune).
 */
final class Handler(delaySampling:Boolean, delayExpressions:Boolean) {
  /**
   * Log-posterior, if delayed expressions are enabled.
   */
  π:Expression<Real>?;

  /*
   * Arguments.
   */
  b1:Tape<Random<Boolean>>?;
  i1:Tape<Random<Integer>>?;
  i2:Tape<Random<Integer[_]>>?;
  r1:Tape<Random<Real>>?;
  r2:Tape<Random<Real[_]>>?;
  r3:Tape<Random<Real[_,_]>>?;

  /**
   * Number of arguments.
   */
  n:Integer <- 0;

  /**
   * Accumulated weight.
   */
  w:Real! <- 0.0;

  /**
   * Is delayed sampling enabled?
   */
  delaySampling:Boolean <- delaySampling;

  /**
   * Are delayed expressions enabled?
   */
  delayExpressions:Boolean <- delayExpressions;

  /**
   * Handle an assume event.
   *
   * - x: Random.
   * - p: Distribution.
   *
   * Returns: `x`.
   */
  function handleAssume<Value>(x:Random<Value>, p:Distribution<Value>) ->
      Random<Value> {
    if x.hasValue() {
      handleObserve(x, p);
    } else if !delaySampling {
      handleSimulate(x, p);
    } else {
      x.assume(p);
    }
    return x;
  }

  /**
   * Handle a simulate event.
   *
   * - x: Random.
   * - p: Distribution.
   *
   * Returns: `x`.
   */
  function handleSimulate<Value>(x:Random<Value>, p:Distribution<Value>) ->
      Random<Value> {
    x.assume(p);
    handleDelaySimulate(x);
    return x;
  }

  /**
   * Handle a simulate event.
   *
   * - p: Distribution.
   *
   * Returns: `x`.
   */
  function handleSimulate<Value>(p:Distribution<Value>) -> Value {
    if delayExpressions && p.supportsLazy() {
      let x <- p.simulateLazy()!;
      p.graft(x);
      return x;
    } else {
      let x <- p.simulate();
      p.graft(x);
      return x;
    }
  }

  /**
   * Handle an observe event.
   *
   * - x: Random.
   * - p: Distribution.
   *
   * Returns: `x`.
   */
  function handleObserve<Value>(x:Random<Value>, p:Distribution<Value>) ->
      Random<Value> {
    x.assume(p);
    handleDelayObserve(x);
    return x;
  }

  /**
   * Handle an observe event.
   *
   * - x: Random.
   * - p: Distribution.
   *
   * Returns: `x`.
   */
  function handleObserve<Value>(x:Value, p:Distribution<Value>) -> Value {
    if delayExpressions && p.supportsLazy() {
      handleFactor(p.logpdfLazy(box(x))!);
    } else {
      handleFactor(p.logpdf(x));
    }
    p.graft(x);
    return x;
  }

  /**
   * Handle a factor event.
   *
   * - w: Log-weight.
   *
   * A factor event is triggered by the `factor` statement.
   */
  function handleFactor<Arg>(w:Arg) {
    if delayExpressions {
      this.w <- this.w + global.peek(w);
      if π? {
        π <- box(π! + w);
      } else {
        π <- box(w);
      }
    } else {
      this.w <- this.w + global.value(w);
    }
  }

  /**
   * Handle a delayed simulation event.
   *
   * - x: Random.
   */
  function handleDelaySimulate<Value>(x:Random<Value>) {
    let p <- x.getDistribution();
    if delayExpressions && p.supportsLazy() {
      x.move(p.simulateLazy()!);
      arg(x);
    } else {
      x.move(p.simulate());
    }
    x.p <- nil;
  }

  /**
   * Handle a delayed observation event.
   *
   * - x: Random.
   */
  function handleDelayObserve<Value>(x:Random<Value>) {
    let p <- x.getDistribution();
    if delayExpressions && p.supportsLazy() {
      handleFactor(p.logpdfLazy(x)!);
    } else {
      handleFactor(p.logpdf(x.value()));
    }
    x.p <- nil;
  }

  /**
   * Handle a prune event.
   *
   * - p: Distribution.
   * - x: Child of `p` on $M$-path.
   *
   * Returns: Updated distribution.
   */
  function handleDelayPrune<Value>(p:Distribution<Value>, x:Random<Value>) ->
      Delay {
    if delayExpressions && p.supportsLazy() {
      return p.updateLazy(x)!;
    } else {
      return p.update(x.value())!;
    }
  }

  /**
   * Handle a prune event.
   *
   * - p: Distribution.
   * - x: Child of `p` on $M$-path.
   *
   * Returns: Updated distribution.
   */
  function handleDelayPrune<Value>(p:Distribution<Value>, x:Value) -> Delay {
    if delayExpressions && p.supportsLazy() {
      return p.updateLazy(box(x))!;
    } else {
      return p.update(x)!;
    }
  }

  /**
   * Evaluate the log-posterior.
   */
  function peek() -> Real! {
    if π? {
      return π!.peek();
    } else {
      return 0.0;
    }
  }

  /**
   * Get value of real arguments.
   */
  function args() -> Real[_] {
    /* determine size */
    n <- 0;
    if r1? {
      n <- n + argSizeReverse(r1!);
    }
    if r2? {
      n <- n + argSizeReverse(r2!);
    }
    if r3? {
      n <- n + argSizeReverse(r3!);
    }

    /* get argument values */
    x:Real[n];
    let i <- 1;
    if r1? {
      let iter1 <- r1!.walk();
      while iter1.hasNext() {
        let v <- iter1.next();
        x[i] <- v.peek();
        i <- i + 1;
      }
    }
    if r2? {
      let iter2 <- r2!.walk();
      while iter2.hasNext() {
        let v <- iter2.next();
        let m <- i + v.size() - 1;
        x[i..m] <- v.peek();
        i <- m + 1;
      }
    }
    if r3? {
      let iter3 <- r3!.walk();
      while iter3.hasNext() {
        let v <- iter3.next();
        let m <- i + v.size() - 1;
        x[i..m] <- vec(v.peek());
        i <- m + 1;
      }
    }
    assert i == n + 1;
    return x;
  }

  /**
   * Compute the gradient.
   */
  function grad() -> Real[_] {
    d:Real[n];
    if π? {
      /* compute gradient */
      π!.grad(1.0);

      /* get argument gradients */
      let i <- 1;
      if r1? {
        let iter1 <- r1!.walk();
        while iter1.hasNext() {
          let v <- iter1.next();
          assert v.d?;
          d[i] <- v.d!;
          i <- i + 1;
        }
      }
      if r2? {
        let iter2 <- r2!.walk();
        while iter2.hasNext() {
          let v <- iter2.next();
          let j <- i + v.size() - 1;
          assert v.d?;
          d[i..j] <- v.d!;
          i <- j + 1;
        }
      }
      if r3? {
        let iter3 <- r3!.walk();
        while iter3.hasNext() {
          let v <- iter3.next();
          let j <- i + v.size() - 1;
          assert v.d?;
          d[i..j] <- vec(v.d!);
          i <- j + 1;
        }
      }
      assert i == n + 1;
    }
    return d;
  }

  /**
   * Move the expression.
   */
  function move(x:Real[_]) -> Real! {
    if π? {
      /* set argument values */
      let i <- 1;
      if r1? {
        let iter1 <- r1!.walk();
        while iter1.hasNext() {
          let v <- iter1.next();
          v.move(x[i]);
          i <- i + 1;
        }
      }
      if r2? {
        let iter2 <- r2!.walk();
        while iter2.hasNext() {
          let v <- iter2.next();
          let j <- i + v.size() - 1;
          v.move(x[i..j]);
          i <- j + 1;
        }
      }
      if r3? {
        let iter3 <- r3!.walk();
        while iter3.hasNext() {
          let v <- iter3.next();
          let j <- i + v.size() - 1;
          v.move(mat(x[i..j], v.columns()));
          i <- j + 1;
        }
      }
      assert i == n + 1;

      /* re-evaluate */
      return π!.eval();
    } else {
      return 0.0;
    }
  }

  /**
   * Label generations.
   *
   * See also: [Expression](../Expression/)
   */
  function label(gen:Integer) {
    if π? {
      π!.label(gen);
    }
  }

  /**
   * Prune generations.
   *
   * See also: [Expression](../Expression/)
   */
  function constant(gen:Integer) {
    if π? {
      π!.constant(gen);
    }
  }

  function arg(x:Random<Boolean>) {
    if !b1? {
      b1 <- construct<Tape<Random<Boolean>>>();
    }
    b1!.pushBack(x);
    let p <- x.getDistribution().logpdfLazy(x);
    if p? {
      if π? {
        π <- box(π! + p!);
      } else {
        π <- box(p!);
      }
    }
  }

  function arg(x:Random<Integer>) {
    if !i1? {
      i1 <- construct<Tape<Random<Integer>>>();
    }
    i1!.pushBack(x);
    let p <- x.getDistribution().logpdfLazy(x);
    if p? {
      if π? {
        π <- box(π! + p!);
      } else {
        π <- box(p!);
      }
    }
  }

  function arg(x:Random<Integer[_]>) {
    if !i2? {
      i2 <- construct<Tape<Random<Integer[_]>>>();
    }
    i2!.pushBack(x);
    let p <- x.getDistribution().logpdfLazy(x);
    if p? {
      if π? {
        π <- box(π! + p!);
      } else {
        π <- box(p!);
      }
    }
  }

  function arg(x:Random<Real>) {
    if !r1? {
      r1 <- construct<Tape<Random<Real>>>();
    }
    r1!.pushBack(x);
    let p <- x.getDistribution().logpdfLazy(x);
    if p? {
      if π? {
        π <- box(π! + p!);
      } else {
        π <- box(p!);
      }
    }
  }

  function arg(x:Random<Real[_]>) {
    if !r2? {
      r2 <- construct<Tape<Random<Real[_]>>>();
    }
    r2!.pushBack(x);
    let p <- x.getDistribution().logpdfLazy(x);
    if p? {
      if π? {
        π <- box(π! + p!);
      } else {
        π <- box(p!);
      }
    }
  }

  function arg(x:Random<Real[_,_]>) {
    if !r3? {
      r3 <- construct<Tape<Random<Real[_,_]>>>();
    }
    r3!.pushBack(x);
    let p <- x.getDistribution().logpdfLazy(x);
    if p? {
      if π? {
        π <- box(π! + p!);
      } else {
        π <- box(p!);
      }
    }
  }
}

/*
 * Determine the total size of all arguments in a [Tape](../Tape/),
 * simultaneously removing all constants.
 *
 * - o: The tape.
 */
function argSize<Value>(o:Tape<Random<Value>>) -> Integer {
  let n <- 0;
  let i <- 1;
  while i <= o.size() {
    if !o[i].isConstant() {
      n <- n + o[i].size();
      i <- i + 1;
    } else {
      o.erase(i);
    }
  }
  return n;
}

/*
 * As `argSize()`, but traverses the `Tape` in reverse, which may be more
 * efficient if its current position is closer to the back than the front.
 */
function argSizeReverse<Value>(o:Tape<Random<Value>>) ->
    Integer {
  let n <- 0;
  let i <- o.size();
  while i >= 1 {
    if !o[i].isConstant() {
      n <- n + o[i].size();
    } else {
      o.erase(i);
    }
    i <- i - 1;
  }
  return n;
}
