/**
 * Single object model.
 *
 * - s: The starting time of the track.
 * - θ: The global parameters.
 */
final class Track(s:Integer, θ:Global) < Model {
  /**
   * Starting time of this track.
   */
  s:Integer <- s;

  /**
   * Parameters.
   */
  θ:Global <- θ;

  /**
   * Hidden states.
   */
  x:Tape<Random<Real[_]>>;

  /**
   * Observations.
   */
  y:Tape<Random<Real[_]>>;
  
  override function simulate(t:Integer) {
    if t == 1 {
      /* start */
      let μ <- vector(0.0, rows(θ.A));
      μ[1] <~ Uniform(θ.l, θ.u);
      μ[2] <~ Uniform(θ.l, θ.u);
      x[t] ~ MultivariateGaussian(μ, θ.M);
    } else {
      /* step */
      x[t] ~ MultivariateGaussian(θ.A*x[t - 1], θ.Q);
    }

    let d <~ Bernoulli(θ.d);  // is the track detected?
    if d {
      y[t] ~ MultivariateGaussian(θ.B*x[t], θ.R);
    }
  }
  
  override function write(buffer:Buffer) {
    buffer.set("s", s);
    buffer.set("x", x);
    buffer.set("y", y);
  }
}
