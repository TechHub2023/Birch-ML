class EcologyModel < Model {
  /*
   * Parameters.
   */
  b:Random<Real[_]>;
  c:Random<Real>;
  q:Random<Real>;
  r:Random<Real>;

  /**
   * Log-count of the latent population.
   */
  x:Tape<Random<Real>>;

  /**
   * Observed population.
   */
  y:Tape<Random<Real>>;

  function simulate() {
    q ~ InverseGamma(2.5, 2.5);
    r ~ InverseGamma(2.5, 2.5);
    b ~ Gaussian(vector(1.0, 2), identity(2), q);
    c ~ Gaussian(1.0, 2.0);
  }

  function simulate(t:Integer) {
    if t == 1 {
      x[t] ~ Gaussian(3.5, 0.1);
    } else {
      l:Expression<Real> <- box(1.0);
      r:Expression<Real> <- exp(x[t - 1]*c);
      let a <- join([l, r]);
      x[t] ~ Gaussian(dot(a, b) + x[t - 1], q);
    }
    y[t] ~ Gaussian(exp(x[t]), r);
  }

  function read(buffer:Buffer) {
    b <-? buffer.get<Real[_]>("b");
    c <-? buffer.get<Real>("c");
    q <-? buffer.get<Real>("q");
    r <-? buffer.get<Real>("r");
  }

  function read(t:Integer, buffer:Buffer) {
    y[t] <-? buffer.get<Real>();
  }

  function write(buffer:Buffer) {
    buffer.set("b", b);
    buffer.set("c", c);
    buffer.set("q", q);
    buffer.set("r", r);
  }

  function write(t:Integer, buffer:Buffer) {
    buffer.set(x[t]);
  }
}
