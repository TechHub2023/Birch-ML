class EcologyModel < Model {
  /*
   * Parameters.
   */
  b:Random<Real[_]>;
  c:Random<Real>;
  q:Random<Real>;
  r:Random<Real>;

  /**
   * Log-count of the latent population.
   */
  x:Tape<Random<Real>>;

  /**
   * Observed population.
   */
  y:Tape<Random<Real>>;

  override function simulate() {
    q ~ InverseGamma(2.5, 2.5);
    r ~ InverseGamma(2.5, 2.5);
    b ~ MultivariateGaussian(vector(1.0, 2), identity(2)*q);
    c ~ Gaussian(1.0, 2.0);
  }

  override function simulate(t:Integer) {
    if t == 1 {
      x[t] ~ Gaussian(3.5, 0.1);
    } else {
      /* creating `a` is the trickiest part of the model, but it preserves the
       * ability to marginalize out all parameters but `c`, and even to make
       * gradient-based moves on `c`: `box()` converts each `Real` to an
       * `Expression<Real>`, the square brackets combine the two
       * `Expression<Real>` to a single `Expression<Real>[_]`, then `join()`
       * converts the `Expression<Real>[_]` to an `Expression<Real[_]>` */ 
      let a <- join([box(1.0), box(exp(x[t - 1]*c))]);

      /* with `a` so constructed, the mean of the next line looks like a
       * linear transformation of `b`, itself normal-inverse-gamma distributed
       * with `q`, establishing a conjugacy relationship */
      x[t] ~ Gaussian(dot(a, b) + x[t - 1], q);
    }
    y[t] ~ Gaussian(exp(x[t]), r);
  }

  override function read(buffer:Buffer) {
    b <-? buffer.get<Real[_]>("b");
    c <-? buffer.get<Real>("c");
    q <-? buffer.get<Real>("q");
    r <-? buffer.get<Real>("r");
  }

  override function read(t:Integer, buffer:Buffer) {
    y[t] <-? buffer.get<Real>();
  }

  override function write(buffer:Buffer) {
    buffer.set("b", b);
    buffer.set("c", c);
    buffer.set("q", q);
    buffer.set("r", r);
  }

  override function write(t:Integer, buffer:Buffer) {
    buffer.set(x[t]);
  }
}
